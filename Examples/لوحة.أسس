اشمل "مـحا"؛
مـحا.اشمل_ملف("Alusus/WebPlatform"، "مـنصة_ويب.أسس")؛
مـحا.اشمل_ملف("Alusus/Http"، "بـننف.أسس")؛
مـحا.اشمل_ملف("Alusus/Json"، "جـيسون.أسس")؛
اشمل "مـتم/سندات"؛
اشمل "مـتم/مـصفوفة"؛
اشمل "مـتم/نـص"؛
اشمل "مـتم/طـرفية"؛

استخدم مـتم؛
استخدم مـنصة_ويب؛

//=============================================================================
// واجهة المستخدم

عرف خط_الكتابة: "30px Arial"؛ 
عرف لون_اصلي: لـون("8e50ef")؛

دالة أنشئ_الترويسة():سـندنا[Widget]{
    حدد_ميزة_عنصر("body"، "style"، "padding: 0; margin: 0;")؛

    أرجع صـندوق.أنشى({}).{
        العرض = نـص("100%")؛
        الطول = نـص("40pt")؛
        الحشوة = ربـاعية(5، 0)؛
        لون_الخلفية = لون_اصلي؛
        لون_الإطار = لون_اصلي؛
        الإطار = ربـاعية(1.5)؛

        أضف_فروع({
            عـنوان.أنشئ(نـص("أمثلة منصة ويب الأسس - لوحة رسم")).{
                لون_الخط = لـون("fff")؛
                حجم_الخط = 21.0؛
                العرض = نـص("100%")؛
                الطول = نـص("100%")؛
                لون_الخلفية = لون_اصلي؛
            }
        })؛
    }~مثل[سـندنا[Widget]]؛
}

@منفذ_مرئي["/"، "مثال منصة ويب - لوحة رسم"]
دالة رئيسي {  
    عرف لوحة:سـندنا[لـوحة] = لـوحة.أنشئ ()؛
    حدد_المشهد(صـندوق.أنشى({ أنشئ_الترويسة()، لوحة }))؛

    ارسم_خط (لوحة.معرف_مورد، 100،180، 0،0 )؛
    ارسم_دائرة(لوحة.معرف_مورد، 100،180، 90،0 )؛
    عرف نقاط: مـصفوفة [صحيح] ({ 50، 50، 100، 50، 100، 100، 50، 100، 25، 75 })؛
    حدد_نمط_الملء (لوحة.معرف_مورد،"black"، "blue"، 0، 0، 100، 100)؛
     ارسم_مضلع (لوحة.معرف_مورد، 5، نقاط.صوان)؛
    ارسم_كتابة(لوحة.معرف_مورد، "اهلا"، خط_الكتابة، 50، 50)؛
    نفذ_حلقة_معالجة_الأحداث()؛
}


طـرفية.اطبع("تشغيل الخادم على المنفذ 8010...\nURL: http://localhost:8010/\n")؛
شغل_الخادم ({ "listening_ports"، "8010"، "static_file_max_age"، "0" })؛

