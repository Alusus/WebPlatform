اشمل "مـحا"؛
مـحا.اشمل_ملف("Alusus/WebPlatform"، "مـنصة_ويب.أسس")؛
مـحا.اشمل_ملف("Alusus/Http"، "بـننف.أسس")؛
مـحا.اشمل_ملف("Alusus/Json"، "جـيسون.أسس")؛
مـحا.اشمل_ملف("Alusus/Promises"، "مـؤجلات.أسس")؛
اشمل "مـتم/سندات"؛
اشمل "مـتم/مـصفوفة"؛
اشمل "مـتم/نـص"؛
اشمل "مـتم/طـرفية"؛
اشمل "مـتم/ريـاضيات"؛

استخدم مـتم؛
استخدم مـنصة_ويب؛

//=============================================================================
// الخادم

@مسار_موارد عرف مسار_الموارد: "Assets/"؛

//=============================================================================
// واجهة المستخدم

عرف _خط_الكتابة_: "30px AlususMono"؛
عرف _لون_اصلي_: لـون("8e50ef")؛

دالة أنشئ_الترويسة():سـندنا[Widget]{
    أرجع صـندوق({}).{
        الطراز.{
            العرض = "100%"؛
            الطول = "40pt"؛
            الحشوة = ربـاعية(5، 0)؛
            الخلفية = _لون_اصلي_؛
            لون_الإطار = _لون_اصلي_؛
            سمك_الإطار = ربـاعية(1.5)؛
            طراز_الإطار = "solid"؛
            الإظهار = "flex"؛
            النسق = "column"؛
        }؛
        أضف_فروع({
            كـتابة(نـص("أمثلة منصة ويب الأسس - مرسم")).{ الطراز.{
                لون_الخط = لـون("fff")؛
                حجم_الخط = 21.0؛
                العرض = "100%"؛
                الطول = "100%"؛
                الخلفية = _لون_اصلي_؛
                الهامش = ربـاعية(5، 15)؛
            } }
        })؛
    }؛
}

صنف مـوقع {
    عرف س: صحيح؛
    عرف ص: صحيح؛
}

@منفذ_مرئي["/"، "مثال منصة ويب - مرسم"]
دالة رئيسي {
    // حمل الموارد من الخادم
    عرف صورة: مـورد_صورة؛
    عرف موسيقى: مـورد_صوت؛
    عرف صوت: مـورد_صوت؛
    انتظر(مـؤجلات.مـؤجلة[صحيح].الكل({
        صورة.حمل("Assets/logo.svg"),
        حمل_خط("AlususMono", "Assets/AlususMono.otf"),
        موسيقى.حمل("Assets/music.mp3"),
        صوت.حمل("Assets/sound.mp3")
    }).تجاهل_النتيجة())؛

    عرف موقع: مـوقع؛
    موقع.س = 200؛
    موقع.ص = 150؛
    عرف تغير: مـوقع؛
    تغير.س = تغير.ص = 0؛
    عرف مضغوط: ثنائي = 0؛
    عرف المؤشر_محتكر: ثنائي = 0؛
    عرف ملء_الشاشة: ثـنائي = 0؛
    عرف عدد_المقابض: صحيح = 0؛

    // إنشاء المشهد
    عرف مرسم: سـندنا[مـرسم]؛
    نـافذة.النموذج.الطراز.{
        الحشوة = "0"؛
        الهامش = "0"؛
    }؛
    نـافذة.النموذج.حدد_المشهد(صـندوق({}).{
        الطراز.{
            المحاذاة = "center"؛
            الإظهار = "flex"؛
            النسق = "column"؛
            الاتجاه = "rtl"؛
        }؛
        أضف_فروع({
            أنشئ_الترويسة(),
            صـندوق({}).{
                الطراز.الإظهار = "flex"؛
                الطراز.النسق = "row"؛
                أضف_فروع({
                    الـزر(نـص("شغل الموسيقى")).{
                        الطراز.{
                            الطول = "30pt"؛
                            العرض = "120pt"؛
                            الخلفية = لـون(200, 200, 200)؛
                            حجم_الخط = 16.0؛
                        }؛
                        عند_الضغط.اربط(مغلفة (موسيقى: كسند)&(ودجة: سند[ودجـة], حمولة: سند[صحيح]) {
                            موسيقى.شغل(1)؛
                        })؛
                    },
                    الـزر(نـص("أوقف الموسيقى")).{
                        الطراز.{
                            الطول = "30pt"؛
                            العرض = "120pt"؛
                            الخلفية = لـون(200, 200, 200)؛
                            حجم_الخط = 16.0؛
                        }؛
                        عند_الضغط.اربط(مغلفة (موسيقى: كسند)&(ودجة: سند[ودجـة], حمولة: سند[صحيح]) {
                            موسيقى.أوقف()؛
                        })؛
                    },
                    الـزر(نـص("شغل الصوت")).{
                        الطراز.{
                            الطول = "30pt"؛
                            العرض = "120pt"؛
                            الخلفية = لـون(200, 200, 200)؛
                            حجم_الخط = 16.0؛
                        }؛
                        عند_الضغط.اربط(مغلفة (صوت: كسند)&(ودجة: سند[ودجـة], حمولة: سند[صحيح]) {
                            صوت.شغل(0)؛
                        })؛
                    },
                    الـزر(نـص("شغل الصوت بتكرار")).{
                        الطراز.{
                            الطول = "30pt"؛
                            العرض = "150pt"؛
                            الخلفية = لـون(200, 200, 200)؛
                            حجم_الخط = 16.0؛
                        }؛
                        عند_الضغط.اربط(مغلفة (صوت: كسند)&(ودجة: سند[ودجـة], حمولة: سند[صحيح]) {
                            صوت.شغل(1)؛
                        })؛
                    },
                    الـزر(نـص("أوقف الصوت")).{
                        الطراز.{
                            الطول = "30pt"؛
                            العرض = "120pt"؛
                            الخلفية = لـون(200, 200, 200)؛
                            حجم_الخط = 16.0؛
                        }؛
                        عند_الضغط.اربط(مغلفة (صوت: كسند)&(ودجة: سند[ودجـة], حمولة: سند[صحيح]) {
                            صوت.أوقف()؛
                        })؛
                    }
                })؛
            }
            مـرسم().{
                مرسم = هذا؛
                عرض_الصورة = نـص("1280px")؛
                طول_الصورة = نـص("800px")؛
                الطراز.{
                    العرض = "100%"؛
                    الطول = "100%"؛
                    أقصى_عرض = "1280px"؛
                    أقصى_طول = "800px"؛
                    سمك_الإطار = ربـاعية(2)؛
                    طراز_الإطار = "solid"؛
                    لون_الإطار = لـون(0, 0, 0)؛
                    الخلفية = لـون(220, 220, 220)؛
                }؛
                عرف أبعاد: أبـعاد(1، 1)؛
                عند_تحريك_المؤشر.اربط(
                    مغلفة (
                        موقع: كسند، أبعاد: كسند، المؤشر_محتكر: كسند
                    )&(
                        ودجة: سند[ودجـة]، حمولة: سند[حـمولة_تحريك_المؤشر]
                    ) {
                        إذا المؤشر_محتكر {
                            موقع.س += حمولة.إزاحة_س؛
                            موقع.ص += حمولة.إزاحة_ص؛
                        } وإلا {
                            موقع.س = حمولة.موقع_س * 1280 ÷ أبعاد.عرض؛
                            موقع.ص = حمولة.موقع_ص * 800 ÷ أبعاد.طول؛
                        }
                    }
                )؛
                عند_دخول_المؤشر.اربط(مغلفة (ودجة: سند[ودجـة]، حمولة: سند[صحيح]) {
                    هذا.الطراز.لون_الإطار = لـون(0، 0، 0)؛
                })؛
                عند_خروج_المؤشر.اربط(مغلفة (ودجة: سند[ودجـة]، حمولة: سند[صحيح]) {
                    هذا.الطراز.لون_الإطار = لـون(255، 0، 0)؛
                })؛
                عند_بدء_النقرة.اربط(مغلفة (مضغوط: كسند)&(ودجة: سند[ودجـة]، حمولة: سند[صحيح]) {
                    إذا حمولة == 0 مضغوط = 1؛
                })؛
                عند_انتهاء_النقرة.اربط(مغلفة (مضغوط: كسند)&(ودجة: سند[ودجـة]، حمولة: سند[صحيح]) {
                    إذا حمولة == 0 مضغوط = 0؛
                })؛
                عند_تغير_الأبعاد.اربط(مغلفة (أبعاد: كسند)&(ودجة: سند[ودجـة]، حمولة: سند[صحيح]) {
                    أبعاد = هذا.هات_الأبعاد()؛
                })؛
            }
        })؛
    })؛

    نـافذة.النموذج.{
        المفاتيح_المكبوتة = "ArrowUp,ArrowDown,ArrowLeft,ArrowRight"؛
        عند_بدء_الكبسة.اربط(مغلفة (
            تغير: كسند، المؤشر_محتكر: كسند، ملء_الشاشة: كسند
        )&(
            نافذة: سند[نـافذة]، حمولة: سند[نـص]
        ) {
            إذا حمولة == "ArrowUp" تغير.ص = -5
            وإلا إذا حمولة == "ArrowDown" تغير.ص = 5
            وإلا إذا حمولة == "ArrowRight" تغير.س = 5
            وإلا إذا حمولة == "ArrowLeft" تغير.س = -5
            وإلا إذا حمولة == "Enter" {
                إذا ملء_الشاشة ألغ_ملء_الشاشة()
                وإلا مرسم.التمس_ملء_الشاشة()؛
            }
            وإلا إذا حمولة == "Space" {
                إذا المؤشر_محتكر ألغ_احتكار_المؤشر()
                وإلا مرسم.التمس_احتكار_المؤشر()؛
            }
        })؛
        عند_انتهاء_الكبسة.اربط(مغلفة (تغير: كسند)&(نافذة: سند[نـافذة]، حمولة: سند[نـص]) {
            إذا حمولة == "ArrowUp" تغير.ص = 0
            وإلا إذا حمولة == "ArrowDown" تغير.ص = 0
            وإلا إذا حمولة == "ArrowRight" تغير.س = 0
            وإلا إذا حمولة == "ArrowLeft" تغير.س = 0؛
        })؛
        عند_تغير_احتكار_المؤشر.اربط(مغلفة (المؤشر_محتكر: كسند)&(نافذة: سند[نـافذة]، حمولة: سند[ثـنائي]) {
            المؤشر_محتكر = حمولة؛
        })؛
        عند_تغير_ملء_الشاشة.اربط(مغلفة (ملء_الشاشة: كسند)&(نافذة: سند[نـافذة]، حمولة: سند[ثـنائي]) {
            ملء_الشاشة = حمولة؛
            إذا حمولة مرسم.التمس_احتكار_المؤشر()
            وإلا ألغ_احتكار_المؤشر()؛
        })؛
        عند_ربط_مقبض.اربط(مغلفة (عدد_المقابض: كسند)&(نافذة: سند[نـافذة]، حمولة: سند[نـص]) {
            عدد_المقابض = هات_عدد_المقابض()؛
        })؛
        عند_فصل_مقبض.اربط(مغلفة (عدد_المقابض: كسند)&(نافذة: سند[نـافذة]، حمولة: سند[نـص]) {
            عدد_المقابض = هات_عدد_المقابض()؛
        })؛
    }؛

    // أنشئ صورة ديناميكيًا
    عرف صورة_ديناميكية: مـورد_صورة؛
    عرف مورد_مرسم: مـورد_مرسم(100, 100)؛
    مورد_مرسم.حدد_عرض_القلم(5)؛
    مورد_مرسم.حدد_نمط_القلم("#ff5555")؛
    مورد_مرسم.ارسم_خط(0, 0, 100, 100)؛
    مورد_مرسم.ارسم_خط(100, 0, 0, 100)؛
    عرف نقاط: مـصفوفة[صحيح]({ 25, 25, 75, 25, 75, 75, 25, 75 })؛
    مورد_مرسم.ارسم_مضلع(4, نقاط.صوان, 0)؛
    صورة_ديناميكية.هيئ_من_مرسم(مورد_مرسم)؛

    // حلقة التحريك
    عرف س: صحيح = -80؛
    ابدأ_المؤقت_المتكرر(8000, مغلفة (
        س: كسند, صورة: كسند, صورة_ديناميكية: كسند، موقع: كسند، تغير: كسند، مضغوط: كسند، عدد_المقابض: كسند
    ) & (جسون: جـيسون) {
        مرسم.فرغ()؛
        مرسم.ارسم_خط(0,0, 100,100)؛
        مرسم.ارسم_دائرة(100,180, 90,0 )؛
        عرف نقاط: مـصفوفة [صحيح]({ 50, 50, 100, 50, 100, 100, 50, 100, 25, 75 })؛
        مرسم.حدد_نمط_الملء("black", "blue", 0, 0, 100, 100)؛
        مرسم.ارسم_مضلع(5, نقاط.صوان, 1)؛
        عرف _كتابة_: "الأسس"؛
        مرسم.ارسم_كتابة(_كتابة_, _خط_الكتابة_, 550, 150)؛

        عرف حجم: صحيح = ريـاضيات.جا(3.14 * س / 320.0) * 40 + 100؛
        عرف ص1: صحيح = ريـاضيات.جا(3.14 * س / 320.0) * 100 + 400 - حجم / 2؛
        عرف ص2: صحيح = ريـاضيات.جتا(3.14 * س / 320.0) * 100 + 400 - حجم / 2؛
        عرف شفافية: عائم = ريـاضيات.جا(3.14 * س / 80.0) / 4 + 0.75؛
        مرسم.ارسم_صورة(صورة, س, ص1, حجم, حجم, شفافية)؛
        مرسم.ارسم_صورة(صورة_ديناميكية, س, ص2, حجم, حجم, شفافية)؛

        إذا عدد_المقابض > 0 {
            تغير.س = هات_قيمة_محور_المقبض(0، 0) * 5؛
            تغير.ص = هات_قيمة_محور_المقبض(0، 1) * 5؛
            مضغوط = هات_قيمة_زر_المقبض(0، 0) > 0.5؛
        }

        إذا مضغوط مرسم.ارسم_صورة(صورة, موقع.س, موقع.ص, حجم, حجم, شفافية)
        وإلا مرسم.ارسم_صورة(صورة_ديناميكية, موقع.س, موقع.ص, حجم, حجم, شفافية)؛
        إذا ++س >= 1280 س = -80؛
        موقع.س += تغير.س؛
        موقع.ص += تغير.ص؛
    })؛

    نفذ_حلقة_معالجة_الأحداث()؛
}


طـرفية.اطبع("تشغيل الخادم على المنفذ 8010...\nURL: http://localhost:8010/\n")؛
شغل_الخادم ({ "listening_ports"، "8010"، "static_file_max_age"، "0" })؛

