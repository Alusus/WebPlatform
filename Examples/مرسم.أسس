اشمل "مـحا"؛
مـحا.اشمل_ملف("Alusus/WebPlatform"، "مـنصة_ويب.أسس")؛
مـحا.اشمل_ملف("Alusus/Http"، "بـننف.أسس")؛
مـحا.اشمل_ملف("Alusus/Json"، "جـيسون.أسس")؛
مـحا.اشمل_ملف("Alusus/Promises"، "مـؤجلات.أسس")؛
اشمل "مـتم/سندات"؛
اشمل "مـتم/مـصفوفة"؛
اشمل "مـتم/نـص"؛
اشمل "مـتم/طـرفية"؛
اشمل "مـتم/ريـاضيات"؛

استخدم مـتم؛
استخدم مـنصة_ويب؛

//=============================================================================
// الخادم

@مسار_موارد عرف مسار_الموارد: "Assets/"؛

//=============================================================================
// واجهة المستخدم

عرف _خط_الكتابة_: "30px AlususMono"؛
عرف لون_اصلي: لـون("8e50ef")؛

دالة أنشئ_الترويسة():سـندنا[Widget]{
    حدد_ميزة_عنصر("body"، "style"، "padding: 0; margin: 0;")؛

    أرجع صـندوق.أنشئ({}).{
        العرض = نـص("100%")؛
        الطول = نـص("40pt")؛
        الحشوة = ربـاعية(5، 0)؛
        لون_الخلفية = لون_اصلي؛
        لون_الإطار = لون_اصلي؛
        الإطار = ربـاعية(1.5)؛

        أضف_فروع({
            عـنوان.أنشئ(نـص("أمثلة منصة ويب الأسس - مرسم")).{
                لون_الخط = لـون("fff")؛
                حجم_الخط = 21.0؛
                العرض = نـص("100%")؛
                الطول = نـص("100%")؛
                لون_الخلفية = لون_اصلي؛
            }
        })؛
    }~مثل[سـندنا[ودجـة]]؛
}

@منفذ_مرئي["/"، "مثال منصة ويب - مرسم"]
دالة رئيسي {
    // حمل الموارد من الخادم
    عرف صورة: مـورد_صورة؛
    عرف موسيقى: مـورد_صوت؛
    عرف صوت: مـورد_صوت؛
    انتظر(مـؤجلات.مـؤجلة[صحيح].الكل({
        صورة.حمل("Assets/logo.svg"),
        حمل_خط("AlususMono", "Assets/AlususMono.otf"),
        موسيقى.حمل("Assets/music.mp3"),
        صوت.حمل("Assets/sound.mp3")
    }).تجاهل_النتيجة())؛

    عرف سس: صحيح = 200؛
    عرف صص: صحيح = 150؛
    عرف زس: صحيح = 0؛
    عرف زص: صحيح = 0؛

    // إنشاء المشهد
    عرف مرسم: سـندنا[مـرسم]؛
    نـافذة.النموذج.حدد_المشهد(صـندوق.أنشئ({}).{
        محاذاة = نـص("center")؛
        أضف_فروع({
            أنشئ_الترويسة(),
            صـندوق.أنشئ({}).{
                النسق = نـص("row")؛
                أضف_فروع({
                    الـزر.أنشئ(نـص("شغل الموسيقى")).{
                        الطول = نـص("30pt")؛
                        العرض = نـص("120pt")؛
                        لون_الخلفية = لـون(200, 200, 200)؛
                        حجم_الخط = 16.0؛
                        عند_الضغط.اربط(مغلفة (موسيقى: كسند)&(ودجة: سند[الـزر], حمولة: سند[صحيح]) {
                            موسيقى.شغل(1)؛
                        })؛
                    },
                    الـزر.أنشئ(نـص("أوقف الموسيقى")).{
                        الطول = نـص("30pt")؛
                        العرض = نـص("120pt")؛
                        لون_الخلفية = لـون(200, 200, 200)؛
                        حجم_الخط = 16.0؛
                        عند_الضغط.اربط(مغلفة (موسيقى: كسند)&(ودجة: سند[الـزر], حمولة: سند[صحيح]) {
                            موسيقى.أوقف()؛
                        })؛
                    },
                    الـزر.أنشئ(نـص("شغل الصوت")).{
                        الطول = نـص("30pt")؛
                        العرض = نـص("120pt")؛
                        لون_الخلفية = لـون(200, 200, 200)؛
                        حجم_الخط = 16.0؛
                        عند_الضغط.اربط(مغلفة (صوت: كسند)&(ودجة: سند[الـزر], حمولة: سند[صحيح]) {
                            صوت.شغل(0)؛
                        })؛
                    },
                    الـزر.أنشئ(نـص("شغل الصوت بتكرار")).{
                        الطول = نـص("30pt")؛
                        العرض = نـص("150pt")؛
                        لون_الخلفية = لـون(200, 200, 200)؛
                        حجم_الخط = 16.0؛
                        عند_الضغط.اربط(مغلفة (صوت: كسند)&(ودجة: سند[الـزر], حمولة: سند[صحيح]) {
                            صوت.شغل(1)؛
                        })؛
                    },
                    الـزر.أنشئ(نـص("أوقف الصوت")).{
                        الطول = نـص("30pt")؛
                        العرض = نـص("120pt")؛
                        لون_الخلفية = لـون(200, 200, 200)؛
                        حجم_الخط = 16.0؛
                        عند_الضغط.اربط(مغلفة (صوت: كسند)&(ودجة: سند[الـزر], حمولة: سند[صحيح]) {
                            صوت.أوقف()؛
                        })؛
                    }
                })؛
            }
            مـرسم.أنشئ().{
                مرسم = هذا؛
                العرض = نـص("1280px")؛
                الطول = نـص("800px")؛
                عرض_الصورة = نـص("1280px")؛
                طول_الصورة = نـص("800px")؛
                الإطار = ربـاعية(1, 1, 1, 1)؛
                لون_الإطار = لـون(0, 0, 0)؛
                لون_الخلفية = لـون(220, 220, 220)؛
                عند_تحريك_المؤشر.اربط(
                    مغلفة (سس: كسند، صص: كسند)&(ودجة: سند[مـرسم]، حمولة: سند[حـمولة_تحريك_المؤشر]) {
                        سس = حمولة.موقع_س؛
                        صص = حمولة.موقع_ص؛
                    }
                )؛
            }
        })؛
    })؛

    نـافذة.النموذج.{
        المفاتيح_المكبوتة = "ArrowUp,ArrowDown,ArrowLeft,ArrowRight"؛
        عند_بدء_الكبسة.اربط(مغلفة (زس: كسند، زص: كسند)&(نافذة: سند[نـافذة]، حمولة: سند[نـص]) {
            إذا حمولة == "ArrowUp" زص = -5
            وإلا إذا حمولة == "ArrowDown" زص = 5
            وإلا إذا حمولة == "ArrowRight" زس = 5
            وإلا إذا حمولة == "ArrowLeft" زس = -5؛
        })؛
        عند_انتهاء_الكبسة.اربط(مغلفة (زس: كسند، زص: كسند)&(نافذة: سند[نـافذة]، حمولة: سند[نـص]) {
            إذا حمولة == "ArrowUp" زص = 0
            وإلا إذا حمولة == "ArrowDown" زص = 0
            وإلا إذا حمولة == "ArrowRight" زس = 0
            وإلا إذا حمولة == "ArrowLeft" زس = 0؛
        })؛
    }؛

    // أنشئ صورة ديناميكيًا
    عرف صورة_ديناميكية: مـورد_صورة؛
    عرف مورد_مرسم: مـورد_مرسم(100, 100)؛
    مورد_مرسم.حدد_عرض_القلم(5)؛
    مورد_مرسم.حدد_نمط_القلم("#ff5555")؛
    مورد_مرسم.ارسم_خط(0, 0, 100, 100)؛
    مورد_مرسم.ارسم_خط(100, 0, 0, 100)؛
    عرف نقاط: مـصفوفة[صحيح]({ 25, 25, 75, 25, 75, 75, 25, 75 })؛
    مورد_مرسم.ارسم_مضلع(4, نقاط.صوان, 0)؛
    صورة_ديناميكية.هيئ_من_مرسم(مورد_مرسم)؛

    // حلقة التحريك
    عرف س: صحيح = -80؛
    ابدأ_المؤقت(8000, مغلفة (
        س: كسند, صورة: كسند, صورة_ديناميكية: كسند، سس: كسند، صص: كسند، زس: كسند، زص: كسند
    ) & (جسون: جـيسون) {
        مرسم.فرغ()؛
        مرسم.ارسم_خط(0,0, 100,100)؛
        مرسم.ارسم_دائرة(100,180, 90,0 )؛
        عرف نقاط: مـصفوفة [صحيح]({ 50, 50, 100, 50, 100, 100, 50, 100, 25, 75 })؛
        مرسم.حدد_نمط_الملء("black", "blue", 0, 0, 100, 100)؛
        مرسم.ارسم_مضلع(5, نقاط.صوان, 1)؛
        عرف _كتابة_: "الأسس"؛
        مرسم.ارسم_كتابة(_كتابة_, _خط_الكتابة_, 550, 150)؛

        عرف حجم: صحيح = ريـاضيات.جا(3.14 * س / 320.0) * 40 + 100؛
        عرف ص1: صحيح = ريـاضيات.جا(3.14 * س / 320.0) * 100 + 400 - حجم / 2؛
        عرف ص2: صحيح = ريـاضيات.جتا(3.14 * س / 320.0) * 100 + 400 - حجم / 2؛
        عرف شفافية: عائم = ريـاضيات.جا(3.14 * س / 80.0) / 4 + 0.75؛
        مرسم.ارسم_صورة(صورة, س, ص1, حجم, حجم, شفافية)؛
        مرسم.ارسم_صورة(صورة_ديناميكية, س, ص2, حجم, حجم, شفافية)؛
        مرسم.ارسم_صورة(صورة_ديناميكية, سس, صص, حجم, حجم, شفافية)؛
        إذا ++س >= 1280 س = -80؛
        سس += زس؛
        صص += زص؛
    })؛

    نفذ_حلقة_معالجة_الأحداث()؛
}


طـرفية.اطبع("تشغيل الخادم على المنفذ 8010...\nURL: http://localhost:8010/\n")؛
شغل_الخادم ({ "listening_ports"، "8010"، "static_file_max_age"، "0" })؛

