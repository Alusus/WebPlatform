اشمل "مـحا"؛
مـحا.اشمل_ملف("Alusus/WebPlatform"، "مـنصة_ويب.أسس")؛
اشمل "مـتم/نـص"؛
اشمل "مـتم/طـرفية"؛
اشمل "مـتم/سندات"؛
استخدم مـتم؛
استخدم مـنصة_ويب؛

//==============================================================================
// الخادم

@مسار_موارد عرف مسار_الموارد: "Assets/"؛

//==============================================================================
// مـركبات واجهة المستخدم

صنف كـبسة_القائمة {
    @حقنة عرف مركب: مـركب؛

    عملية هذا~هيئ(رابط: نـص, عنوان: نـص) {
        هذا.المشهد = صـندوق({}).{
            الطراز.{
                النسق = "row"؛
                المحاذاة = "center"؛
                محاذاة_النص = "center"؛
                العرض = "60pt"؛
                المؤشر = "pointer"؛
                الانتقال   = ".3s"؛
            }؛
            الطراز("عنوان").{
                حجم_الخط = 14.0؛
                لون_الخط = لـون("444")؛
            }
            الطراز({ مـحدد_حالة._حوم_ }).{
                التحول   = "scale(1.3)"؛
            }
            الطراز({ مـحدد_حالة._حوم_, "عنوان" }).{
                حجم_الخط = 14.0؛
                لون_الخط = لـون("000")؛
            }
            عند_الضغط.اربط(مغلفة (ودجة: سند[ودجـة], حمولة: سند[صـحيح]) {
                نـافذة.النموذج.ادفع_حالة(رابط)؛
            })؛
            أضف_فروع({
                عـنوان(عنوان).{
                    اسم_الفئة = نـص("عنوان")
                }
            })؛
        }؛
    }

    عملية هذا_الصنف(رابط: نـص, عنوان: نـص): سـندنا[كـبسة_القائمة] {
        أرجع سـندنا[كـبسة_القائمة]().{ احجز()~هيئ(رابط, عنوان) }؛
    }
}

صنف كـبسة_جانبية {
    @حقنة عرف مركب: مـركب؛
    عرف عند_نقر_الكبسة: مغلفة()؛

    عملية هذا~هيئ(عنوان: نـص) {
        عرف هذا_الكائن: سند[هذا_الصنف](هذا)؛
        هذا.المشهد = صـندوق({}).{
            الطراز.{
                المحاذاة = "center"؛
                المؤشر = "pointer"؛
                العرض = "40pt"؛
                محاذاة_النص = "center"؛
            }؛
            الطراز("عنوان").{
                حجم_الخط = 14.0؛
                لون_الخط = لـون("444")؛
            }
            الطراز({ مـحدد_حالة._حوم_, "عنوان" }).{
                حجم_الخط = 14.0؛
                لون_الخط = لـون("000")؛
            }
            عند_الضغط.اربط(مغلفة (ودجة: سند[ودجـة], حمولة: سند[صـحيح]) {
                هذا_الكائن.عند_نقر_الكبسة()؛
            })؛
            أضف_فروع({
                عـنوان(عنوان).{
                    اسم_الفئة = نـص("عنوان")
                }
            })؛
        }؛
    }

    عملية هذا_الصنف(عنوان: نـص): سـندنا[كـبسة_جانبية] {
        أرجع سـندنا[كـبسة_جانبية]().{ احجز()~هيئ(عنوان) }؛
    }
}

صنف الـترويسة {
    @حقنة عرف مركب: مـركب؛
    عرف الدليل: سـندنا[صـندوق]؛
    عرف مبدال_التوجيه: سـندنا[مـبدال_توجيه]؛
    عرف عند_تغير_المسار: مغلفة (سند[مـتحسس_مسار], سند[حـمولة_مسار]) =
        مغلفة (سند[مـتحسس_مسار], سند[حـمولة_مسار]) {
            هذا.حدث_الدليل()؛
        }؛

    عملية هذا~هيئ() {
        عرف هذا_الكائن: سند[هذا_الصنف](هذا)؛
        هذا.المشهد = صـندوق({}).{
            الطراز.{
                العرض = "100%"؛
                الطول = "85pt"؛
                الحشوة = ربـاعية(4, 0, 0, 0)؛
                سمك_الإطار = ربـاعية(0، 0، 3، 0)؛
                طراز_الإطار = "solid"؛
                لون_الإطار = لـون("666")؛
                ملء_السطر = "space-between"؛
                المحاذاة = "center"؛
                الإظهار = "flex"؛
                النسق = "column"؛
            }؛
            عرف ع: صـحيح؛
            لكل ع = 0, ع < 3, ++ع {
                الطراز(نـص("الدليل")+ع).{
                    العرض = "60pt"؛
                    الطول = "3pt"؛
                    الخلفية = لـون(0, 0, 150)؛
                    التحول = نـص.املأ("translateX(%ipt)", -ع * 60)؛
                    الانتقال = نـص(".5s")؛
                }؛
            }

            أضف_فروع({
                صـندوق({}).{
                    الطراز.{
                        الإظهار = "flex"؛
                        النسق = "row"؛
                        ملء_السطر = "space-between"؛
                        المحاذاة = "center"؛
                    }؛
                    أضف_فروع({
                        صـورة().{
                            الرابط = نـص("/Assets/wblogo.svg")؛
                            الطراز.{
                                الطول = "50pt"؛
                                الحشوة = ربـاعية(0, 10)؛
                            }؛
                        },
                        صـندوق({ عـنوان(نـص("مثال منصة ويب الأسس")).{ الطراز.{
                            لون_الخط = لـون("000")؛
                            حجم_الخط = 21.0؛
                            الطول = "100%"؛
                            الحشوة = ربـاعية(0, 10)؛
                        } } })
                    })؛
                },
                صـندوق({}).{
                    الطراز.{
                        الإظهار = "flex"؛
                        النسق = "row"؛
                        ملء_السطر = "center"؛
                    }؛
                    أضف_فروع({
                        كـبسة_القائمة(نـص("/one"), نـص("واحد")),
                        كـبسة_القائمة(نـص("/two"), نـص("اثنان")),
                        كـبسة_القائمة(نـص("/three"), نـص("ثلاثة"))
                    })؛
                },
                صـندوق({}).{
                    الطراز.{
                        الإظهار = "flex"؛
                        النسق = "row"؛
                        ملء_السطر = "start"؛
                        العرض = "180pt"؛
                        الحشوة = ربـاعية(0, 0, 4, 0)؛
                    }؛
                    أضف_فروع({
                        صـندوق({}).{
                            هذا_الكائن.الدليل = هذا؛
                        }
                    })؛
                }
            })؛
        }؛
    }

    عملية هذا~أتلف() {
        إذا ليس هذا.مبدال_التوجيه.أهو_لعدم() {
            هذا.مبدال_التوجيه.متحسس_المسار.عند_التوجيه.افصل(هذا.عند_تغير_المسار)؛
        }
    }

    عملية هذا.حدد_مبدال_التوجيه(rs: سـندنا[مـبدال_توجيه]) {
        إذا ليس هذا.مبدال_التوجيه.أهو_لعدم() {
            هذا.مبدال_التوجيه.متحسس_المسار.عند_التوجيه.افصل(هذا.عند_تغير_المسار)؛
        }
        هذا.مبدال_التوجيه = rs؛
        هذا.مبدال_التوجيه.متحسس_المسار.عند_التوجيه.اربط(هذا.عند_تغير_المسار)؛
        هذا.الدليل.اسم_الفئة = نـص("الدليل") + صـحيح()~use_in(ع) {
            ع = هذا.مبدال_التوجيه.استنتج_تسلسل_المسار_الحالي()؛
            إذا ع == -1 ع = 0؛
        }
    }

    عملية هذا.حدث_الدليل() {
        هذا.الدليل.اسم_الفئة = نـص("الدليل") + هذا.مبدال_التوجيه.تسلسل_المسار_الحالي؛
    }

    عملية هذا_الصنف(): سـندنا[الـترويسة] {
        أرجع سـندنا[الـترويسة].أنشئ()؛
    }
}

صنف صـفحة_نصية {
    @حقنة عرف مركب: مـركب؛

    عملية هذا~هيئ(عنوان: نـص) {
        هذا.المشهد = عـنوان(عنوان).{
            الطراز.{
                لون_الخط = لـون(0, 0, 0)؛
                حجم_الخط = 20.0؛
            }
        }؛
    }

    عملية هذا_الصنف(عنوان: نـص): سـندنا[صـفحة_نصية] {
        أرجع سـندنا[صـفحة_نصية]().{ احجز()~هيئ(عنوان) }؛
    }
}

صنف مـشهد_صورة {
    @حقنة عرف مركب: مـركب؛

    عملية هذا~هيئ(مسار: نـص) {
        هذا.المشهد = صـورة().{
            الرابط = مسار؛
            الطراز.{
                العرض = "100%"؛
                الطول = "100%"؛
            }؛
        }؛
    }

    عملية هذا_الصنف(مسار: نـص): سـندنا[مـشهد_صورة] {
        أرجع سـندنا[مـشهد_صورة]().{ احجز()~هيئ(مسار) }؛
    }
}

صنف عـارض_صور {
    @حقنة عرف مركب: مـركب؛

    عملية هذا~هيئ() {
        هذا.المشهد = صـندوق({}).{
            الطراز.{
                العرض = "100%"؛
                الطول = "100%"؛
                الإظهار = "flex"؛
                النسق = "row"؛
            }؛
            أضف_فروع({
                صـندوق({}).{
                    الطراز.{
                        النسق = "column"؛
                        الخلفية = لـون("eee")؛
                    }؛
                    أضف_فروع({
                        كـبسة_جانبية(نـص("1")).{ عند_نقر_الكبسة = مغلفة() { نـافذة.النموذج.ادفع_حالة("/one/img1") } },
                        كـبسة_جانبية(نـص("2")).{ عند_نقر_الكبسة = مغلفة() { نـافذة.النموذج.ادفع_حالة("/one/img2") } },
                        كـبسة_جانبية(نـص("3")).{ عند_نقر_الكبسة = مغلفة() { نـافذة.النموذج.ادفع_حالة("/one/img3") } },
                    })؛
                },
                صـندوق({}).{
                    الطراز.{
                        المرونة = "auto"؛
                    }؛
                    أضف_فروع({
                        مـبدال_توجيه().{
                            الطراز.{
                                المرونة = "auto"؛
                                الطول = "500pt"؛
                            }؛
                            حدد_النمط(نـص("^/one/([a-z0-9]+)"))؛
                            حدد_الانتقال(
                                أنشئ_انتقال_تلاشي_مبدال(1.0),
                                أنشئ_انتقال_تلاشي_مبدال(1.0)
                            )؛
                            مسار("img1") = مغلفة(حـمولة_مسار): سـندنا[مـركب] {
                                أرجع مـشهد_صورة(نـص("/Assets/wblogo.svg"))؛
                            }؛
                            مسار("img2") = مغلفة(حـمولة_مسار): سـندنا[مـركب] {
                                أرجع مـشهد_صورة(نـص("/Assets/future-musume.jpg"))؛
                            }؛
                            مسار("img3") = مغلفة(حـمولة_مسار): سـندنا[مـركب] {
                                أرجع مـشهد_صورة(نـص("/Assets/bridge.png"))؛
                            }؛
                        }
                    })؛
                }
            })؛
        }
    }

    عملية هذا_الصنف(): سـندنا[عـارض_صور] {
        أرجع سـندنا[عـارض_صور].أنشئ()؛
    }
}

صنف مـشهد_عدد {
    @حقنة عرف مركب: مـركب؛

    عملية هذا~هيئ(عنوان: نـص) {
        هذا.المشهد = صـندوق({}).{
            الطراز.{
                العرض = "100%"؛
                الطول = "100%"؛
                الإظهار = "flex"؛
                المحاذاة = "center"؛
                ملء_السطر = "center"؛
            }؛
            أضف_فروع({
                صـندوق({}).{
                    الطراز.{
                        العرض = "90%"؛
                        الطول = "90%"؛
                        نصف_قطر_الإطار = ربـاعية(30)؛
                        لون_الإطار = لـون("444")؛
                        طراز_الإطار = "solid"؛
                        سمك_الإطار = ربـاعية(10)؛
                        الإظهار = "flex"؛
                        المحاذاة = "center"؛
                        ملء_السطر = "center"؛
                    }؛
                    أضف_فروع({
                        عـنوان(عنوان).{
                            الطراز.{
                                لون_الخط = لـون(0, 0, 0)؛
                                حجم_الخط = 250.0؛
                            }
                        }
                    })؛
                }
            })؛
        }؛
    }

    عملية هذا_الصنف(عنوان: نـص): سـندنا[مـشهد_عدد] {
        أرجع سـندنا[مـشهد_عدد]().{ احجز()~هيئ(عنوان) }؛
    }
}

صنف مـشهد_عداد {
    @حقنة عرف مركب: مـركب؛
    @مشترك عرف العدد_الحالي: صـحيح؛
    عرف مبدال: سـندنا[مـبدال]؛

    عملية هذا~هيئ() {
        عرف هذا_الكائن: سند[هذا_الصنف](هذا)؛
        هذا.المشهد = صـندوق({}).{
            الطراز.{
                العرض = "100%"؛
                الطول = "100%"؛
                الإظهار = "flex"؛
                النسق = "row"؛
            }؛
            أضف_فروع({
                صـندوق({}).{
                    الطراز.{
                        النسق = "column"؛
                        الخلفية = لـون("eee")؛
                    }؛
                    أضف_فروع({
                        كـبسة_جانبية(نـص("-")).{ عند_نقر_الكبسة = مغلفة() {
                            هذا_الكائن.مبدال.بدل_إلى(مـشهد_عدد(نـص() + (--العدد_الحالي)), نـص("للخلف"))؛
                        } },
                        كـبسة_جانبية(نـص("+")).{ عند_نقر_الكبسة = مغلفة() {
                            هذا_الكائن.مبدال.بدل_إلى(مـشهد_عدد(نـص() + (++العدد_الحالي)), نـص("للأمام"))؛
                        } }
                    })؛
                },
                صـندوق({}).{
                    الطراز.{
                        المرونة = "auto"؛
                    }؛
                    أضف_فروع({
                        مـبدال().{
                            الطراز.{
                                المرونة = "auto"؛
                                الطول = "500pt"؛
                            }؛
                            حدد_الانتقال(نـص("للأمام"), أنشئ_انتقال_انزلاق_مبدال(0.5, 1، 0))؛
                            حدد_الانتقال(نـص("للخلف"), أنشئ_انتقال_انزلاق_مبدال(0.5, 1، 1))؛
                            بدل_إلى(مـشهد_عدد(نـص() + العدد_الحالي))؛
                            هذا_الكائن.مبدال = هذا؛
                        }
                    })؛
                }
            })؛
        }
    }

    عملية هذا_الصنف(): سـندنا[مـشهد_عداد] {
        أرجع سـندنا[مـشهد_عداد].أنشئ()؛
    }
}

//==============================================================================
// صـفحات واجهة المستخدم

عرف لوريم:
    "لوريم ايبسوم دولار سيت أميت ,كونسيكتيتور أدايبا يسكينج أليايت,سيت دو أيوسمود تيمبور"
    "أنكايديديونتيوت لابوري ات دولار ماجنا أليكيوا . يوت انيم أد مينيم فينايم,كيواس نوستريد"
    "أكسير سيتاشن يللأمكو لابورأس نيسي يت أليكيوب أكس أيا كوممودو كونسيكيوات . ديواس"
    "أيوتي أريري دولار إن ريبريهينديرأيت فوليوبتاتي فيلايت أيسسي كايلليوم دولار أيو فيجايت"
    "نيولا باراياتيور. أيكسسيبتيور ساينت أوككايكات كيوبايداتات نون بروايدينت ,سيونت ان كيولبا"
    "كيو أوفيسيا ديسيريونتموليت انيم أيدي ايست لابوريوم.<br><br>"
    "سيت يتبيرسبايكياتيس يوندي أومنيس أستي ناتيس أيررور سيت فوليبتاتيم أكيسأنتييوم"
    "دولاريمكيو لايودانتيوم,توتام ريم أبيرأم,أيكيو أبسا كيواي أب أللو أنفينتوري فيرأتاتيس ايت"
    "كياسي أرشيتيكتو بيتاي فيتاي ديكاتا سيونت أكسبليكابو. نيمو أنيم أبسام فوليوباتاتيم كيواي"
    "فوليوبتاس سايت أسبيرناتشر أيوت أودايت أيوت فيوجايت, سيد كيواي كونسيكيونتشر ماجناي"
    "دولارس أيوس كيواي راتاشن فوليوبتاتيم سيكيواي نيسكايونت. نيكيو بوررو كيوايسكيوم"
    "ايست,كيواي دولوريم ايبسيوم كيوا دولار سايت أميت, كونسيكتيتيور,أديبايسكاي فيلايت, سيد"
    "كيواي نون نيومكيوام ايايوس موداي تيمبورا انكايديونت يوت لابوري أيت دولار ماجنام"
    "ألايكيوام كيوايرات فوليوبتاتيم. يوت اينايم أد مينيما فينيام, كيواس نوستريوم أكسيركايتاشيم"
    "يلامكوربوريس سيوسكايبيت لابورايوسام, نايساي يوت ألايكيوايد أكس أيا كوموداي"
    "كونسيكيواتشر؟ كيوايس أيوتيم فيل أيوم أيوري ريبريهينديرايت كيواي ان إيا فوليوبتاتي"
    "فيلايت ايسسي كيوم نايهايل موليستايا كونسيكيواتيو,فيلايليوم كيواي دولوريم أيوم فيوجايات كيو"
    "فوليوبتاس نيولا باراياتيور؟"؛

@منفذ_مرئي["/*", "مثال منصة الويب"]
دالة رئيسي {
    حدد_ميزة_عنصر("body", "style", "padding: 0; margin: 0;")؛
    حمل_خط("AlususMono", "Assets/AlususMono.otf")؛

    عرف مبدال: سـندنا[مـبدال]؛
    عرف ترويسة: سـندنا[الـترويسة]؛
    نـافذة.النموذج.حدد_المشهد(صـندوق({}).{
        الطراز.{
            الإظهار = "flex"؛
            النسق = "column"؛
            المحاذاة = "center"؛
            الاتجاه = "rtl"؛
            فئة_الخط = "AlususMono"؛
        }؛
        أضف_فروع({
            الـترويسة().{ ترويسة = هذا },
            مـبدال_توجيه().{
                الطراز.{
                    العرض = "100%"؛
                    أقصى_عرض = "600pt"؛
                    أدنى_طول = "500pt"؛
                }؛
                حدد_النمط(نـص("^/([a-z]+)"))؛
                حدد_الانتقال(
                    أنشئ_انتقال_انزلاق_مبدال(0.5, 0، 1),
                    أنشئ_انتقال_انزلاق_مبدال(0.5, 0، 0)
                )؛
                مسار("one") = مغلفة(حـمولة_مسار): سـندنا[مـركب] {
                    أرجع عـارض_صور()؛
                }؛
                مسار("two") = مغلفة(حـمولة_مسار): سـندنا[مـركب] {
                    أرجع مـشهد_عداد()؛
                }؛
                مسار("three") = مغلفة(حـمولة_مسار): سـندنا[مـركب] {
                    أرجع صـفحة_نصية(نـص(لوريم))؛
                }؛
                ترويسة.حدد_مبدال_التوجيه(هذا)؛
            }
        })؛
    })؛

    نفذ_حلقة_معالجة_الأحداث()؛
}


طـرفية.اطبع("تشغيل الخادم على المنفذ 8010...\nURL: http://localhost:8010/\n")؛
شغل_الخادم ({ "listening_ports"، "8010"، "static_file_max_age"، "0" })؛

