اشمل "مـحا"؛
مـحا.اشمل_ملف("Alusus/WebPlatform"، "مـنصة_ويب.أسس")؛
اشمل "مـتم/نـص"؛
اشمل "مـتم/طـرفية"؛
اشمل "مـتم/سندات"؛
استخدم مـتم؛
استخدم مـنصة_ويب؛

//==============================================================================
// الخادم

@مسار_موارد عرف مسار_الموارد: "Assets/"؛

//==============================================================================
// ثوابت

عرف لوريم:
    "# لوريم\ج"
    "\ج"
    "لوريم ايبسوم دولار سيت أميت ,كونسيكتيتور أدايبا يسكينج أليايت,سيت دو أيوسمود تيمبور"
    "أنكايديديونتيوت لابوري ات دولار ماجنا أليكيوا . يوت انيم أد مينيم فينايم,كيواس نوستريد"
    "أكسير سيتاشن يللأمكو لابورأس نيسي يت أليكيوب أكس أيا كوممودو كونسيكيوات . ديواس"
    "أيوتي أريري دولار إن ريبريهينديرأيت فوليوبتاتي فيلايت أيسسي كايلليوم دولار أيو فيجايت"
    "نيولا باراياتيور. أيكسسيبتيور ساينت أوككايكات كيوبايداتات نون بروايدينت ,سيونت ان كيولبا"
    "كيو أوفيسيا ديسيريونتموليت انيم أيدي ايست لابوريوم.\ج"
    "\ج"
    "سيت يتبيرسبايكياتيس يوندي أومنيس أستي ناتيس أيررور سيت فوليبتاتيم أكيسأنتييوم"
    "دولاريمكيو لايودانتيوم,توتام ريم أبيرأم,أيكيو أبسا كيواي أب أللو أنفينتوري فيرأتاتيس ايت"
    "كياسي أرشيتيكتو بيتاي فيتاي ديكاتا سيونت أكسبليكابو. نيمو أنيم أبسام فوليوباتاتيم كيواي"
    "فوليوبتاس سايت أسبيرناتشر أيوت أودايت أيوت فيوجايت, سيد كيواي كونسيكيونتشر ماجناي"
    "دولارس أيوس كيواي راتاشن فوليوبتاتيم سيكيواي نيسكايونت.\ج"
    "\ج"
    "## عنوان فرعي\ج"
    "\ج"
    "* عنصر 1\ج"
    "* عنصر 2\ج"
    "* عنصر 3\ج"
    "  * عنصر 3.1\ج"
    "  * عنصر 3.2\ج"
    "\ج"
    "## عن المثال\ج"
    "\ج"
    "طُوّر باستخدام [لغة الأسس](https://alusus.org) و [مـنصة ويب الأسس](https://github.com/Alusus/WebPlatform)."؛

//==============================================================================
// مـركبات واجهة المستخدم

صنف كـبسة_القائمة {
    @حقنة عرف مركب: مـركب؛

    عملية هذا~هيئ(رابط: نـص, عنوان: نـص) {
        هذا.المشهد = صـندوق({}).{
            الطراز.{
                النسق = "row"؛
                المحاذاة = "center"؛
                محاذاة_النص = "center"؛
                العرض = "60pt"؛
                المؤشر = "pointer"؛
                الانتقال   = ".3s"؛
            }؛
            الطراز(">عنوان").{
                حجم_الخط = 14.0؛
                لون_الخط = لـون("444")؛
            }
            الطراز({ مـحدد_حالة._حوم_ }).{
                التحول   = "scale(1.3)"؛
            }
            الطراز({ مـحدد_حالة._حوم_, ">عنوان" }).{
                حجم_الخط = 14.0؛
                لون_الخط = لـون("000")؛
            }
            عند_الضغط.اربط(مغلفة (ودجة: سند[ودجـة], حمولة: سند[صـحيح]) {
                نـافذة.النموذج.ادفع_مسارا(رابط)؛
            })؛
            أضف_فروع({
                كـتابة(عنوان).{
                    اسم_الفئة = نـص("عنوان")
                }
            })؛
        }؛
    }

    عملية هذا_الصنف(رابط: نـص, عنوان: نـص): سـندنا[كـبسة_القائمة] {
        أرجع سـندنا[كـبسة_القائمة]().{ احجز()~هيئ(رابط, عنوان) }؛
    }
}

صنف كـبسة_جانبية {
    @حقنة عرف مركب: مـركب؛
    عرف عند_نقر_الكبسة: مغلفة()؛

    عملية هذا~هيئ(عنوان: نـص) {
        عرف هذا_الكائن: سند[هذا_الصنف](هذا)؛
        هذا.المشهد = صـندوق({}).{
            الطراز.{
                المحاذاة = "center"؛
                المؤشر = "pointer"؛
                العرض = "40pt"؛
                محاذاة_النص = "center"؛
            }؛
            الطراز(">عنوان").{
                حجم_الخط = 14.0؛
                لون_الخط = لـون("444")؛
            }
            الطراز({ مـحدد_حالة._حوم_, ">عنوان" }).{
                حجم_الخط = 14.0؛
                لون_الخط = لـون("000")؛
            }
            عند_الضغط.اربط(مغلفة (ودجة: سند[ودجـة], حمولة: سند[صـحيح]) {
                هذا_الكائن.عند_نقر_الكبسة()؛
            })؛
            أضف_فروع({
                كـتابة(عنوان).{
                    اسم_الفئة = نـص("عنوان")
                }
            })؛
        }؛
    }

    عملية هذا_الصنف(عنوان: نـص): سـندنا[كـبسة_جانبية] {
        أرجع سـندنا[كـبسة_جانبية]().{ احجز()~هيئ(عنوان) }؛
    }
}

صنف الـترويسة {
    @حقنة عرف مركب: مـركب؛
    عرف الدليل: سـندنا[صـندوق]؛

    عملية هذا~هيئ() {
        عرف هذا_الكائن: سند[هذا_الصنف](هذا)؛
        هذا.المشهد = صـندوق({}).{
            الطراز.{
                العرض = "100%"؛
                الطول = "85pt"؛
                الحشوة = ربـاعية(4, 0, 0, 0)؛
                سمك_الإطار = ربـاعية(0، 0، 3، 0)؛
                طراز_الإطار = "solid"؛
                لون_الإطار = لـون("666")؛
                ملء_السطر = "space-between"؛
                المحاذاة = "center"؛
                الإظهار = "flex"؛
                النسق = "column"؛
            }؛
            عرف ع: صـحيح؛
            لكل ع = 0, ع < 3, ++ع {
                الطراز(نـص(">>الدليل")+ع).{
                    العرض = "60pt"؛
                    الطول = "3pt"؛
                    الخلفية = لـون(0, 0, 150)؛
                    التحول = نـص.املأ("translateX(%ipt)", -ع * 60)؛
                    الانتقال = نـص(".5s")؛
                }؛
            }

            أضف_فروع({
                صـندوق({}).{
                    الطراز.{
                        الإظهار = "flex"؛
                        النسق = "row"؛
                        ملء_السطر = "space-between"؛
                        المحاذاة = "center"؛
                    }؛
                    أضف_فروع({
                        صـورة().{
                            الرابط = نـص("/Assets/wblogo.svg")؛
                            الطراز.{
                                الطول = "50pt"؛
                                الحشوة = ربـاعية(0, 10)؛
                            }؛
                        },
                        صـندوق({ كـتابة(نـص("مثال منصة ويب الأسس")).{ الطراز.{
                            لون_الخط = لـون("000")؛
                            حجم_الخط = 21.0؛
                            الطول = "100%"؛
                            الحشوة = ربـاعية(0, 10)؛
                        } } })
                    })؛
                },
                صـندوق({}).{
                    الطراز.{
                        الإظهار = "flex"؛
                        النسق = "row"؛
                        ملء_السطر = "center"؛
                    }؛
                    أضف_فروع({
                        كـبسة_القائمة(نـص("/one"), نـص("واحد")),
                        كـبسة_القائمة(نـص("/two"), نـص("اثنان")),
                        كـبسة_القائمة(نـص("/three"), نـص("ثلاثة"))
                    })؛
                },
                صـندوق({}).{
                    الطراز.{
                        الإظهار = "flex"؛
                        النسق = "row"؛
                        ملء_السطر = "start"؛
                        العرض = "180pt"؛
                        الحشوة = ربـاعية(0, 0, 4, 0)؛
                    }؛
                    أضف_فروع({
                        صـندوق({}).{
                            هذا_الكائن.الدليل = هذا؛
                        }
                    })؛
                }
            })؛
        }؛
    }

    عملية هذا.حدث_الدليل(ع: صـحيح) {
        هذا.الدليل.اسم_الفئة = نـص("الدليل") + ع؛
    }

    عملية هذا_الصنف(): سـندنا[الـترويسة] {
        أرجع سـندنا[الـترويسة].أنشئ()؛
    }
}

عرف النص: نـص(لوريم)؛

صنف صـفحة_نصية {
    @حقنة عرف مركب: مـركب؛
    عرف مشهد_وثيقة: سـندنا[مـشهد_وثيقة]؛

    عملية هذا~هيئ() {
        عرف الكائن: سند[هذا_الصنف](هذا)؛
        هذا.المشهد = صـندوق({}).{
            الطراز.{
                المحاذاة = "end"؛
                الإظهار = "flex"؛
                النسق = "column"؛
                الطول = "100%"؛
                الفيض = "scroll"؛
            }؛
            أضف_فروع({
                كـبسة_جانبية(نـص("🖉")).{
                    عند_نقر_الكبسة = مغلفة() {
                        نـافذة.النموذج.ادفع_مسارا("/three/edit")؛
                    }
                },
                مـشهد_وثيقة().{
                    الكائن.مشهد_وثيقة = هذا؛
                    الطراز.{
                        العرض = "100%"؛
                        لون_الخط = لـون(0, 0, 0)؛
                        حجم_الخط = 20.0؛
                    }؛
                    حدد_ماركداون(النص)؛
                }
            })؛
        }؛
    }

    عملية هذا_الصنف(): سـندنا[صـفحة_نصية] {
        أرجع سـندنا[صـفحة_نصية].أنشئ()؛
    }
}

صنف مـحرر_نصي {
    @حقنة عرف مركب: مـركب؛
    عرف مدخل_نص: سـندنا[مـدخل_نص]؛
    عرف عند_التحديث: مغلفة(نـص)؛

    عملية هذا~هيئ(نص: نـص) {
        عرف الكائن: سند[هذا_الصنف](هذا)؛
        هذا.المشهد = صـندوق({}).{
            الطراز.{
                العرض = "100%"؛
                الطول = "100%"؛
                المحاذاة = "end"؛
                الإظهار = "flex"؛
                النسق = "column"؛
                الخلفية = "white"؛
            }؛
            أضف_فروع({
                كـبسة_جانبية(نـص("💾")).{ عند_نقر_الكبسة = مغلفة() {
                    الكائن.عند_التحديث(الكائن.مدخل_نص.النص)؛
                } },
                مـدخل_نص().{
                    الطراز.{
                        العرض = "100%"؛
                        الطول = "100%"؛
                        حجم_الخط = 20.0؛
                    }؛
                    النص = نص؛
                    الكائن.مدخل_نص = هذا؛
                },
            })؛
        }؛
    }

    عملية هذا_الصنف(ن: نـص): سـندنا[مـحرر_نصي] {
        أرجع سـندنا[مـحرر_نصي]().{ احجز()~هيئ(ن) }؛
    }
}

صنف مـشهد_نصي {
    @حقنة عرف مركب: مـركب؛
    عرف صفحة_نصية: سـندنا[صـفحة_نصية]؛

    عملية هذا~هيئ() {
        عرف الكائن: سند[هذا_الصنف](هذا)؛
        هذا.المشهد = مـكداس_توجيه().{
            الطراز.{
                العرض = "100%"؛
                الطول = "100%"؛
            }؛
            حدد_الانتقال(
                أنشئ_انتقال_انزلاق_مكداس(0.5, 1, 0)،
                أنشئ_انتقال_انزلاق_مكداس(0.5, 1, 1)
            )؛
            مسار(نـص("^/three")) = مغلفة(حـمولة_مسار): سـندنا[مـركب] {
                الكائن.صفحة_نصية = صـفحة_نصية()؛
                أرجع الكائن.صفحة_نصية؛
            }؛
            مسار(نـص("^/three/edit")) = مغلفة(حـمولة_مسار): سـندنا[مـركب] {
                أرجع مـحرر_نصي(النص).{
                    هذا.عند_التحديث = مغلفة(ن: نـص) {
                        النص = ن؛
                        الكائن.صفحة_نصية.مشهد_وثيقة.حدد_ماركداون(النص)؛
                        نـافذة.النموذج.ادفع_مسارا("/three")؛
                    }
                }؛
            }؛
        }؛
    }

    عملية هذا_الصنف(): سـندنا[مـشهد_نصي] {
        أرجع سـندنا[مـشهد_نصي].أنشئ()؛
    }
}

صنف مـشهد_صورة {
    @حقنة عرف مركب: مـركب؛

    عملية هذا~هيئ(مسار: نـص) {
        هذا.المشهد = صـورة().{
            الرابط = مسار؛
            الطراز.{
                العرض = "100%"؛
                الطول = "100%"؛
            }؛
        }؛
    }

    عملية هذا_الصنف(مسار: نـص): سـندنا[مـشهد_صورة] {
        أرجع سـندنا[مـشهد_صورة]().{ احجز()~هيئ(مسار) }؛
    }
}

صنف عـارض_صور {
    @حقنة عرف مركب: مـركب؛

    عملية هذا~هيئ() {
        هذا.المشهد = صـندوق({}).{
            الطراز.{
                العرض = "100%"؛
                الطول = "100%"؛
                الإظهار = "flex"؛
                النسق = "row"؛
            }؛
            أضف_فروع({
                صـندوق({}).{
                    الطراز.{
                        النسق = "column"؛
                        الخلفية = لـون("eee")؛
                    }؛
                    أضف_فروع({
                        كـبسة_جانبية(نـص("1")).{ عند_نقر_الكبسة = مغلفة() { نـافذة.النموذج.ادفع_مسارا("/one/img1") } },
                        كـبسة_جانبية(نـص("2")).{ عند_نقر_الكبسة = مغلفة() { نـافذة.النموذج.ادفع_مسارا("/one/img2") } },
                        كـبسة_جانبية(نـص("3")).{ عند_نقر_الكبسة = مغلفة() { نـافذة.النموذج.ادفع_مسارا("/one/img3") } },
                    })؛
                },
                صـندوق({}).{
                    الطراز.{
                        المرونة = "auto"؛
                    }؛
                    أضف_فروع({
                        مـبدال_توجيه().{
                            الطراز.{
                                المرونة = "auto"؛
                                الطول = "500pt"؛
                            }؛
                            حدد_الانتقال(
                                أنشئ_انتقال_تلاشي_مبدال(1.0),
                                أنشئ_انتقال_تلاشي_مبدال(1.0)
                            )؛
                            مسار("^/one/img1$") = مغلفة(حـمولة_مسار): سـندنا[مـركب] {
                                أرجع مـشهد_صورة(نـص("/Assets/wblogo.svg"))؛
                            }؛
                            مسار("^/one/img2$") = مغلفة(حـمولة_مسار): سـندنا[مـركب] {
                                أرجع مـشهد_صورة(نـص("/Assets/future-musume.jpg"))؛
                            }؛
                            مسار("^/one/img3$") = مغلفة(حـمولة_مسار): سـندنا[مـركب] {
                                أرجع مـشهد_صورة(نـص("/Assets/bridge.png"))؛
                            }؛
                        }
                    })؛
                }
            })؛
        }
    }

    عملية هذا_الصنف(): سـندنا[عـارض_صور] {
        أرجع سـندنا[عـارض_صور].أنشئ()؛
    }
}

صنف مـشهد_عدد {
    @حقنة عرف مركب: مـركب؛

    عملية هذا~هيئ(عنوان: نـص) {
        هذا.المشهد = صـندوق({}).{
            الطراز.{
                العرض = "100%"؛
                الطول = "100%"؛
                الإظهار = "flex"؛
                المحاذاة = "center"؛
                ملء_السطر = "center"؛
            }؛
            أضف_فروع({
                صـندوق({}).{
                    الطراز.{
                        العرض = "90%"؛
                        الطول = "90%"؛
                        نصف_قطر_الإطار = ربـاعية(30)؛
                        لون_الإطار = لـون("444")؛
                        طراز_الإطار = "solid"؛
                        سمك_الإطار = ربـاعية(10)؛
                        الإظهار = "flex"؛
                        المحاذاة = "center"؛
                        ملء_السطر = "center"؛
                    }؛
                    أضف_فروع({
                        كـتابة(عنوان).{
                            الطراز.{
                                لون_الخط = لـون(0, 0, 0)؛
                                حجم_الخط = 250.0؛
                            }
                        }
                    })؛
                }
            })؛
        }؛
    }

    عملية هذا_الصنف(عنوان: نـص): سـندنا[مـشهد_عدد] {
        أرجع سـندنا[مـشهد_عدد]().{ احجز()~هيئ(عنوان) }؛
    }
}

صنف مـشهد_عداد {
    @حقنة عرف مركب: مـركب؛
    @مشترك عرف العدد_الحالي: صـحيح؛
    عرف مبدال: سـندنا[مـبدال]؛

    عملية هذا~هيئ() {
        عرف هذا_الكائن: سند[هذا_الصنف](هذا)؛
        هذا.المشهد = صـندوق({}).{
            الطراز.{
                العرض = "100%"؛
                الطول = "100%"؛
                الإظهار = "flex"؛
                النسق = "row"؛
            }؛
            أضف_فروع({
                صـندوق({}).{
                    الطراز.{
                        النسق = "column"؛
                        الخلفية = لـون("eee")؛
                    }؛
                    أضف_فروع({
                        كـبسة_جانبية(نـص("-")).{ عند_نقر_الكبسة = مغلفة() {
                            هذا_الكائن.مبدال.بدل_إلى(مـشهد_عدد(نـص() + (--العدد_الحالي)), نـص("للخلف"))؛
                        } },
                        كـبسة_جانبية(نـص("+")).{ عند_نقر_الكبسة = مغلفة() {
                            هذا_الكائن.مبدال.بدل_إلى(مـشهد_عدد(نـص() + (++العدد_الحالي)), نـص("للأمام"))؛
                        } }
                    })؛
                },
                صـندوق({}).{
                    الطراز.{
                        المرونة = "auto"؛
                    }؛
                    أضف_فروع({
                        مـبدال().{
                            الطراز.{
                                المرونة = "auto"؛
                                الطول = "500pt"؛
                            }؛
                            حدد_الانتقال(نـص("للأمام"), أنشئ_انتقال_انزلاق_مبدال(0.5, 1، 0))؛
                            حدد_الانتقال(نـص("للخلف"), أنشئ_انتقال_انزلاق_مبدال(0.5, 1، 1))؛
                            بدل_إلى(مـشهد_عدد(نـص() + العدد_الحالي))؛
                            هذا_الكائن.مبدال = هذا؛
                        }
                    })؛
                }
            })؛
        }
    }

    عملية هذا_الصنف(): سـندنا[مـشهد_عداد] {
        أرجع سـندنا[مـشهد_عداد].أنشئ()؛
    }
}

//==============================================================================
// صـفحات واجهة المستخدم

@منفذ_مرئي["/*", "مثال منصة الويب"]
دالة رئيسي {
    حدد_ميزة_عنصر("body", "style", "padding: 0; margin: 0;")؛
    حمل_خط("AlususMono"، "/Assets/AlususMono.otf")؛

    عرف ترويسة: سـندنا[الـترويسة]؛
    نـافذة.النموذج.حدد_المشهد(صـندوق({}).{
        الطراز.{
            الإظهار = "flex"؛
            النسق = "column"؛
            المحاذاة = "center"؛
            الاتجاه = "rtl"؛
            فئة_الخط = "AlususMono"؛
        }؛
        أضف_فروع({
            الـترويسة().{ ترويسة = هذا },
            مـبدال_توجيه().{
                الطراز.{
                    العرض = "100%"؛
                    أقصى_عرض = "600pt"؛
                    أدنى_طول = "500pt"؛
                }؛
                حدد_الانتقال(
                    أنشئ_انتقال_انزلاق_مبدال(0.5, 0، 1),
                    أنشئ_انتقال_انزلاق_مبدال(0.5, 0، 0)
                )؛
                مسار("^/one") = مغلفة(حـمولة_مسار): سـندنا[مـركب] {
                    ترويسة.حدث_الدليل(0)؛
                    أرجع عـارض_صور()؛
                }؛
                مسار("^/two$") = مغلفة(حـمولة_مسار): سـندنا[مـركب] {
                    ترويسة.حدث_الدليل(1)؛
                    أرجع مـشهد_عداد()؛
                }؛
                مسار("^/three") = مغلفة(حـمولة_مسار): سـندنا[مـركب] {
                    ترويسة.حدث_الدليل(2)؛
                    أرجع مـشهد_نصي()؛
                }؛
            }
        })؛
    })؛

    نفذ_حلقة_معالجة_الأحداث()؛
}


طـرفية.اطبع("تشغيل الخادم على المنفذ 8010...\nURL: http://localhost:8010/\n")؛
شغل_الخادم({ "listening_ports"، "8010"، "static_file_max_age"، "0" })؛

