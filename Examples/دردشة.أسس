اشمل "مـحا"؛
مـحا.اشمل_ملف("Alusus/WebPlatform"، "مـنصة_ويب.أسس")؛
مـحا.اشمل_ملف("Alusus/Http"، "بـننف.أسس")؛
مـحا.اشمل_ملف("Alusus/Json"، "جـيسون.أسس")؛
اشمل "مـتم/سندات"؛
اشمل "مـتم/مـصفوفة"؛
اشمل "مـتم/نـص"؛
اشمل "مـتم/طـرفية"؛
اشمل "مغلفة"؛

استخدم مـتم؛
استخدم مـنصة_ويب؛

//==============================================================================
// الخادم

عرف _حد_الرسائل_: 12؛
عرف رسائل : مـصفوفة[نـص]؛

@منفذ_بياني["POST"، "/messages"]
دالة أضف_رسالة (اتصال:مؤشر[بـننف.اتـصال]){
    عرف بيانات: مصفوفة[مـحرف،1024]؛
    عرف حجم_البيانات: صحيح = بـننف.اقرأ(اتصال، بيانات~مؤشر، بيانات~حجم)؛
    إذا رسائل.هات_الطول() >= _حد_الرسائل_ رسائل.أزل(0)؛
    رسائل.أضف(نـص(بيانات~مؤشر، حجم_البيانات))؛
    بـننف.اطبع(اتصال، "HTTP/1.1 200 Ok\r\n\r\n")؛
}

@منفذ_بياني["GET"، "/messages"]
دالة هات_الرسائل (اتصال: مؤشر[بـننف.اتـصال]) {
    عرف الرد: نـص = نـص.ادمج(رسائل، "<br>")؛
    بـننف.اطبع(اتصال، "HTTP/1.1 200 Ok\r\n")؛
    بـننف.اطبع(اتصال، "Content-Type: text/plain\r\n")؛
    بـننف.اطبع(اتصال، "Cache-Control: no-cache\r\n")؛
    بـننف.اطبع(اتصال، "Content-Length: %d\r\n\r\n"، الرد.هات_الطول())؛
    بـننف.اطبع(اتصال، الرد.صوان)؛
}

@مسار_موارد عرف مسار_الموارد: "Assets/"؛


//=============================================================================
// مـركبات واجهة المستخدم

عرف خط_الكتابة: "30px Arial"؛ 

عرف لون_اصلي: لـون("8e50ef")؛
عرف لون_فاتح: لـون("c380ff")؛

صنف خـيار_القائمة {
    @حقنة عرف مركب: مـركب؛

    عملية هذا~هيئ(رابط: نـص, عنوان: نـص, أيقونة: نـص) {
        هذا.المشهد = صـندوق({}).{
            الطراز.{
                الحشوة = ربـاعية(3)؛
                الخلفية = لون_فاتح؛
                النسق = نـص("row")؛
                المحاذاة = نـص("center")؛
            }؛
            الطراز("icon").{
                الحشوة = ربـاعية(0, 4)؛
                العرض = نـص("20pt")؛
                الطول = نـص("0pt")؛
                الانتقال = نـص(".2s")؛
            }
            الطراز("label").{
                حجم_الخط = 16.0؛
                لون_الخط = لـون("eee")؛
            }
            الطراز({ مـحدد_حالة._حوم_, "icon" }).{
                الطول = نـص("20pt")؛
                الانتقال = نـص(".2s")؛
            }
            الطراز({ مـحدد_حالة._حوم_, "label" }).{
                حجم_الخط = 16.0؛
                لون_الخط = لـون("fff")؛
            }
            أضف_فروع({
                ارتـباط_تشعبي(رابط, عـنوان(عنوان).{
                    اسم_الفئة = نـص("label")
                }),
                ارتـباط_تشعبي(رابط, صـورة().{
                    الرابط = أيقونة؛
                    اسم_الفئة = نـص("icon")؛
                })
            })؛
        }؛
    }

    عملية هذا_الصنف(رابط: نـص, عنوان: نـص, أيقونة: نـص): سـندنا[خـيار_القائمة] {
        أرجع سـندنا[خـيار_القائمة]().{ احجز()~هيئ(رابط، عنوان، أيقونة) }؛
    }
}

صنف الـترويسة {
    @حقنة عرف مركب: مـركب؛

    عملية هذا~هيئ() {
        هذا.المشهد = صـندوق({}).{
            الطراز.{
                العرض = نـص("100%")؛
                الطول = نـص("85pt")؛
                الحشوة = ربـاعية(4، 0، 0، 0)؛
                الخلفية = لون_اصلي؛
                الإطار = ربـاعية(1.5)؛
                طراز_الإطار = نـص("solid")؛
                لون_الإطار = لون_اصلي؛
                ملء_السطر = نـص("space-between")؛
                النسق = نـص("column")؛
            }؛

            أضف_فروع({
                صـندوق({}).{
                    الطراز.{
                        النسق = نـص("row")؛
                        ملء_السطر = نـص("space-between")؛
                        المحاذاة = نـص("center")؛
                    }؛
                    أضف_فروع({
                        صـندوق({ عـنوان(نـص("أمثلة منصة ويب الأسس - الدردشة")).{ الطراز.{
                            لون_الخط = لـون("fff")؛
                            حجم_الخط = 21.0؛
                            الطول = نـص("100%")؛
                            الخلفية = لون_اصلي؛
                        } } })،
                        صـورة().{
                            الرابط = نـص("/Assets/wblogo.svg")؛
                            الطراز.{
                                الطول = نـص("50pt;")؛
                                الحشوة = ربـاعية(0، 10)؛
                            }؛
                        }
                    })؛
                }،
                صـندوق({}).{
                    الطراز.{
                        النسق = نـص("row")؛
                        ملء_السطر = نـص("space-between")؛
                    }؛
                    أضف_فروع({
                        خـيار_القائمة(نـص("/")، نـص("الرئيسية")، نـص("/Assets/chat.svg"))،
                        خـيار_القائمة(نـص("/about")، نـص("عن البرنامج")، نـص("/Assets/about.svg"))
                    })؛
                }
            })؛
        }؛
    }

    عملية هذا_الصنف(): سـندنا[الـترويسة] {
        أرجع سـندنا[الـترويسة].أنشئ()؛
    }
}

صنف مـدخل_نصي {
    @حقنة عرف مركب: مـركب؛

    عرف قد_تم_الإدخال: مغلفة (نـص)؛
    عرف مدخل_النص: سـندنا[مـدخل_نص]؛

    عملية هذا.العرض = نـص {
        هذا.المشهد.الطراز.العرض = قيمة؛
        أرجع قيمة؛
    }

    عملية هذا.الطول = نـص {
        هذا.المشهد.الطراز.الطول = قيمة؛
        أرجع قيمة؛
    }

    عملية هذا~هيئ() {
        عرف الكائن: سند[هذا_الصنف](هذا)؛

        هذا.المشهد = صـندوق({}).{
            الطراز.{
                النسق = نـص("row")؛
                الإطار = ربـاعية(1.5)؛
                طراز_الإطار = نـص("solid")؛
                لون_الإطار = لون_اصلي؛
                الخلفية = لون_اصلي؛
                ملء_السطر = نـص("space-between")؛
            }؛
            أضف_فروع({
                مـدخل_نص().{
                    الكائن.مدخل_النص = هذا؛
                    الطراز.{
                        العرض = نـص("100%")؛
                        الطول = نـص("100%")؛
                        الخلفية = لـون("fff")؛
                        حجم_الخط = 12.0؛
                    }؛
                    عند_انتهاء_الكبسة.اربط(مغلفة (ودجة: سند[مـدخل_نص]، الحمولة: سند[نـص]) {
                        إذا الحمولة == "Shift+Enter" {
                            عرف بيانات_جديده: نـص = نـص("- ") + رمّز_النص_الفائق(ودجة.هات_النص()).شذب()؛
                            ودجة.حدد_النص(نـص())؛
                            إذا ليس الكائن.قد_تم_الإدخال.أهو_لعدم() الكائن.قد_تم_الإدخال(بيانات_جديده)؛
                        }
                    })؛
                }،
                الـزر(نـص("أرسل")).{
                    الطراز.{
                        الطول = نـص("100%")؛
                        العرض = نـص("50pt")؛
                        الخلفية = لـون(200، 200، 200)؛
                        حجم_الخط = 16.0؛
                    }؛
                    عند_الضغط.اربط(مغلفة (ودجة: سند[الـزر]، الحمولة: سند[صحيح]) {
                        عرف بيانات_جديده: نـص = نـص("- ") + رمّز_النص_الفائق(الكائن.مدخل_النص.هات_النص()).شذب()؛
                        الكائن.مدخل_النص.حدد_النص(نـص())؛
                            إذا ليس الكائن.قد_تم_الإدخال.أهو_لعدم() الكائن.قد_تم_الإدخال(بيانات_جديده)؛
                    })؛
                }
            })؛
        }؛
    }

    عملية هذا_الصنف(): سـندنا[مـدخل_نصي] {
        أرجع سـندنا[مـدخل_نصي].أنشئ()؛
    }
}

دالة رمّز_النص_الفائق(نص: نـص): نـص {
    أرجع نص
        .استبدل("\ج"، "<br>")
        .استبدل("،"، "&comma;")
        .استبدل("\""، "&quote;")؛
}

//=============================================================================
// صـفحات واجهة المستخدم

عرف _جلب_: "GET"؛
عرف _إرسال_: "POST"؛
عرف _المسار_: "/messages"؛
عرف _ترويسة_صنف_بيانات_نصي_: "Content-Type: application/text"؛

@منفذ_مرئي["/"، "مثال منصة ويب - الدردشة"]
دالة رئيسي {
    حدد_ميزة_عنصر("body"، "style"، "padding: 0; margin: 0;")؛

    عرف عند_استلام_بيانات: مغلفة (جـيسون)؛

    نـافذة.النموذج.حدد_المشهد(صـندوق({}).{
        الطراز.{
            الطول = نـص("100%")؛
            ملء_السطر = نـص("space-between")؛
            الاتجاه = نـص("rtl")؛
            النسق = نـص("column")؛
        }؛
        أضف_فروع({
            الـترويسة()،
            صـندوق({}).{
                الطراز.{
                    العرض = نـص("100%")؛
                    الطول = نـص("auto-flex")؛
                    الحشوة = ربـاعية(5)؛
                    النسق = نـص("column")؛
                }
                عرف عنوان_إشعارات: سـندنا[عـنوان]؛
                أضف_فروع({
                    عـنوان(نـص()).{
                        عنوان_إشعارات = هذا؛
                        الطراز.{
                            العرض = نـص("100%")؛
                            الطول = نـص("20pt")؛
                            لون_الخط = لـون(200، 50، 50)؛
                            حجم_الخط = 10.0؛
                        }؛
                    }،
                    عـنوان(نـص()).{
                        الطراز.{
                            العرض = نـص("100%")؛
                            الطول = نـص("100%")؛
                            لون_الخط = لـون(50، 50، 50)؛
                            حجم_الخط = 20.0؛
                        }؛
                        عند_استلام_بيانات = مغلفة (جيسون: جـيسون) {
                            عرف الحالة: صحيح = جيسون.هات_كائن("eventData").هات_صحيح("status")؛
                            إذا الحالة >= 200 و الحالة < 300 {
                                عرف البيانات: نـص = جيسون.هات_كائن("eventData").هات_نص("body")؛
                                إذا هذا.هات_النص() != البيانات {
                                    هذا.حدد_النص(البيانات)؛
                                }
                                إذا عنوان_إشعارات.هات_النص() != "" {
                                    عنوان_إشعارات.حدد_النص(نـص(""))؛
                                }
                            } وإلا {
                                عنوان_إشعارات.حدد_النص(نـص("الاتصال مقطوع. رمز حالة ب.ن.ن.ف: ") + الحالة)؛
                            }
                        }؛
                    }
                })؛
            }،
            مـدخل_نصي().{
                العرض = نـص("100%")؛
                الطول = نـص("50pt")؛
                قد_تم_الإدخال = مغلفة (بيانات_جديده: نـص) {
                    أرسل_نداء(
                        _إرسال_، _المسار_، _ترويسة_صنف_بيانات_نصي_، بيانات_جديده، 10000، مغلفة(جـيسون) {}
                    )؛
                    أرسل_نداء(_جلب_، _المسار_، 0، 0، 500، عند_استلام_بيانات)؛
                }؛
            }
        })؛
    })؛
    
    ابدأ_المؤقت(500000، مغلفة (جـيسون) {
        أرسل_نداء(_جلب_، _المسار_، 0، 0، 500، عند_استلام_بيانات)؛
    })؛
    أرسل_نداء(_جلب_، _المسار_، 0، 0، 500، عند_استلام_بيانات)؛

     نفذ_حلقة_معالجة_الأحداث()؛    
}

@منفذ_مرئي["/about"، "مثال منصة ويب - الدردشة"]
دالة عن_البرنامج {
    حدد_ميزة_عنصر("body"، "style"، "padding: 0; margin: 0;")؛

    نـافذة.النموذج.حدد_المشهد(صـندوق({}).{
        الطراز.الاتجاه = نـص("rtl")؛
        الطراز.النسق = نـص("column")؛
        أضف_فروع({
            الـترويسة()،
            عـنوان(نـص("مثال الدردشة")).{ الطراز.{
                لون_الخط = لـون(0، 0، 0)؛
                حجم_الخط = 30.0؛
            } }،
            عـنوان(نـص("طُور باستخدام منصة ويب الأسس")).{ الطراز.{
                لون_الخط = لـون(0، 0، 0)؛
                حجم_الخط = 20.0؛
            } }
        })
    })؛

    نفذ_حلقة_معالجة_الأحداث()؛
}


طـرفية.اطبع("تشغيل الخادم على المنفذ 8010...\nURL: http://localhost:8010/\n")؛
شغل_الخادم ({ "listening_ports"، "8010"، "static_file_max_age"، "0" })؛

