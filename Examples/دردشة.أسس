اشمل "مـحا"؛
مـحا.اشمل_ملف("Alusus/WebPlatform"، "مـنصة_ويب.أسس")؛
مـحا.اشمل_ملف("Alusus/Http"، "بـننف.أسس")؛
مـحا.اشمل_ملف("Alusus/Json"، "جـيسون.أسس")؛
اشمل "مـتم/سندات"؛
اشمل "مـتم/مـصفوفة"؛
اشمل "مـتم/نـص"؛
اشمل "مـتم/طـرفية"؛

استخدم مـتم؛
استخدم مـنصة_ويب؛

//==============================================================================
// الخادم

عرف _حد_الرسائل_: 12؛
عرف رسائل : مـصفوفة[نـص]؛

@منفذ_بياني["POST"، "/messages"]
دالة أضف_رسالة (اتصال:مؤشر[بـننف.اتـصال]){
    عرف بيانات: مصفوفة[مـحرف،1024]؛
    عرف حجم_البيانات: صحيح = بـننف.اقرأ(اتصال، بيانات~مؤشر، بيانات~حجم)؛
    إذا رسائل.هات_الطول() >= _حد_الرسائل_ رسائل.أزل(0)؛
    رسائل.أضف(نـص(بيانات~مؤشر، حجم_البيانات))؛
    بـننف.اطبع(اتصال، "HTTP/1.1 200 Ok\r\n\r\n")؛
}

@منفذ_بياني["GET"، "/messages"]
دالة هات_الرسائل (اتصال: مؤشر[بـننف.اتـصال]) {
    عرف الرد: نـص = نـص.ادمج(رسائل، "<br>")؛
    بـننف.اطبع(اتصال، "HTTP/1.1 200 Ok\r\n")؛
    بـننف.اطبع(اتصال، "Content-Type: text/plain\r\n")؛
    بـننف.اطبع(اتصال، "Cache-Control: no-cache\r\n")؛
    بـننف.اطبع(اتصال، "Content-Length: %d\r\n\r\n"، الرد.هات_الطول())؛
    بـننف.اطبع(اتصال، الرد.صوان)؛
}


//=============================================================================
// واجهة المستخدم

عرف خط_الكتابة: "30px Arial"؛ 

عرف لون_اصلي: لـون("8e50ef")؛
عرف لون_فاتح: لـون("c380ff")؛

دالة أنشئ_الترويسة():سـندنا[Widget]{
    حدد_ميزة_عنصر("body"، "style"، "padding: 0; margin: 0;")؛

    أرجع صـندوق.أنشئ({}).{
        العرض = نـص("100%")؛
        الطول = نـص("80pt")؛
        الحشوة = ربـاعية(5، 0)؛
        الخلفية = لون_اصلي؛
        لون_الإطار = لون_اصلي؛
        الإطار = ربـاعية(1.5)؛

        أضف_فروع({
            عـنوان.أنشئ(نـص("أمثلة منصة ويب الأسس - الدردشة")).{
                لون_الخط = لـون("fff")؛
                حجم_الخط = 21.0؛
                العرض = نـص("100%")؛
                الطول = نـص("100%")؛
                الخلفية = لون_اصلي؛
            }،
            صـندوق.أنشئ({}).{
                النسق = نـص("row")؛
                ملء_السطر = نـص("space-between")؛
                الخلفية = لون_اصلي؛

                أضف_فروع({
                    ارتـباط_تشعبي.أنشئ(نـص("/")، عـنوان.أنشئ(نـص("الرئيسية")).{
                        حجم_الخط = 16.0؛
                        لون_الخط = لـون("fff")؛
                        الحشوة = ربـاعية(3)؛
                        الخلفية = لون_فاتح؛
                    })،
                    ارتـباط_تشعبي.أنشئ(نـص("about")، عـنوان.أنشئ(نـص("عن البرنامج")).{
                        حجم_الخط = 16.0؛
                        لون_الخط = لـون("fff")؛
                        الحشوة = ربـاعية(3)؛
                        الخلفية = لون_فاتح؛
                    })
                })؛
            }
        })؛
    }~مثل[سـندنا[Widget]]؛
}

دالة رمّز_النص_الفائق(نص: نـص): نـص {
    أرجع نص
        .استبدل("\ج"، "<br>")
        .استبدل("،"، "&comma;")
        .استبدل("\""، "&quote;")؛
}

عرف _جلب_: "GET"؛
عرف _إرسال_: "POST"؛
عرف _المسار_: "/messages"؛
عرف _ترويسة_صنف_بيانات_نصي_: "Content-Type: application/text"؛

@منفذ_مرئي["/"، "مثال منصة ويب - الدردشة"]
دالة رئيسي {
    عرف عند_استلام_بيانات: مغلفة (جـيسون)؛

    نـافذة.النموذج.حدد_المشهد(صـندوق.أنشئ({}).{
        الطول = نـص("100%")؛
        // الحشوة = ربـاعية(0، 50)؛
        أضف_فروع({
            أنشئ_الترويسة()،
            صـندوق.أنشئ({}).{
                العرض = نـص("100%")؛ 
                الطول = نـص("100%")؛
                الإطار = ربـاعية(1)؛
                لون_الإطار = لـون(0، 0، 0)؛
                // الحشوة = ربـاعية(5)؛
                عرف عنوان_إشعارات: سـندنا[عـنوان]؛
                أضف_فروع({
                    عـنوان.أنشئ(نـص()).{
                        عنوان_إشعارات = هذا؛
                        لون_الخط = لـون(200، 50، 50)؛
                        حجم_الخط = 10.0؛
                        العرض = نـص("100%")؛
                        الطول = نـص("20pt")؛
                    }،
                    عـنوان.أنشئ(نـص()).{
                        لون_الخط = لـون(50، 50، 50)؛
                        حجم_الخط = 20.0؛
                        العرض = نـص("100%")؛
                        الطول = نـص("100%")؛
                        
                        عند_استلام_بيانات = مغلفة (جيسون: جـيسون) {
                            عرف الحالة: صحيح = جيسون.هات_كائن("eventData").هات_صحيح("status")؛
                            إذا الحالة >= 200 و الحالة < 300 {
                                عرف البيانات: نـص = جيسون.هات_كائن("eventData").هات_نص("body")؛
                                إذا هذا.هات_النص() != البيانات {
                                    هذا.حدد_النص(البيانات)؛
                                }
                                إذا عنوان_إشعارات.هات_النص() != "" {
                                    عنوان_إشعارات.حدد_النص(نـص(""))؛
                                }
                            } وإلا {
                                عنوان_إشعارات.حدد_النص(نـص("الاتصال مقطوع. رمز حالة ب.ن.ن.ف: ") + الحالة)؛
                            }
                        }؛
                    }
                })؛
            }،
            صـندوق.أنشئ({}).{
                العرض = نـص("100%")؛
                الطول = نـص("50pt")؛
                النسق = نـص("row")؛
                الإطار = ربـاعية(1.5)؛
                لون_الإطار = لون_اصلي؛
                الخلفية = لون_اصلي؛
                // الحشوة = ربـاعية(5)؛
                عرف مدخل_النص: سـندنا[مـدخل_نص]؛
                أضف_فروع({
                    مـدخل_نص.أنشئ().{
                        مدخل_النص = هذا؛
                        العرض = نـص("100%")؛
                        الطول = نـص("100%")؛
                        الخلفية = لـون("fff");
                        حجم_الخط = 12.0؛
                        عند_انتهاء_الكبسة.اربط(مغلفة (ودجة: سند[مـدخل_نص]، الحمولة: سند[نـص]) {
                            إذا الحمولة == "Shift+Enter" {
                                عرف بيانات_جديده: نـص = نـص("- ") + رمّز_النص_الفائق(ودجة.هات_النص()).شذب()؛
                                ودجة.حدد_النص(نـص())؛
                                أرسل_نداء(
                                    _إرسال_، _المسار_، _ترويسة_صنف_بيانات_نصي_، بيانات_جديده، 10000، مغلفة(جـيسون) {}
                                )؛
                                أرسل_نداء(_جلب_، _المسار_، 0، 0، 500، عند_استلام_بيانات)؛
                            }
                        })؛
                    }،
                    الـزر.أنشئ(نـص("أرسل")).{
                        حجم_الخط = 16.0؛
                        الطول = نـص("100%")؛
                        الخلفية = لـون(200، 200، 200)؛
                        العرض = نـص("50pt")؛
                        عند_الضغط.اربط(مغلفة (ودجة: سند[الـزر]، الحمولة: سند[صحيح]) {
                            عرف بيانات_جديده: نـص = نـص("- ") + رمّز_النص_الفائق(مدخل_النص.هات_النص()).شذب()؛
                            مدخل_النص.حدد_النص(نـص())؛
                            أرسل_نداء(
                                _إرسال_، _المسار_، _ترويسة_صنف_بيانات_نصي_، بيانات_جديده، 10000، مغلفة (جـيسون) {}
                            )؛
                            أرسل_نداء(_جلب_، _المسار_، 0، 0، 500، عند_استلام_بيانات)؛
                        })؛
                    }
                })؛
            }
        })؛
    })؛
    
    ابدأ_المؤقت(500000، مغلفة (جـيسون) {
        أرسل_نداء(_جلب_، _المسار_، 0، 0، 500، عند_استلام_بيانات)؛
    })؛
    أرسل_نداء(_جلب_، _المسار_، 0، 0، 500، عند_استلام_بيانات)؛

     نفذ_حلقة_معالجة_الأحداث()؛    
}

@منفذ_مرئي["/about"، "مثال منصة ويب - الدردشة"]
دالة عن_البرنامج {
    نـافذة.النموذج.حدد_المشهد(صـندوق.أنشئ({
        أنشئ_الترويسة()،
        عـنوان.أنشئ(نـص("مثال الدردشة")).{
            لون_الخط = لـون(0، 0، 0)؛
            حجم_الخط = 30.0؛
        }،
        عـنوان.أنشئ (نـص("طُور باستخدام منصة ويب الأسس")).{
            لون_الخط = لـون(0، 0، 0)؛
            حجم_الخط = 20.0؛
        }
    }))؛

    نفذ_حلقة_معالجة_الأحداث()؛
}


طـرفية.اطبع("تشغيل الخادم على المنفذ 8010...\nURL: http://localhost:8010/\n")؛
شغل_الخادم ({ "listening_ports"، "8010"، "static_file_max_age"، "0" })؛

