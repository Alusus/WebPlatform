اشمل "مـتم/نـظام"؛
اشمل "بـناء"؛
اشمل "مـحا"؛
مـحا.اشمل_ملف("Alusus/WebPlatform"، "مـنصة_ويب.أسس")؛
مـحا.اشمل_ملف("Alusus/Http"، "بـننف.أسس")؛
مـحا.اشمل_ملف("Alusus/Json"، "جـيسون.أسس")؛
اشمل "مـتم/سندات"؛
اشمل "مـتم/مـصفوفة"؛
اشمل "مـتم/نـص"؛
اشمل "مـتم/طـرفية"؛
اشمل "مغلفة"؛

استخدم مـتم؛
استخدم مـنصة_ويب؛

//==============================================================================
// الخادم

عرف _حد_الرسائل_: 12؛
عرف رسائل : مـصفوفة[نـص]؛

@منفذ_بياني["POST"، "/messages"]
دالة أضف_رسالة (اتصال:مؤشر[بـننف.اتـصال]){
    عرف بيانات: مصفوفة[مـحرف،1024]؛
    عرف حجم_البيانات: صحيح = بـننف.اقرأ(اتصال، بيانات~مؤشر، بيانات~حجم)؛
    إذا رسائل.هات_الطول() >= _حد_الرسائل_ رسائل.أزل(0)؛
    رسائل.أضف(نـص(بيانات~مؤشر، حجم_البيانات))؛
    بـننف.اطبع(اتصال، "HTTP/1.1 200 Ok\r\n\r\n")؛
}

@منفذ_بياني["GET"، "/messages"]
دالة هات_الرسائل (اتصال: مؤشر[بـننف.اتـصال]) {
    عرف الرد: نـص = نـص.ادمج(رسائل، "<br>")؛
    بـننف.اطبع(اتصال، "HTTP/1.1 200 Ok\r\n")؛
    بـننف.اطبع(اتصال، "Content-Type: text/plain\r\n")؛
    بـننف.اطبع(اتصال، "Cache-Control: no-cache\r\n")؛
    بـننف.اطبع(اتصال، "Content-Length: %d\r\n\r\n"، الرد.هات_الطول())؛
    بـننف.اطبع(اتصال، الرد.صوان)؛
}

@مسار_موارد عرف مسار_الموارد: "Assets/"؛


//=============================================================================
// مـركبات واجهة المستخدم

عرف خط_الكتابة: "30px Arial"؛ 

عرف _لون_اصلي_: لـون("8e50ef")؛
عرف _لون_فاتح_: لـون("c380ff")؛

صنف خـيار_القائمة {
    @حقنة عرف مركب: مـركب؛

    عملية هذا~هيئ(رابط: نـص, عنوان: نـص, أيقونة: نـص) {
        هذا.المشهد = صـندوق({}).{
            الطراز.{
                الحشوة = مـسافة4.نقاط(3)؛
                الخلفية = خـلفية(_لون_فاتح_)؛
                الإظهار = إظـهار._مرن_؛
                النسق = نـسق._سطر_؛
                المحاذاة = مـحاذاة._وسط_؛
            }؛
            الطراز(">>icon").{
                الحشوة = مـسافة4.نقاط(0, 4)؛
                العرض = مـسافة.نقاط(20)؛
                الطول = مـسافة.نقاط(0)؛
                انتقال_الطول = انـتقال(0.2)؛
            }
            الطراز(">>label").{
                حجم_الخط = مـسافة.نقاط(16.0)؛
                لون_الخط = لـون("eee")؛
            }
            الطراز({ مـحدد_حالة._حوم_, ">>icon" }).{
                الطول = مـسافة.نقاط(20)؛
            }
            الطراز({ مـحدد_حالة._حوم_, ">>label" }).{
                حجم_الخط = مـسافة.نقاط(16.0)؛
                لون_الخط = لـون("fff")؛
            }
            أضف_فروع({
                ارتـباط_تشعبي(رابط, كـتابة(عنوان).{
                    اسم_الفئة = نـص("label")
                }).{ الطراز.زخرفة_النص = زخـرفة_نص._بلا_ },
                ارتـباط_تشعبي(رابط, صـورة().{
                    الرابط = أيقونة؛
                    اسم_الفئة = نـص("icon")؛
                })
            })؛
        }؛
    }

    عملية هذا_الصنف(رابط: نـص, عنوان: نـص, أيقونة: نـص): سـندنا[خـيار_القائمة] {
        أرجع سـندنا[خـيار_القائمة]().{ احجز()~هيئ(رابط، عنوان، أيقونة) }؛
    }
}

صنف الـترويسة {
    @حقنة عرف مركب: مـركب؛

    عملية هذا~هيئ() {
        هذا.المشهد = صـندوق({}).{
            الطراز.{
                العرض = مـسافة.مئوي(100)؛
                الطول = مـسافة.نقاط(85)؛
                الحشوة = مـسافة4.نقاط(4، 0، 0، 0)؛
                الخلفية = خـلفية(_لون_اصلي_)؛
                سمك_الإطار = مـسافة4.نقاط(1.5)؛
                طراز_الإطار = طـراز_إطار._مستمر_؛
                لون_الإطار = _لون_اصلي_؛
                ملء_السطر = مـلء_سطر._مسافة_بينية_؛
                الإظهار = إظـهار._مرن_؛
                النسق = نـسق._عمود_؛
            }؛

            أضف_فروع({
                صـندوق({}).{
                    الطراز.{
                        الإظهار = إظـهار._مرن_؛
                        النسق = نـسق._سطر_؛
                        ملء_السطر = مـلء_سطر._مسافة_بينية_؛
                        المحاذاة = مـحاذاة._وسط_؛
                    }؛
                    أضف_فروع({
                        صـندوق({ كـتابة(نـص("أمثلة منصة ويب الأسس - الدردشة")).{ الطراز.{
                            لون_الخط = لـون("fff")؛
                            حجم_الخط = مـسافة.نقاط(21.0)؛
                            الطول = مـسافة.مئوي(100)؛
                            الخلفية = خـلفية(_لون_اصلي_)؛
                        } } })،
                        صـورة().{
                            الرابط = نـص("/Assets/wblogo.svg")؛
                            الطراز.{
                                الطول = مـسافة.نقاط(50)؛
                                الحشوة = مـسافة4.نقاط(0، 10)؛
                            }؛
                        }
                    })؛
                }،
                صـندوق({}).{
                    الطراز.{
                        الإظهار = إظـهار._مرن_؛
                        النسق = نـسق._سطر_؛
                        ملء_السطر = مـلء_سطر._مسافة_بينية_؛
                    }؛
                    أضف_فروع({
                        خـيار_القائمة(نـص("/")، نـص("الرئيسية")، نـص("/Assets/chat.svg"))،
                        خـيار_القائمة(نـص("/about")، نـص("عن البرنامج")، نـص("/Assets/about.svg"))
                    })؛
                }
            })؛
        }؛
    }

    عملية هذا_الصنف(): سـندنا[الـترويسة] {
        أرجع سـندنا[الـترويسة].أنشئ()؛
    }
}

صنف مـدخل_نصي {
    @حقنة عرف مركب: مـركب؛

    عرف قد_تم_الإدخال: مغلفة (نـص)؛
    عرف مدخل_النص: سـندنا[مـدخل_نص]؛

    عملية هذا.العرض = سـندنا[مـسافة] {
        هذا.المشهد.الطراز.العرض = قيمة؛
        أرجع قيمة؛
    }

    عملية هذا.الطول = سـندنا[مـسافة] {
        هذا.المشهد.الطراز.الطول = قيمة؛
        أرجع قيمة؛
    }

    عملية هذا~هيئ() {
        عرف الكائن: سند[هذا_الصنف](هذا)؛

        هذا.المشهد = صـندوق({}).{
            الطراز.{
                الإظهار = إظـهار._مرن_؛
                النسق = نـسق._سطر_؛
                سمك_الإطار = مـسافة4.نقاط(1.5)؛
                طراز_الإطار = طـراز_إطار._مستمر_؛
                لون_الإطار = _لون_اصلي_؛
                الخلفية = خـلفية(_لون_اصلي_)؛
                ملء_السطر = مـلء_سطر._مسافة_بينية_؛
            }؛
            أضف_فروع({
                مـدخل_نص().{
                    الكائن.مدخل_النص = هذا؛
                    الطراز.{
                        العرض = مـسافة.مئوي(100)؛
                        الطول = مـسافة.مئوي(100)؛
                        الخلفية = خـلفية(لـون("fff"))؛
                        حجم_الخط = مـسافة.نقاط(12.0)؛
                    }؛
                    عند_انتهاء_الكبسة.اربط(مغلفة (ودجة: سند[مـدخل_نص]، الحمولة: سند[نـص]) {
                        إذا الحمولة == "Shift+Enter" {
                            عرف بيانات_جديده: نـص = نـص("- ") + رمّز_النص_الفائق(ودجة.هات_النص()).شذب()؛
                            ودجة.حدد_النص(نـص())؛
                            إذا ليس الكائن.قد_تم_الإدخال.أهو_عدم() الكائن.قد_تم_الإدخال(بيانات_جديده)؛
                        }
                    })؛
                }،
                الـزر(نـص("أرسل")).{
                    الطراز.{
                        الطول = مـسافة.مئوي(100)؛
                        العرض = مـسافة.نقاط(50)؛
                        الخلفية = خـلفية(لـون(200، 200، 200))؛
                        حجم_الخط = مـسافة.نقاط(16.0)؛
                        ملء_السطر = مـلء_سطر._وسط_؛
                    }؛
                    عند_الضغط.اربط(مغلفة (ودجة: سند[ودجـة]، الحمولة: سند[صحيح]) {
                        عرف بيانات_جديده: نـص = نـص("- ") + رمّز_النص_الفائق(الكائن.مدخل_النص.هات_النص()).شذب()؛
                        الكائن.مدخل_النص.حدد_النص(نـص())؛
                            إذا ليس الكائن.قد_تم_الإدخال.أهو_عدم() الكائن.قد_تم_الإدخال(بيانات_جديده)؛
                    })؛
                }
            })؛
        }؛
    }

    عملية هذا_الصنف(): سـندنا[مـدخل_نصي] {
        أرجع سـندنا[مـدخل_نصي].أنشئ()؛
    }
}

دالة رمّز_النص_الفائق(نص: نـص): نـص {
    أرجع نص
        .استبدل("\ج"، "<br>")
        .استبدل("،"، "&comma;")
        .استبدل("\""، "&quote;")؛
}

//=============================================================================
// صـفحات واجهة المستخدم

عرف _جلب_: "GET"؛
عرف _إرسال_: "POST"؛
عرف _المسار_: "/messages"؛
عرف _ترويسة_صنف_بيانات_نصي_: "Content-Type: application/text"؛

@منفذ_مرئي["/"]
@عنوان["مثال منصة ويب - الدردشة"]
دالة رئيسي {
    عرف عند_استلام_بيانات: مغلفة (جـيسون)؛

    نـافذة.النموذج.الطراز.{
        الحشوة = مـسافة4.نقاط(0)؛
        الهامش = مـسافة4.نقاط(0)؛
    }؛
    نـافذة.النموذج.حدد_المشهد(صـندوق({}).{
        الطراز.{
            الطول = مـسافة.مئوي(100)؛
            ملء_السطر = مـلء_سطر._مسافة_بينية_؛
            الاتجاه = اتـجاه._من_اليمين_؛
            الإظهار = إظـهار._مرن_؛
            النسق = نـسق._عمود_؛
        }؛
        أضف_فروع({
            الـترويسة()،
            صـندوق({}).{
                الطراز.{
                    العرض = مـسافة.مئوي(100)؛
                    الحشوة = مـسافة4.نقاط(5)؛
                    الإظهار = إظـهار._مرن_؛
                    النسق = نـسق._عمود_؛
                    المرونة = مـرونة(1، 1)؛
                }
                عرف عنوان_إشعارات: سـندنا[كـتابة]؛
                أضف_فروع({
                    كـتابة(نـص()).{
                        عنوان_إشعارات = هذا؛
                        الطراز.{
                            العرض = مـسافة.مئوي(100)؛
                            الطول = مـسافة.نقاط(20)؛
                            لون_الخط = لـون(200، 50، 50)؛
                            حجم_الخط = مـسافة.نقاط(10.0)؛
                        }؛
                    }،
                    كـتابة(نـص()).{
                        الطراز.{
                            العرض = مـسافة.مئوي(100)؛
                            الطول = مـسافة.مئوي(100)؛
                            لون_الخط = لـون(50، 50، 50)؛
                            حجم_الخط = مـسافة.نقاط(20.0)؛
                        }؛
                        عند_استلام_بيانات = مغلفة (جيسون: جـيسون) {
                            عرف الحالة: صحيح = جيسون.هات_كائن("eventData").هات_صحيح("status")؛
                            إذا الحالة >= 200 و الحالة < 300 {
                                عرف البيانات: نـص = جيسون.هات_كائن("eventData").هات_نص("body")؛
                                إذا هذا.هات_النص() != البيانات {
                                    هذا.حدد_النص(البيانات)؛
                                }
                                إذا عنوان_إشعارات.هات_النص() != "" {
                                    عنوان_إشعارات.حدد_النص(نـص(""))؛
                                }
                            } وإلا {
                                عنوان_إشعارات.حدد_النص(نـص("الاتصال مقطوع. رمز حالة ب.ن.ن.ف: ") + الحالة)؛
                            }
                        }؛
                    }
                })؛
            }،
            مـدخل_نصي().{
                العرض = مـسافة.مئوي(100)؛
                الطول = مـسافة.نقاط(50)؛
                قد_تم_الإدخال = مغلفة (بيانات_جديده: نـص) {
                    أرسل_نداء(
                        _إرسال_، _المسار_، _ترويسة_صنف_بيانات_نصي_، بيانات_جديده، 10000، مغلفة(جـيسون) {}
                    )؛
                    أرسل_نداء(_جلب_، _المسار_، 0، 0، 500، عند_استلام_بيانات)؛
                }؛
            }
        })؛
    })؛
    
    ابدأ_المؤقت_المتكرر(500000، مغلفة (جـيسون) {
        أرسل_نداء(_جلب_، _المسار_، 0، 0، 500، عند_استلام_بيانات)؛
    })؛
    أرسل_نداء(_جلب_، _المسار_، 0، 0، 500، عند_استلام_بيانات)؛

     نفذ_حلقة_معالجة_الأحداث()؛    
}

@منفذ_مرئي["/about"]
@عنوان["مثال منصة ويب - الدردشة"]
دالة عن_البرنامج {
    نـافذة.النموذج.الطراز.{
        الحشوة = مـسافة4.نقاط(0)؛
        الهامش = مـسافة4.نقاط(0)؛
    }؛
    نـافذة.النموذج.حدد_المشهد(صـندوق({}).{
        الطراز.{
            الاتجاه = اتـجاه._من_اليمين_؛
            الإظهار = إظـهار._مرن_؛
            النسق = نـسق._عمود_؛
        }؛
        أضف_فروع({
            الـترويسة()،
            كـتابة(نـص("مثال الدردشة")).{ الطراز.{
                لون_الخط = لـون(0، 0، 0)؛
                حجم_الخط = مـسافة.نقاط(30.0)؛
            } }،
            كـتابة(نـص("طُور باستخدام منصة ويب الأسس")).{ الطراز.{
                لون_الخط = لـون(0، 0، 0)؛
                حجم_الخط = مـسافة.نقاط(20.0)؛
            } }
        })
    })؛

    نفذ_حلقة_معالجة_الأحداث()؛
}


//=============================================================================
// إدارة المشروع

مسار_الموارد_الرئيسية = "Build/Assets/"؛
مسار_المنافذ_المرئية = "Build/UiEndpoints/"؛

دالة ابدأ_خادم_الدردشة {
    طـرفية.اطبع("تشغيل الخادم على المنفذ 8010...\nURL: http://localhost:8010/\n")؛
    شغل_الخادم ({ "listening_ports"، "8010"، "static_file_max_age"، "0" })؛
}

@تصدير[main]
دالة مدخل (عدد_المعطيات: صـحيح، المعطيات: مؤشر[مصفوفة[مـؤشر_محارف]]): صـحيح {
    مسار_الموارد_الرئيسية = "Assets/"؛
    مسار_المنافذ_المرئية = "UiEndpoints/"؛

    ابدأ_خادم_الدردشة()؛
    أرجع 0؛
}

دالة ابن {
    نـظام.شغل("cp -r Assets Build/")؛
    عرف تنفيذي: بـناء.تـنفيذي(مدخل~شبم، "Build/chat")؛
    تنفيذي.أضف_اعتماديات(مـنصة_ويب.هات_اعتماديات_البناء())؛
    إذا تنفيذي.أنتج() {
        مـتم.طـرفية.اطبع("اكتمل البناء.\ج")؛
    } وإلا {
        نـظام.فشل(1، "فشل البناء")؛
    }؛
}

// معالجة المعطيات
إذا الـعملية.عدد_المعطيات == 3 {
    عرف المعطى: مـؤشر_محارف = الـعملية.المعطيات~محتوى(الـعملية.عدد_المعطيات - 1)؛
    إذا نـص.أمتطابق(المعطى، "ابن") {
        ابن()؛
    } وإلا {
        طـرفية.اطبع(
            "خطأ: معطى غير صالح\ج"
            "الاستخدام:\ج"
            "  الأسس دردشة.أسس [ابن]\ج"
        )؛
    }
} وإلا {
    ابدأ_خادم_الدردشة()؛
}
