اشمل "مـحا"؛
مـحا.اشمل_ملف("Alusus/WebPlatform", "مـنصة_ويب.أسس");
مـحا.اشمل_ملف("Alusus/Http", "بـننف.أسس");

اشمل "مـتم/سندات"؛
اشمل "مـتم/مـصفوفة"؛
اشمل "مـتم/نـص";
اشمل "مـتم/طـرفية";

استخدم مـتم;
استخدم مـنصة_ويب;

//==============================================================================
// الخادم

عرف رسالة : نـص;

@منفذ_بياني["POST", "/messages"]
دالة ارسال_الرسالة (اتصال:مؤشر[بـننف.اتـصال]){
    عرف ارسال_البيانات: مصفوفة[مـحرف,1024];
   بـننف.اقرأ(اتصال, ارسال_البيانات~مؤشر, ارسال_البيانات~حجم);
    إذا رسالة.هات_الطول() > 0 رسالة += "<br>";
     رسالة +=  ارسال_البيانات~مؤشر;
   بـننف.اطبع(اتصال, "HTTP/1.1 200 Ok\r\n\r\n");
}

@منفذ_بياني["GET", "/messages"]
دالة هات_الرسالة (اتصال: مؤشر[بـننف.اتـصال]) {
    بـننف.اطبع(اتصال, "HTTP/1.1 200 Ok\r\n");
    بـننف.اطبع(اتصال, "Content-Type: text/plain\r\n");
    بـننف.اطبع(اتصال, "Cache-Control: no-cache\r\n");
    بـننف.اطبع(اتصال, "Content-Length: %d\r\n\r\n", رسالة.هات_الطول());
    بـننف.اطبع(اتصال, رسالة.صوان);
}


//=============================================================================
// واجهة المستخدم

عرف خط_الكتابة: "30px Arial"; 

عرف لون_اصلي: لـون("8e50ef");
عرف لون_فاتح: لـون("c380ff");

دالة أنشاء_مقدمة():سـندنا[Widget]{
    حدد_ميزة_عنصر("body", "style", "padding: 0; margin: 0;");
    عرف التسمية : سـندنا[عـنوان] = عـنوان.أنشئ(نـص("أمثلة منصة ويب الأسس - الدردشة"));
    التسمية.حدد_لون_الخط(لـون("fff"));
    التسمية.حدد_حجم_الخط(21.0);
    التسمية.حدد_العرض(نـص("100%")); 
    التسمية.حدد_الطول(نـص("100%"));
    التسمية.حدد_لون_الخلفية(لون_اصلي);
    
    عرف الرابط_الاساسي: سـندنا[عـنوان] = عـنوان.أنشئ(نـص("الرئيسية"));
    الرابط_الاساسي.حدد_حجم_الخط(16.0);
    الرابط_الاساسي.حدد_لون_الخط(لـون("fff"));
    الرابط_الاساسي.حدد_الحشوة(ربـاعية(3));
    الرابط_الاساسي.حدد_لون_الخلفية(لون_فاتح); 
    عرف عن_الرابط: سـندنا[عـنوان] = عـنوان.أنشئ(نـص("عن البرنامج"));
    عن_الرابط.حدد_حجم_الخط(16.0);
    عن_الرابط.حدد_لون_الخط(لـون("fff"));
    عن_الرابط.حدد_الحشوة(ربـاعية(3));
    عن_الرابط.حدد_لون_الخلفية(لون_فاتح);
    
    عرف صندوق_القائمة: سـندنا[صـندوق] = صـندوق.أنشى({
        ارتـباط_تشعبي.أنشئ(نـص("/"), الرابط_الاساسي),
        ارتـباط_تشعبي.أنشئ(نـص("about"), عن_الرابط)
    });
    صندوق_القائمة.حدد_النسق(نـص("row"));
    صندوق_القائمة.حدد_ملء_السطر(نـص("space-between"));
    صندوق_القائمة.حدد_لون_الخلفية(لون_اصلي);
    
    عرف صندوق: سـندنا[صـندوق] = صـندوق.أنشى({التسمية, صندوق_القائمة});
    صندوق.حدد_العرض(نـص("100%"));
    صندوق.حدد_الطول(نـص("80pt"));
    صندوق.حدد_الحشوة(ربـاعية(5, 0));
    صندوق.حدد_لون_الخلفية(لون_اصلي);
    صندوق.حدد_لون_الإطار(لون_اصلي);
    صندوق.حدد_الإطار(ربـاعية(1.5));
    
    أرجع صندوق~مثل[سـندنا[Widget]];
}

@منفذ_مرئي["/", "مثال منصة ويب - الدردشة"]
دالة رئيسي {
    عرف التسمية: سـندنا[عـنوان] = عـنوان.أنشئ(نـص());
    التسمية.حدد_لون_الخط(لـون(50, 50, 50));
    التسمية.حدد_حجم_الخط(20.0);
    التسمية.حدد_العرض(نـص("100%"));
    التسمية.حدد_الطول(نـص("100%"));
    
    عرف أدخال_نص: سـندنا[مـدخل_نص] = مـدخل_نص.أنشئ(); 
    أدخال_نص.حدد_العرض(نـص("100%"));
    أدخال_نص.حدد_الطول(نـص("100%"));
    أدخال_نص.حدد_حجم_الخط(12.0);
    أدخال_نص.عند_انتهاء_الكبسة.اربط(عند_المفتاح~مؤشر, التسمية.كائن~مؤشر);
 
    عرف الزر: سـندنا[الـزر] = الـزر.أنشئ(نـص("أرسل"));
    عرف انقر_فوق_الزر: انـقر_فوق_الزر;
    انقر_فوق_الزر.التسمية~عطل_التتبع = التسمية.كائن;
    انقر_فوق_الزر.صندوق_الادخال~عطل_التتبع = أدخال_نص.كائن; 
    الزر.عند_الضغط.اربط(عند_ضغط_الزر~مؤشر, انقر_فوق_الزر~مؤشر);
    الزر.حدد_حجم_الخط(16.0);
    الزر.حدد_الطول(نـص("100%")); 
    الزر.حدد_لون_الخلفية(لـون(200, 200, 200)); 
    الزر.حدد_العرض(نـص("50pt"));
    
    عرف عرض_الصندوق: سـندنا[صـندوق] = صـندوق.أنشى({التسمية});
    عرض_الصندوق.حدد_العرض(نـص("100%")); 
    عرض_الصندوق.حدد_الطول(نـص("100%"));
    عرض_الصندوق.حدد_الإطار(ربـاعية(1));
    عرض_الصندوق.حدد_لون_الإطار(لـون(0, 0, 0));
    // عرض_الصندوق.حدد_الحشوة(ربـاعية(5));
    
    عرف صندوق_الادخال: سـندنا[صـندوق] = صـندوق.أنشى({أدخال_نص, الزر});
    صندوق_الادخال.حدد_العرض(نـص("100%"));
    صندوق_الادخال.حدد_الطول(نـص("50pt"));
    صندوق_الادخال.حدد_النسق(نـص("row"));
    صندوق_الادخال.حدد_الإطار(ربـاعية(1.5));
    صندوق_الادخال.حدد_لون_الإطار(لون_اصلي);
    صندوق_الادخال.حدد_لون_الخلفية(لون_اصلي);
    // صندوق_الادخال.حدد_الحشوة(ربـاعية(5));
    
    عرف صندوق: سـندنا[صـندوق] = صـندوق.أنشى({أنشاء_مقدمة(), عرض_الصندوق, صندوق_الادخال});
    صندوق.حدد_الطول(نـص("100%"));
    // صندوق.حدد_الحشوة(ربـاعية(0, 50));
     
    حدد_المشهد(صندوق);
    
    عرف مقبس: مـقبس[فـراغ ,Json]; 
    مقبس.الدالة = على_الوقت~مؤشر;
    مقبس.إضافية = التسمية.كائن~مؤشر;
    ابدأ_المؤقت(500000, مقبس~مؤشر);
    
     نفذ_حلقة_معالجة_الأحداث();    
}

صنف انـقر_فوق_الزر {
    عرف التسمية: سند[عـنوان];
    عرف صندوق_الادخال: سند[مـدخل_نص];
}

ماكرو هيئ_معطى_ممثل [اسم_المتغير, نوع_المتغير, اسم_المعطى]{
    عرف اسم_المتغير: سند [نوع_المتغير];
    اسم_المتغير~مؤشر = اسم_المعطى~مثل[مؤشر[نوع_المتغير]];
}

دالة عند_ضغط_الزر(القطعة: سند[الـزر], الحمولة: سند[صـحيح], اضافي: مؤشر) {
    هيئ_معطى_ممثل[انقر_فوق_الزر, انـقر_فوق_الزر, اضافي];
    عرف بيانات_جديده: نـص = انقر_فوق_الزر.صندوق_الادخال.هات_النص().استبدل("\n", "<br>");
    انقر_فوق_الزر.صندوق_الادخال.حدد_النص(نـص());
    أرسل_نداء("POST", "/messages", "Content-Type: application/text", بيانات_جديده, 0);
}

دالة عند_المفتاح(القطعة: سند[مـدخل_نص], الحمولة: سند[نـص], اضافي: مؤشر) {
    هيئ_معطى_ممثل[التسمية، عـنوان، اضافي];
    إذا الحمولة == "Shift+Enter" {
        عرف بيانات_جديده: نـص = القطعة.هات_النص().استبدل("\n", "<br>");
        القطعة.حدد_النص(نـص());
        أرسل_نداء("POST", "/messages", "Content-Type: application/text", بيانات_جديده, 0);
    }
}

دالة على_الوقت(تمثال: سند[فـراغ], الحمولة: سند [Json], اضافي: مؤشر) {
    @مشترك عرف مقبس: مـقبس[فـراغ, Json]; 
    مقبس.fn = عند_الطلب~مؤشر;
    مقبس.إضافية = اضافي;

    أرسل_نداء("GET", "/messages", null, null, مقبس~مؤشر);
}

دالة عند_الطلب (تمثال: سند[فـراغ], الحمولة: سند [Json], اضافي: مؤشر) {
    هيئ_معطى_ممثل[التسمية, عـنوان, اضافي];
    عرف البيانات: نـص = الحمولة.getObject("eventData").getString("body");
    إذا التسمية.هات_النص() != البيانات {
        التسمية.حدد_النص(البيانات);
    }
}

@منفذ_مرئي["/about", "مثال منصة ويب - الدردشة"]
دالة مثال_لوحة {  
    عرف التسمية1 : سـندنا[عـنوان] = عـنوان.أنشئ(نـص("مثال الدردشة"));
    التسمية1.حدد_لون_الخط(لـون(0, 0, 0));
    التسمية1.حدد_حجم_الخط(30.0);
    عرف التسمية2:سـندنا[عـنوان] = عـنوان.أنشئ (نـص("طُور باستخدام منصة ويب الأسس"));
    التسمية2.حدد_لون_الخط(لـون(0, 0, 0));
    التسمية2.حدد_حجم_الخط(20.0); 
    عرف لوحة:سـندنا[لـوحة] = لـوحة.أنشئ ()؛
    عرف صندوق: سـندنا [صـندوق] = صـندوق.أنشى({ أنشاء_مقدمة(), لوحة ,التسمية1, التسمية2 })
    حدد_المشهد(صندوق);
    ارسم_خط (لوحة.معرف, 100,180, 0,0 );
    ارسم_دائرة(لوحة.معرف, 100,180, 90,0 );
    عرف نقاط: مـصفوفة [صحيح] ({ 50, 50, 100, 50, 100, 100, 50, 100, 25, 75 });
    حدد_نمط_الملء (لوحة.معرف,"black", "blue", 0, 0, 100, 100);
     ارسم_مضلع (لوحة.معرف, 5, نقاط.صوان);
    ارسم_كتابة(لوحة.معرف, "اهلا", خط_الكتابة, 50, 50);
    نفذ_حلقة_معالجة_الأحداث();
}


طـرفية.اطبع("تشغيل الخادم على المنفذ 8010...\nURL: http://localhost:8010/\n");
شغل_الخادم ({ "listening_ports", "8010", "static_file_max_age", "0" });

