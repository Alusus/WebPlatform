import "Apm";
Apm.importFile("Alusus/WebPlatform");
use Srl;
use WebPlatform;

//==============================================================================
// Backend

def MAX_MESSAGES: 12;
def messages: Array[String];

@beEndpoint["POST", "/messages"]
func postMessages (conn: ptr[Http.Connection]) {
    def postData: array[Char, 1024];
    def postDataSize: Int = Http.read(conn, postData~ptr, postData~size);
    if messages.getLength() >= MAX_MESSAGES messages.remove(0);
    messages.add(String(postData~ptr, postDataSize));
    Http.print(conn, "HTTP/1.1 200 Ok\r\n\r\n");
}

@beEndpoint["GET", "/messages"]
func getMessages (conn: ptr[Http.Connection]) {
    def response: String = String.merge(messages, "<br>");
    Http.print(conn, "HTTP/1.1 200 Ok\r\n");
    Http.print(conn, "Content-Type: text/plain\r\n");
    Http.print(conn, "Cache-Control: no-cache\r\n");
    Http.print(conn, "Content-Length: %d\r\n\r\n", response.getLength());
    Http.print(conn, response.buf);
}

//==============================================================================
// Frontend

def PrimeryColor: Color("8e50ef");
def LightColor: Color("c380ff");

func createHeader (): SrdRef[Widget] {
    setElementAttribute("body", "style", "padding: 0; margin: 0;");

    return Box.new({}).{
        width = String("100%");
        height = String("80pt");
        padding = Quad(5, 0);
        backgroundColor = PrimeryColor;
        borderColor = PrimeryColor;
        border = Quad(1.5);

        addChildren({
            Label.new(String("أمثلة منصة ويب الأسس - الدردشة")).{
                fontColor = Color("fff");
                fontSize = 21.0;
                width = String("100%");
                height = String("100%");
                backgroundColor = PrimeryColor;
            },
            Box.new({}).{
                layout = String("row");
                justify = String("space-between");
                backgroundColor = PrimeryColor;

                addChildren({
                    Hyperlink.new(String("/"), Label.new(String("الرئيسية")).{
                        fontSize = 16.0;
                        fontColor = Color("fff");
                        padding = Quad(3);
                        backgroundColor = LightColor;
                    }),
                    Hyperlink.new(String("/about"), Label.new(String("عن البرنامج")).{
                        fontSize = 16.0;
                        fontColor = Color("fff");
                        padding = Quad(3);
                        backgroundColor = LightColor;
                    })
                });
            }
        });
    }~cast[SrdRef[Widget]];
}

func encodeHtml(str: String): String {
    return str
        .replace("\n", "<br>")
        .replace(",", "&comma;")
        .replace("\"", "&quot;");
}

@uiEndpoint["/", "WebPlatform Example - Chat"]
func main {
    def onFetch: closure (json: Json);

    setView(Box.new({}).{
        height = String("100%");
        // padding = Quad(0, 50);
        addChildren({
            createHeader(),
            Box.new({}).{
                width = String("100%");
                height = String("100%");
                border = Quad(1);
                borderColor = Color(0, 0, 0);
                // padding = Quad(5);
                def notificationLabel: SrdRef[Label];
                addChildren({
                    Label.new(String()).{
                        notificationLabel = this;
                        width = String("100%");
                        height = String("20pt");
                        fontColor = Color(200, 50, 50);
                        fontSize = 10.0;
                    },
                    Label.new(String()).{
                        width = String("100%");
                        height = String("100%");
                        fontColor = Color(50, 50, 50);
                        fontSize = 20.0;

                        onFetch = closure (json: Json) {
                            def status: Int = json.getObject("eventData").getInt("status");
                            if status >= 200 and status < 300 {
                                def data: String = json.getObject("eventData").getString("body");
                                if this.getText() != data {
                                    this.setText(data);
                                }
                                if notificationLabel.getText() != "" {
                                    notificationLabel.setText(String(""));
                                }
                            } else {
                                notificationLabel.setText(String("الاتصال مقطوع. رمز حالة ب.ن.ن.ف: ") + status);
                            }
                        };
                    }
                });
            },
            Box.new({}).{
                width = String("100%");
                height = String("50pt");
                layout = String("row");
                border = Quad(1.5);
                borderColor = PrimeryColor;
                backgroundColor = PrimeryColor;
                // padding = Quad(5);
                def textInput: SrdRef[TextInput];
                addChildren({
                    TextInput.new().{
                        textInput = this;
                        width = String("100%");
                        height = String("100%");
                        fontSize = 12.0;
                        onKeyUp.connect(closure (widget: ref[TextInput], payload: ref[String]) {
                            if payload == "Shift+Enter" {
                                def newData: String = encodeHtml(widget.getText());
                                widget.setText(String());
                                sendRequest(
                                    "POST", "/messages", "Content-Type: application/text", newData, 10000,
                                    closure (Json) {}
                                );
                                sendRequest("GET", "/messages", null, null, 500, onFetch);
                            }
                        });
                    },
                    Button.new(String("أرسل")).{
                        height = String("100%");
                        width = String("50pt");
                        backgroundColor = Color(200, 200, 200);
                        fontSize = 16.0;
                        onClick.connect(closure (widget: ref[Button], payload: ref[Int]) {
                            def newData: String = encodeHtml(textInput.getText());
                            textInput.setText(String());
                            sendRequest(
                                "POST", "/messages", "Content-Type: application/text", newData, 10000,
                                closure (Json) {}
                            );
                            sendRequest("GET", "/messages", null, null, 500, onFetch);
                        });
                    }
                });
            }
        })
    });

    startTimer(500000, closure (json: Json) {
        sendRequest("GET", "/messages", null, null, 500, onFetch);
    });
    sendRequest("GET", "/messages", null, null, 500, onFetch);

    runEventLoop();
}

@uiEndpoint["/about", "WebPlatform Example - Chat"]
func about {
    setView(Box.new({}).{
        addChildren({
            createHeader(),
            Label.new(String("مثال الدردشة")).{
                fontColor = Color(0, 0, 0);
                fontSize = 30.0;
            },
            Label.new(String("طُور باستخدام منصة ويب الأسس")).{
                fontColor = Color(0, 0, 0);
                fontSize = 20.0;
            }
        })
    });

    runEventLoop();
}


Console.print("Starting server on port 8010...\nURL: http://localhost:8010/\n");
runServer({ "listening_ports", "8010", "static_file_max_age", "0" });

