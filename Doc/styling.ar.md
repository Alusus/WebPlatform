# WebPlatform

[[English]](styling.en.md)

[[رجوع]](../readme.ar.md)

## الطرز (Styling)

الطرز تسمح للمستخدم بالتحكم بمظهر واجهة المستخدم عبر التحكم بالخصائص المظهرية للودجات.

### طـراز (Style)

الصنف الرئيسي للتحكم بالخصائص المظهرية لودجة معينة في حالة معينة. تتوفر فيه الخصائص التالية:

<div dir=rtl>

```
طراز_الإطار: طـراز_إطار
طراز_الآوتلاين: طـراز_إطار
الطوفان: طـوفان
الموقع: مـوقع
النسق: نـسق
لف_المرونة: لـف_مرونة
ملء_السطر: مـلئ_سطر
المحاذاة: مـحاذاة
محاذاة_النص: مـحاذاة_نص
زخرفة_النص: زخـرفة_نص
طراز_زخرفة_النص: طـراز_زخرفة_نص
الاتجاه: اتـجاه
فئة_الخط: نـص
الفيض_س: فـيض
الفيض_ص: فـيض
الفيض: فـيض
فيض_الكتابة: فـيض_كتابة
الإظهار: إظـهار
المؤشر: مـؤشر
التحريك: تـحريك
تقطيع_الكلمات: تـقطيع_كلمات
محرف_المسافة: مـحرف_مسافة
العرض: مـسافة
الطول: مـسافة
أدنى_عرض: مـسافة
أدنى_طول: مـسافة
أقصى_عرض: مـسافة
أقصى_طول: مـسافة
سمك_الإطار: مـسافة4
لون_الإطار: لـون
نصف_قطر_الإطار: مـسافة4
سمك_الآوتلاين: مـسافة4
إزاحة_الآوتلاين: مـسافة4
لون_الآوتلاين: لـون
تسلسل_العمق: صـحيح
المرونة: مـرونة
الفجوة: مـسافة
الهامش: مـسافة4
لون_زخرفة_النص: لـون
سمك_زخرفة_النص: مـسافة
الحشوة: مـسافة4
حجم_الخط: مـسافة
لون_الخط: لـون
سمك_الخط: مـسافة
تباعد_الحروف: مـسافة
ارتفاع_الكتابة: مـسافة
الشفافية: صـحيح
التحول: تـحول
ظل_الصندوق: ظـل_صندوق
ظل_النص: ظـل_نص
فوق: مـسافة
يسار: مـسافة
يمين: مـسافة
تحت: مـسافة
الخلفية: خـلفية
خوارزمية_التحجيم: خـوارزمية_تحجيم
```

</div>

```
borderStyle: BorderStyle
outlineStyle: BorderStyle
float: Floating
position: Position
flexDirection: Layout
flexWrap: FlexWrap
justifyContent: Justify
alignItems: Align
textAlign: TextAlign
textDecoration: TextDecoration
textDecorationstyle": TextDecorationStyle
direction: Direction
fontFamily: String
overflowX: Overflow
overflowY: Overflow
overflow: Overflow
textOverflow: TextOverflow
display: Display
cursor: Cursor
animation: Animation
wordBreak: WordBreak
whiteSpace: WhiteSpace
width: Length
height: Length
minWidth: Length
minHeight: Length
maxWidth: Length
maxHeight: Length
borderWidth: Length4
borderColor: Color
borderRadius: Length4
outlineWidth: Length4
outlineOffset: Length4
outlineColor: Color
zIndex: Int
flex: Flex
margin: Length4
textDecorationColor: Color
textDecorationThickness: Length
padding: Length4
fontSize: Length
color: Color
fontWeight: Length
letterSpacing: Length
lineHeight: Length
opacity: Int
transform: Transform
boxShadow: BoxShadow
textShadow: TextShadow
top: Length
left: Length
right: Length
bottom: Length
background: Background
scalingAlgorithm: ScalingAlgorithm
```

فيما يلي هدف كل طرز، ويمكن إيجاد القيم في شروح كل صنف من أصناف الطرز.

* `طراز_الإطار` (`borderStyle`) الطرز الخاص بالإطار.
* `طراز_الآوتلاين` (`outlineStyle`) الطرز الخاص بالخط الذي يرسم خارج الإطار وحوله. يستخدم مع ودجات
  الإدخال.
* `الطوفان` (`floating`) تحديد كيفية طوفان العنصر ضمن الصندوق الحاوي.
* `الموقع` (`position`) تحديد طرز موقع العنصر.
* `النسق` (`layout`) تحديد النسق المتبع للعنصر عند استخدام نمط الإظهار `_مرن_` (`FLEX`).
* `لف_المرونة` (`flexWrap`) تحديد ما إذا كانت عناصر flex تلتف على أسطر متعددة.
* `ملء_السطر` (`justify`) الطرز الخاص بكيفية توزع العناصر ضمن السطر.
* `المحاذاة` (`align`) الطرز الخاص بمحاذاة العنصر ضمن العنصر الذي يحتويه.
* `محاذاة_النص` (`textAlign`) الطرز الخاص بمحاذاة النص ضمن العنصر الذي يحتويه.
* `زخرفة_النص` (`textDecoration`) الطرز الخاص بكيفية زخرفة النص.
* `طراز_زخرفة_النص` (`textDecorationStyle`) الطرز الخاص بشكل الزخرفة الخاصة بالنص.
* `الاتجاه` (`direction`) الاتجاه الخاص بالعنصر.
* `فئة_الخط` (`fontFamily`) فئة الخط الذي يتبع إليها النص.
* `الفيض_س` (`overflowX`) الفيض على محور س.
* `الفيض_ص` (`overflowY`) الفيض على محور ص.
* `الفيض` (`overflow`) الفيض على المحورين س و ص.
* `فيض_الكتابة` (`textOverflow`) حالة الكتابة عند تجاوزها حدود الحاوية.
* `الإظهار` (`display`) طريقة عرض العنصر.
* `المؤشر` (`cursor`) الطرز الخاص بالمؤشر.
* `التحريك` (`animation`) التحريكات التي نريد تطبيقها على العنصر.
* `تقطيع_الكلمات` (`wordBreak`) كيف يجب للكلمات أن تقطع في حال وصلت حدود الإطار.
* `محرف_المسافة` (`whiteSpace`) طريقة التعامل مع محارف المسافة.
* `العرض` (`width`) عرض العنصر.
* `الطول` (`height`) طول العنصر.
* `أدنى_عرض` (`minWidth`) أدنى عرض مسموح للعنصر.
* `أدنى_طول` (`minHeight`) أدنى طول مسموح للعنصر.
* `أقصى_عرض` (`maxWidth`) أقصى عرض مسموح للعنصر.
* `أقصى_طول` (`maxHeight`) أقصى طول مسموح للعنصر.
* `سمك_الإطار` (`borderWidth`) سمك الإطار.
* `لون_الإطار` (`borderColor`) لون الإطار.
* `نصف_قطر_الإطار` (`borderRadius`) نصف قطر الإطار، لتنعيم زوايا الإطار (أي يصبح شكلها منحني قليلاً).
* `سمك_الآوتلاين` (`outlineWidth`) سمك الخط حول الإطار.
* `إزاحة_الآوتلاين` (`outlineOffset`) إزاحة الخط حول الإطار عن الإطار ذاته.
* `لون_الآوتلاين` (`outlineColor`) لون الخط حول الإطار.
* `تسلسل_العمق` (`zIndex`) تفيد هذه الخاصية في ترتيب العناصر عند توضعها فوق بعضها.
* `المرونة` (`flex`) الطرز الخاص بالمرونة و التي تحدد نسق لتوضع العناصر.
* `الفجوة` (`gap`) تحدد المسافة بين عناصر flex أو grid، وتوفر مسافات متسقة دون الحاجة للهوامش.
* `الهامش` (`margin`) الهامش حول العنصر.
* `لون_زخرفة_النص` (`textDecorationColor`) اللون الخاص بالزخرفة للنص.
* `سمك_زخرفة_النص` (`textDecorationThickness`) سمك زخرفة النص.
* `الحشوة` (`padding`) الحشو للعنصر.
* `حجم_الخط` (`fontSize`) حجم الخط.
* `لون_الخط` (`fontColor`) لون الخط.
* `سمك_الخط` (`fontWeight`) سمك الخط.
* `تباعد_الحروف` (`letterSpacing`) للتحكم بالمسافة بين الأحرف.
* `ارتفاع_الكتابة` (`lineHeight`) ارتفاع السطر، هذا يفيد في زيادة أو تقليل المسافة بين الأسطر.
* `الشفافية` (`opacity`) شفافية العنصر.
* `التحول` (`transform`) التحويلات المطبقة على العنصر.
* `ظل_الصندوق` (`boxShadow`) الظل الخاص بالصندوق الذي يحوي العنصر.
* `ظل_النص` (`textShadow`) ظل الكتابة التي في العنصر.
* `فوق` (`top`) بعد العنصر عن أعلى العنصر الحاوي له أو الشاشة (تبعاً للخاصية `موقع`).
* `يسار` (`left`) بعد العنصر عن يسار العنصر الحاوي له أو الشاشة (تبعاً للخاصية `موقع`).
* `يمين` (`right`) بعد العنصر عن يمين العنصر الحاوي له أو الشاشة (تبعاً للخاصية `موقع`).
* `تحت` (`bottom`) بعد العنصر عن أسفل العنصر الحاوي له أو الشاشة (تبعاً للخاصية `موقع`).
* `الخلفية` (`background`) خلفية العنصر.
* `خوارزمية_التحجيم` (`scalingAlorithm`) تحديد الخوارزمية المستخدمة لتحجيم الصور والمراسم.

#### حدد_خامة (setRaw)

<div dir=rtl>

```
عملية [اسم_الخصلة: شبم] هذا.حدد_خامة(القيمة: نـص)؛
```

</div>

```
handler [styleProp: ast] this.setRaw(val: String);
```

دالة تمكن المستخدم من كتابة قيمة CSS يدويا لأي من خصال الطراز.

#### أزل (remove)

<div dir=rtl>

```
عملية [اسم_الخصلة: شبم] هذا.أزل()؛
```

</div>

```
handler [styleProp: ast] this.remove();
```

تزيل طرازا محددا لتعيده إلى وضعه المبدئي.


### طـقم_طرز (StyleSet)

يمكن هذا الصنف المستخدم من تحديد عدة طرز للودجة في حالات مختلفة، كما يسمح بتحديد طرز فروع تلك الودجة
وهذا يمكن المبرمج من التحكم بشكل الودجة المبدئي وشكلها عند تغير حالتها (مثلا عند تمرير الفأرة فوقها)
كما تسمح بالتحكم بشكل فروع تلك الودجة عند تغير حالتها. الودجات لا تتعامل مباشرة مع الصنف `طـراز`
وإنما تتعامل مع `طـقم_طرز` بدلًا من ذلك.

#### تطبيق الطرز على عنصر

يمكن تطبيق طرز على عنصر عن طريق الخاصية `الطراز` و التي نضع ضمنها الطرز الذي نريد تطبيقه.

مثال على ذلك:

<div dir=rtl>

```
// دالة لإنشاء ودجة الترويسة
دالة أنشئ_الترويسة():سـندنا[Widget]{
    // هنا نقوم بإنشاء مساحة مستطيلة لوضع مكونات الودجة ضمنها
    أرجع صـندوق({}).{
        // الأطرزة المستعملة لهذه الودجة
        الطراز.{
            العرض = مـسافة.مئوي(100)؛
            الطول = مـسافة.نقاط(40)؛
            الحشوة = مـسافة4.نقاط(5، 0)؛
            الخلفية = خـلفية(_لون_اصلي_)؛
            لون_الإطار = _لون_اصلي_؛
            سمك_الإطار = مـسافة4.نقاط(1.5)؛
            طراز_الإطار = طـراز_إطار._مستمر_؛
            الإظهار = إظـهار._مرن_؛
            النسق = نـسق._عمود_؛
        }؛
        // مكونات الودجة و التي نعتبرها فروع للودجة
        أضف_فروع({
            // هنا سيكون لدينا فقط نص يوضح أننا ننشأ مثال عن المرسم
            كـتابة(نـص("أمثلة منصة ويب الأسس - مرسم")).{ الطراز.{
                لون_الخط = لـون("fff")؛
                حجم_الخط = مـسافة.نقاط(21.0)؛
                العرض = مـسافة.مئوي(100)؛
                الطول = مـسافة.مئوي(100)؛
                الخلفية = خـلفية(_لون_اصلي_)؛
                الهامش = مـسافة4.نقاط(5، 15)؛
            } }
        })؛
    }؛
}
```

</div>

```
func createHeader (): SrdRef[Widget] {
    // هنا نقوم بإنشاء مساحة مستطيلة لوضع مكونات الودجة ضمنها
    return Box({}).{
        // الأطرزة المستعملة لهذه الودجة
        style.{
            width = Length.percent(100);
            height = Length.pt(40);
            padding = Length4.pt(5, 0);
            background = Background(PRIMARY_COLOR);
            borderColor = PRIMARY_COLOR;
            borderWidth = Length4.pt(1.5);
            borderStyle = BorderStyle.SOLID;
            display = Display.FLEX;
            layout = Layout.COLUMN;
        };
        // مكونات الودجة و التي نعتبرها فروع للودجة
        addChildren({
            // هنا سيكون لدينا فقط نص يوضح أننا ننشأ مثال عن المرسم
            Text(String("Alusus Web Platform Examples - Canvas")).{ style.{
                fontColor = Color("fff");
                fontSize = Length.pt(21.0);
                width = Length.percent(100);
                height = Length.percent(100);
                background = Background(PRIMARY_COLOR);
                margin = Length4.pt(5, 15);
            } },
        });
    }~cast[SrdRef[Widget]];
}
```

هنا كما نرى نقوم بكتابة دالة لإنشاء عنصر و تطبيق طرز عليه.

داخل الطرز نقوم بوضع قيم كل خاصية نريدها، مثلاً قمنا بتحديد العرض على أنه 100 كنسبة مئوية.

#### تطبيق الطرز على عنصر في حالة معينة

يمكن القيام بذلك بتمرير محدد الحالة إلى `الطراز` لتحديد الحالة التي نريد فيها لذلك الطراز أن يُطبق.
على سبيل المثال:

<div dir=rtl>

```
صـندوق({}).{
    // نطبق بعض الأطرزة
    الطراز.{
        الخلفية = خـلفية(_لون_فاتح_)؛
    }؛
    // نغير لون الخلفية عند مرور المؤشر فوق الصندوق.
    الطراز(مـحدد_حالة._حوم_).{
        الخلفية = خـلفية(_لون_داكن_)؛
    }
}
```

</div>

```
Box({}).{
    // نطبق بعض الأطرزة
    style.{
        background = Background(LIGHT_COLOR);
    };
    // نغير لون الخلفية عند مرور المؤشر فوق الصندوق.
    style(StateSelector.HOVER).{
        background = Background(DARK_COLOR);
    }
}
```

تتوفر الحالات التالية التي يمكن استخدامها، وهذه الحالات تطابق الحالات المستخدمة في CSS:

<div dir=rtl>

```
عرف مـحدد_حالة: {
    عرف _فعال_: ":active"؛
    عرف _معلم_: ":checked"؛
    عرف _مبدئي_: ":default"؛
    عرف _معطل_: ":disabled"؛
    عرف _فارغ_: ":empty"؛
    عرف _ممكن_: ":enabled"؛
    عرف _بؤرة_: ":focus"؛
    عرف _ملء_الشاشة_: ":fullscreen"؛
    عرف _حوم_: ":hover"؛
    عرف _ضمن_النطاق_: ":in-range"؛
    عرف _وسطي_: ":indeterminate"؛
    عرف _باطل_: ":invalid"؛
    عرف _رابط_: ":link"؛
    عرف _اختياري_: ":optional"؛
    عرف _خارج_النطاق_: ":out-of-range"؛
    عرف _قراءة_فقط_: ":read-only"؛
    عرف _قراءة_وكتابة_: ":read-write"؛
    عرف _إلزامي_: ":required"؛
    عرف _مستهدف_: ":target"؛
    عرف _صالح_: ":valid"؛
    عرف _مزار_: ":visited"؛
}
```

</div>

```
def StateSelector: {
    def ACTIVE: ":active";
    def CHECKED: ":checked";
    def DEFAULT: ":default";
    def DISABLED: ":disabled";
    def EMPTY: ":empty";
    def ENABLED: ":enabled";
    def FOCUS: ":focus";
    def FULLSCREEN: ":fullscreen";
    def HOVER: ":hover";
    def IN_RANGE: ":in-range";
    def INTERMEDIATE: ":indeterminate";
    def INVALID: ":invalid";
    def LINK: ":link";
    def OPTIONAL: ":optional";
    def OUT_OF_RANGE: ":out-of-range";
    def READ_ONLY: ":read-only";
    def READ_WRITE: ":read-write";
    def REQUIRED: ":required";
    def TARGET: ":target";
    def VALID: ":valid";
    def VISITED: ":visited";
}
```

* `_فعال_` (`ACTIVE`) حالة كون العنصر فعال.
* `_معلم_` (`CHECKED`) حال كون العنصر معلم، مثل صندوق التعليم عندما نعلمه.
* `_مبدئي_` (`DEFAULT`) الحالة الافتراضية.
* `_معطل_` (`DISABLED`) حالة كون العنصر معطل، أي عكس الحالة `_فعال_`.
* `_فارغ_` (`EMPTY`) حالة كون العنصر فارغ، مثل مربع نص.
* `_ممكن_` (`ENABLED`) حالة كون العنصر ممكن.
* `_بؤرة_` (`FOCUS`) حالة كون البؤرة على العنصر، أي مثلاً عندما نضعط على مربع نص تصبح البؤرة عليه.
* `_ملء_الشاشة_` (`FULLSCREEN`) حالة ملء الشاشة.
* `_حوم_` (`HOVER`) حالة الحوم بالمؤشر على العنصر.
* `_ضمن_النطاق_` (`IN_RANGE`) حالة أن تكون القيمة ضمن نطاق ما.
* `_وسطي_` (`INTERMEDIATE`) الحالة الوسطية.
* `_باطل_` (`INVALID`) حالة أن يكون العنصر باطل، أي مثلاً عندما نكتب في خانة البريد عنوان بريد غير صالح.
* `_رابط_` (`LINK`) حالة أن يكون رابط.
* `_اختياري_` (`OPTIONAL`) حالة أن يكون العنصر اختياري، أي مثلاً في الاستبيانات قد لا تكون كل العناصر مطلوبة.
* `_خارج_النطاق_` (`OUT_OF_RANGE`) حالة أن تكون القيمة خارج النطاق.
* `_قراءة_فقط_` (`READ_ONLY`) حالة أن يكون العنصر للقراءة فقط.
* `_قراءة_وكتابة_` (`READ_WRITE`) حالة أن يكون العنصر للقراءة و الكتابة.
* `_إلزامي_` (`REQUIRED`) حالة أن يكون العنصر إلزامي، مثل كلمة المرور عند التسجيل في موقع.
* `_مستهدف_` (`TARGET`) حالة أن يكون العنصر مستهدف.
* `_صالح_` (`VALID`) حالة أن يكون العنصر صالح.
* `_مزار_` (`VISITED`) حالة أن يكون العنصر مزار، أي عندما يكون لدينا رابط.

#### تطبيق الطرز على عنصر فرعي

يمكن القيام بذلك بتمرير اسم فئة إلى `الطراز` لتحديد ما نريد أن يطبق عليه الطرز.

كما يمكننا أيضاً تحديد حالة محددة للعنصر يتم عندها تطبيق الطراز على الفرع. مثلاً حالة أن يكون
المؤشر فوق الودجة الرئيسية فيُطبق عندها الطراز على الفرع. يتم ذلك بتمرير الحالة إلى `الطراز`
قبل اسم الفئة، كما في المثال التالي:

<div dir=rtl>

```
صـندوق({}).{
    // نطبق بعض الأطرزة
    الطراز.{
        الحشوة = مـسافة4.نقاط(3)؛
        الخلفية = خـلفية(_لون_فاتح_)؛
        الإظهار = إظـهار._مرن_؛
        النسق = نـسق._سطر_؛
        المحاذاة = مـحاذاة._وسط_؛
    }؛
    // نطبق طراز على ودجة فرعية من الفئة أيقونة
    الطراز(">>icon").{
        الحشوة = مـسافة4.نقاط(0, 4)؛
        العرض = مـسافة.نقاط(20)؛
        الطول = مـسافة.نقاط(0)؛
        انتقال_الطول = انـتقال(0.2)؛
    }
    // نقوم بتطبيق طراز على ودجة فرعية من الفئة أيقونة 
    // هذا الطراز يطبق عندما نحوم بالمؤشر على الأيقونة
    الطراز({ مـحدد_حالة._حوم_, ">>icon" }).{
        الطول = مـسافة.نقاط(20)؛
    }
    أضف_فروع({
        // ودجة رابط
        // و الذي يحوي على ودجة فرعية هي صورة
        ارتـباط_تشعبي(رابط, صـورة().{
            الرابط = أيقونة؛
            // فئة هذا الرابط هو أيقونة
            اسم_الفئة = نـص("icon")؛
        })
    })؛
}؛
```

</div>

```
Box({}).{
    // نطبق بعض الأطرزة
    style.{
        padding = Length4.pt(3);
        background = Background(LIGHT_COLOR);
        display = Display.FLEX;
        layout = Layout.ROW;
        align = Align.CENTER;
    };
    // نطبق طراز على ودجة فرعية من الفئة أيقونة
    style(">>icon").{
        padding = Length4.pt(0, 4);
        width = Length.pt(20);
        height = Length.pt(0);
        heightTransition = Transition(0.2);
    }
    // نقوم بتطبيق طراز على ودجة فرعية من الفئة أيقونة 
    // هذا الطراز يطبق عندما نحوم بالمؤشر على الأيقونة
    style({ StateSelector.HOVER, ">>icon" }).{
        height = Length.pt(20)
    }

    // الودجات الفرعية
    addChildren({
        // ودجة رابط
        // و الذي يحوي على ودجة فرعية هي صورة
        Hyperlink(link, Image().{
            url = icon;
            // فئة هذا الرابط هو أيقونة
            className = String("icon");
        })
    });
}
```

هنا نلاحظ أننا حددنا الابن المباشر الذي له الخاصية icon في أول تطبيق للطرز.

من ثم قمنا بتحديد حالة أن يكون المؤشر فوق العنصر عن طريق تمرير StateSelector.HOVER أيضاً.


### تـحريك (Animation)

<div dir=rtl>

```
صنف تـحريك {
    عرف حدد(مدة_كلية: عـائم، أطرزة: مـصفوفة[طـراز])؛
    عرف حدد(مدة_كلية: عـائم، أطرزة: ربـط[عـائم، طـراز])؛
}
```

</div>

```
class Animation {
    handler this.set(totalDuration: Float, styles: Array[Style]);
    handler this.set(totalDuration: Float, styles: Map[Float, Style]);
}
```

يستعمل هذا الصنف لإنشاء تحريك لعنصر ما.

`حدد` (`set`) وظيفة للقيام بتحديد التحريك الذي نريده.

هنالك نسختان من هذه الطريقة، واحدة تقبل الأنماط على شكل مصفوفة و الأخرى على شكل تـطبيق تمثل مفاتيحه
الوقت الذي يُطبق فيه الطراز المعرف في القيمة المقابلة لذلك المفتاح.


### أبـعاد (Dimensions)

<div dir=rtl>

```
صنف أبـعاد {
    عرف عرض: صـحيح؛
    عرف طول: صـحيح؛
}
```

</div>

```
class Dimensions {
    def width: Int;
    def height: Int;
}
```
صنف يحمل معلومات الأبعاد.

`عرض` (`width`) العرض.

`طول` (`height`) الطول.


### مـستطيل (Rectangle)

<div dir=rtl>

```
صنف مـستطيل {
    عرف س: صـحيح؛
    عرف ص: صـحيح؛
    عرف عرض: صـحيح؛
    عرف طول: صـحيح؛
}
```

</div>

```
class Rectangle {
    def x: Int;
    def y: Int;
    def width: Int;
    def height: Int;
}
```
صنف يحمل معلومات مستطيل.


### لـون (Color)

<div dir=rtl>

```
صنف لـون {
    عرف أحمر: صـحيح = 0؛
    عرف أخضر: صـحيح = 0؛
    عرف أزرق: صـحيح = 0؛
    عرف شفافية: صـحيح = 0؛
    عرف حول_لنص: لقب toString؛
}
```

</div>

```
class Color {
    def red: Int = 0;
    def green: Int = 0;
    def blue: Int = 0;
    def alpha: Int = 0;
}
```
صنف يحمل معلومات اللون.

`أحمر` (`red`) نسبة اللون الأحمر في اللون.

`أخضر` (`green`) نسبة اللون الأخضر في اللون.

`أزرق` (`blue`) نسبة اللون الأزرق في اللون.

`شفافية` (`alpha`) شفافية اللون.


### مـسافة (Length)

صنف يحمل معلومات المسافة.

يقوم هذا الصنف بتعريف العديد من وحدات القياس والتي هي كما يلي:

`بكسلات` (`px`) عدد البكسلات.

`نقاط` (`pt`) عدد النقاط.

`مم` (`mm`) عدد الميليمترات

`عن` (`vw`) مسافة نسبية بالنسبة لـ 1 بالمئة من عرض نافذة المتصفح.

`طن` (`vh`) مسافة نسبية بالنسبة لـ 1 بالمئة من  ارتفاع نافذة المتصفح.

`بنص` (`vmin`) مسافة نسبية بالنسبة لـ 1 بالمئة من أصغر جانب لنافذة المتصفح.

`بنك` (`vmax`) مسافة نسبية بالنسبة لـ 1 بالمئة من أكبر جانب لنافذة المتصفح.

`حخ` (`em`) مسافة نسبية بالنسبة لحجم الخط للعنصر.

`مئوي` (`percent`) نسبة مئوية بالنسبة للعنصر الذي يحتوي العنصر.

`تلقائي` (`auto`) قيمة "auto" من CSS.

`موروث` (`inherit`) قيمة "inherit" من CSS.

كل من وحدات القياس هذه دالة تستخدم لإنشاء كائن جديد من صنف `مـسافة` وإعطائه قيمة المسافة الممررة كمعطى للدالة.
القيمة المُرجعة من هذه الدالات من صنف `سـندنا[مـسافة]` (`SrdRef[Length]`).
على سبيل المثال:

<div dir=rtl>

```
مـسافة.نقاط(50) // تنشئ كائنا يستخدم النقاط كوحدة قياس وتعطيه قيمة 50.
```

</div>

```
Length.pt(50); // Creates an object with value of 50 points.
```

يحوي الصنف أيضًا على الوظيفة التالية:

`حول_لنص` (`toString`) طريقة لتحويل معلومات الصتف إلى ترميز نصي.


### مـسافة4 (Length4)

مشابه لـ`مـسافة` (`Length`) ولكن يعرف 4 قيم، قيمة لكل من الاتجاهات الأربعة.
يُعرف هذا الصنف وحدات القياس التالية:

`بكسلات` (`px`) عدد البكسلات.

`نقاط` (`pt`) عدد النقاط.

`مم` (`mm`) عدد الميليمترات

`عن` (`vw`) مسافة نسبية بالنسبة لـ 1 بالمئة من عرض نافذة المتصفح.

`طن` (`vh`) مسافة نسبية بالنسبة لـ 1 بالمئة من  ارتفاع نافذة المتصفح.

`بنص` (`vmin`) مسافة نسبية بالنسبة لـ 1 بالمئة من أصغر جانب لنافذة المتصفح.

`بنك` (`vmax`) مسافة نسبية بالنسبة لـ 1 بالمئة من أكبر جانب لنافذة المتصفح.

`حخ` (`em`) مسافة نسبية بالنسبة لحجم الخط للعنصر.

`مئوي` (`percent`) نسبة مئوية بالنسبة للعنصر الذي يحتوي العنصر.

`تلقائي` ('auto`) قيمة `auto` من CSS.

`موروث` (`inherit`) قيمة "inherit" من CSS.

كل من وحدات القياس هذه دالة تستخدم لإنشاء كائن جديد من صنف `مـسافة4` وإعطائه قيم المسافة الممررة
كمعطيات للدالة. القيمة المُرجعة من هذه الدالات من صنف `سـندنا[مـسافة4]` (`SrdRef[Length4]`).
على سبيل المثال:

<div dir=rtl>

```
مـسافة4.نقاط(50) // تنشئ كائنا يستخدم النقاط كوحدة قياس وتعطيه قيمة 50 لكل من الاتجاهات الأربع.
مـسافة4.نقاط(50، 30) // تنشئ كائنا يستخدم النقاط كوحدة قياس وتعطيه قيمة 50 عموديا (فوق وتحت)
                     // وقيمة 30 أفقيا (يمين ويسار).
مـسافة4.نقاط(10، 20، 30، 40) // تنشئ كائنا يستخدم النقاط كوجدة قياس وتعطيه القيم فوق = 10،
                             // يمين = 20، تحت = 30، ويسار = 40.
```

</div>

```
Length4.pt(50); // Creates an object with value of 50 points for each of the 4 values.
Length4.pt(50, 30); // Creates an object with value of 50 points for vertical values (top and bottom)
                   // and 30 points for horizontal values (left and right).
Length4.pt(10, 20, 30, 40); // Creates an object with value top = 50 points, right = 20 points,
                           // bottom = 30 points, and left = 40 points.
```

يمكنك أيضًا تهيئة الكائن وإعطاؤه قيم `مـسافة` بدل استخدام دالات وحدات القياس المذكورة أعلاه:

<div dir=rtl>

```
// إنشاء مـسافة4 وتحديد قيمة 50 نقطة للمعطيات العمودية (فوق وتحت)
// وقيمة 30 بكسل للمعطيات الأفقية (يمين ويسار). 
سـندنا[مـسافة4]().{ احجز()~هيئ(مـسافة.نقاط(50)، مـسافة.بكسلات(30)) }
```

</div>

```
// Create Length4 and give it 50 points for vertical values (top and bottom)
// and 30 pixels for horizontal values (left and right).
SrdRef[Length4]().{ alloc()~init(Length.pt(50), Length.px(30)) }
```


### انـتقال (Transition)

<div dir=rtl>

```
صنف انـتقال {
    عرف المدة: عـائم = 0؛
    عرف الدالة: نـص؛
    عرف التأخير: عـائم = 0؛
    عرف حول_لنص(): نـص؛
}
```

</div>

```
class Transition {
    def duration: Float = 0;
    def fn: String;
    def delay: Float = 0;
    handler this.toString(): String;
}
```
صنف يحمل معلومات الانتقال.

`المدة` (`duration`) مدة الانتقال.

`الدالة` (`fn`) اسم دالة الانتقال المطبقة.

`التأخير` (`delay`) التأخير في تنفيذ الانتقال.

`حول_لنص` (`toString`) طريقة لتحويل معلومات الصنف إلى ترميز نصي.


### تـحول (Transform)

<div dir=rtl>

```
وحدة تـحول {
    عرف ماتركس(أ: عـائم، ب: عـائم، ج: عـائم، د: عـائم، انتقال_س: عـائم، انتقال_ع: عـائم): نـص؛
    عرف ماتركس(
        أ1: عـائم، ب1: عـائم، ج1: عـائم، د1: عـائم،
        أ2: عـائم، ب2: عـائم، ج2: عـائم، د2: عـائم،
        أ3: عـائم، ب3: عـائم، ج3: عـائم، د3: عـائم،
        أ4: عـائم، ب4: عـائم، ج4: عـائم، د4: عـائم
    ): نـص؛
    عرف دور(درجة: عـائم): نـص؛
    عرف دور(س: عـائم، ع: عـائم، ج: عـائم، درجة: عـائم): نـص؛
    عرف حجم(س: عـائم): نـص؛
    عرف حجم(س: عـائم، ع: عـائم): نـص؛
    عرف حجم(س: عـائم، ع: عـائم، ج: عـائم): نـص؛
    عرف أزح(س: سند[مـسافة]، ع: سند[مـسافة]): نـص؛
    عرف أزح(س: سند[مـسافة]، ع: سند[مـسافة]، ج: سند[مـسافة]): نـص؛
}
```

</div>

```
module Transform {
    func matrix(a: Float, b: Float, c: Float, d: Float, tx: Float, ty: Float): String;
    func matrix(
            a1: Float, b1: Float, c1: Float, d1: Float,
            a2: Float, b2: Float, c2: Float, d2: Float,
            a3: Float, b3: Float, c3: Float, d3: Float,
            a4: Float, b4: Float, c4: Float, d4: Float
    ): String;
    func rotate(deg: Float): String;
    func rotate(x: Float, y: Float, z: Float, deg: Float): String;
    func scale(x: Float): String;
    func scale(x: Float, y: Float): String;
    func scale(x: Float, y: Float, z: Float): String;
    func translate(x: ref[Length], y: ref[Length]): String;
    func translate(x: ref[Length], y: ref[Length], z: ref[Length]): String;
}
```
صنف يحمل معلومات التحول.

`ماتركس` (`matrix`) طريقة لتعريف مصفوفة التحول.

هنالك نسختان من هذه الطريقة، الأولى تعرف مصفوفة تحول ثنائية الأبعاد بينما الثانية تعرفها ثلاثية الأبعاد.

`دور` (`rotate`) طريقة لتطبيق تحول دوران على عنصر.

هذه الطريقة لها نسختان، الأولى لتطبيق الدوران في فضاء ثنائي البعد، بينما الثانية لتطبيقه في فضاء ثلاثي الأبعاد.

في الطريقة الأولى نحتاج فقط الزاوية، بينما في الطريقة الثانية نحدد أيضاً شعاع المحور الذي نريد التدوير حوله
عبر تحديد مساقطه على المحاور الثلاث.

`حجم` (`scale`) طريقة لتحجيم الشكل و لها نسختان ثنائية بعد و ثلاثية بعد.

`أزح` (`translate`) طريقة لإزاحة شكل و لها نسختان ثنائية بعد و ثلاثية بعد.


### خـلفية (Background)

<div dir=rtl>

```
صنف خـلفية {
    عملية هذا_الصنف(ل: سند[لـون]): سـندنا[خـلفية]؛
    عملية هذا_الصنف(
        ر: نـص،
        تكرار_س: ثـنائي، تكرار_ص: ثـنائي،
        موقع: سند[مـوقع_خلفية]،
        حجم: سند[حـجم_خلفية]
    ): سـندنا[خـلفية]؛
    عملية هذا_الصنف(زاوية: صـحيح، عدد: صـحيح، معطيات: ...أيما): سـندنا[خـلفية]؛
}
```

</div>

```
class Background {
    handler this_type(c: ref[Color]): SrdRef[Background];
    handler this_type(
            u: String, repX: Bool, repY: Bool,
            pos: ref[BackgroundPosition],
            s: ref[BackgroundSize]
        ): SrdRef[Background];
    handler this_type(degree: Int, count: Int, args: ...any): SrdRef[Background];
    handler this_type(v : String): SrdRef[Background];
}
```

صنف يحمل معلومات الخلفية. يحتوي على ثلاث دالات إنشاء:
* الدالة الأولى تنشئ خلفية بلون واحد.
* الدالة الثانية تنشئ خلفية بصورة.
* الدالة الثالثة تنشئ خلفية بتدرج لوني. المعطى الأول يحدد زاوية التدرج بينما يحدد المعطى الثاني عدد
  الألوان المعطاة بعدها والتي سيتم التدرج بينها. بعد المعطى الثاني يجب تمرير المعطيات اللونية على
  شكل ثنائيات عددها يطابق المعطى الثاني. كل ثنائي يتكون من `لـون` يليه عدد صحيح يحدد النسبة التي
  يبدأ عندها هذا اللون من المسافة الكلية.
* الدالة الرابعة تنشئ خلفية مهيأة بقيمة نصية.


### مـرونة (Flex)

<div dir=rtl>

```
صنف مـرونة {
    عرف حول_لنص(): نـص؛
}
```

</div>

```
class Flex {
    handler this.toString(): String;
}
```
صنف يحمل معلومات المرونة.

`حول_لنص` (`toString`) طريقة لتحويل معلومات المرونة إلى ترميز نصي.


### ظـل_صندوق (BoxShadow)

<div dir=rtl>

```
صنف ظـل_صندوق {
    عملية هذا~هيئ()؛
    عملية هذا~هيئ(إزاحة_س: سـندنا[مـسافة]، إزاحة_ص: سـندنا[مـسافة]، لون: لـون)؛
    عملية هذا~هيئ(
        إزاحة_س: سـندنا[مـسافة]،
        إزاحة_ص: سـندنا[مـسافة]،
        نصف_قطر_الغباشة: سـندنا[مـسافة]،
        نصف_قطر_الانتشار: سـندنا[مـسافة]،
        لون: لـون
    )؛
    عملية هذا~هيئ(
        للداخل: ثـنائي،
        إزاحة_س: سـندنا[مـسافة]،
        إزاحة_ص: سـندنا[مـسافة]،
        نصف_قطر_الغباشة: سـندنا[مـسافة]،
        نصف_قطر_الانتشار: سـندنا[مـسافة]،
        لون: لـون
    )؛
    عرف حول_لنص(): نـص؛
}
```

</div>

```
class BoxShadow {
    handler this~init();
    handler this~init(offsetX: SrdRef[Length], offsetY: SrdRef[Length], color: Color);
    handler this~init(
        offsetX: SrdRef[Length],
        offsetY: SrdRef[Length],
        blurRadius: SrdRef[Length],
        spreadRadius: SrdRef[Length],
        color: Color
    );
    handler this~init(
        inset: Bool,
        offsetX: SrdRef[Length],
        offsetY: SrdRef[Length],
        blurRadius: SrdRef[Length],
        spreadRadius: SrdRef[Length],
        color: Color
    );
    handler this.toString(): String;
}
```
صنف يحمل معلومات الظل لصندوق.

`حول_لنص` (`toString`) طريقة لتحويل معلومات الظل إلى ترميز نصي.


### ظـل_نص (TextShadow)

<div dir=rtl>

```
صنف ظـل_نص {
    عملية هذا~هيئ()؛
    عملية هذا~هيئ(إزاحة_س: سـندنا[مـسافة]، إزاحة_ص: سـندنا[مـسافة]، لون: لـون)؛
    عملية هذا~هيئ(
        إزاحة_س: سـندنا[مـسافة]،
        إزاحة_ص: سـندنا[مـسافة]،
        نصف_قطر_الغباشة: سـندنا[مـسافة]،
        لون: لـون
    )؛
    عرف حول_لنص(): نـص؛
}
```

</div>

```
class TextShadow {
    handler this~init();
    handler this~init(offsetX: SrdRef[Length], offsetY: SrdRef[Length], color: Color);
    handler this~init(
        offsetX: SrdRef[Length],
        offsetY: SrdRef[Length],
        blurRadius: SrdRef[Length],
        color: Color
    );
    handler this.toString(): String;
}
```
صنف يحمل معلومات الظل لنص.

`حول_لنص` (`toString`) طريقة لتحويل معلومات الظل إلى ترميز نصي.


### مـوقع (Position)

صنف يحمل قيم الموقع الممكنة على شكل تعدادات.
* `_ساكن_` (`STATIC`)
* `_نسبي_` (`RELATIVE`)
* `_مثبت_` (`FIXED`)
* `_مطلق_` (`ABSOLUTE`)
* `_لزج_` (`STICKY`)


### فـيض (Overflow)

صنف يحمل قيم الفيض الممكنة على شكل تعدادات.
* `_ظاهر_` (`VISIBLE`)
* `_مخفي_` (`HIDDEN`)
* `_مقطوع_` (`CLIP`)
* `_تمرير_` (`SCROLL`)
* `_تلقائي_` (`AUTO`)


### فـيض_كتابة (TextOverflow)

صنف يحمل قيم فيض الكتابة الممكنة على شكل تعدادات.
* `_قطع_` (`CLIP`)
* `_نقط_` (`ELLIPSIS`)


### إظـهار (Display)

صنف يحمل قيم الإظهار الممكنة على شكل تعدادات.
* `_ضمني_` (`INLINE`)
* `_كتلة_ضمنية_ (`INLINE_BLOCK`)
* `_كتلة_` (`BLOCK`)
* `_مرن_` (`FLEX`)
* `_شبكة_` (`GRID`)
* `_مخفي_` (`NONE`)


### نـسق (Layout)

صنف يحمل قيم النسق الممكنة على شكل تعدادات.
* `_سطر_` (`ROW`)
* `_سطر_معكوس_` (`ROW_REVERSE`)
* `_عمود_` (`COLUMN`)
* `_عمود_معكوس_` (`COLUMN_REVERSE`)


### لـف_مرونة (FlexWrap)

صنف يحمل قيم لف المرونة الممكنة على شكل تعدادات.
* `_بلا_لف_` (`NOWRAP`)
* `_لف_` (`WRAP`)
* `_لف_معكوس_` (`WRAP_REVERSE`)


### مـحاذاة (Align)

صنف يحمل قيم المحاذاة الممكنة على شكل تعدادات.
* `_بداية_` (`START`)
* `_وسط_` (`CENTER`)
* `_نهاية_` (`END`)
* `_تمدد_` (`STRETCH`)


### مـلء_سطر (Justify)

صنف يحمل قيم ملء السطر الممكنة على شكل تعدادات.
* `_بداية_` (`START`)
* `_وسط_` (`CENTER`)
* `_نهاية_` (`END`)
* `_تمدد_` (`STRETCH`)
* `_مسافة_بينية_` (`SPACE_BETWEEN`)
* `_مسافة_محيطية_` (`SPACE_AROUND`)
* `_مسافة_متساوية_` (`SPACE_EVENLY`)


### مـؤشر (Cursor)

صنف يحمل قيم المؤشر الممكنة على شكل تعدادات.
* `_تلقائي_` (`AUTO`)
* `_مبدئي_` (`DEFAULT`)
* `_مساعدة_` (`HELP`)
* `_سبابة_` (`POINTER`)
* `_تقدم_` (`PROGRESS`)
* `_انتظار_` (`WAIT`)
* `_تقاطع_` (`CROSSHAIR`)
* `_كتابة_` (`TEXT`)
* `_تحريك_` (`MOVE`)
* `_ممنوع_` (`NOT_ALLOWED`)
* `_إمساك_` (`GRAB`)
* `_ممسك_` (`GRABBING`)
* `_تحجيم_أفقي_` (`EW_RESIZE`)
* `_تحجيم_عمودي_` (`NS_RESIZE`)
* `_تحجيم_مائل_` (`NESW_RESIZE`)
* `_تحجيم_مائل_معكوس_` (`NWSE_RESIZE`)
* `_تكبير_` (`ZOOM_IN`)
* `_تصغير_` (`ZOOM_OUT`)


### طـراز_إطار (BorderStyle)

صنف يحمل قيم طراز الإطار الممكنة على شكل تعدادات.
* `_منقط_` (`DOTTED`)
* `_مقطع_` (`DASHED`)
* `_مستمر_` (`SOLID`)
* `_مزدوج_` (`DOUBLE`)
* `_أخدود_` (`GROOVE`)
* `_حافة_` (`RIDGE`)
* `_بلا_` (`NONE`)
* `_مخفي_` (`HIDDEN`)


### تـقطيع_كلمات (WordBreak)

صنف يحمل قيم تقطيع الكلمات الممكنة على شكل تعدادات.
* `_عادي_` (`NORMAL`)
* `_قطع_الكل_` (`BREAK_ALL`)
* `_احتفظ_بالكل_` (`KEEP_ALL`)
* `_قطع_الكلمات_` (`BREAK_WORD`)


### مـحرف_مسافة (WhiteSpace)

صنف يحمل قيم التعامل مع محرف المسافة على شكل تعدادات.
* `_عادي_` (`NORMAL`)
* `_حرفي_` (`PRE`)
* `_حرفي_لف_` (`PRE_WRAP`)
* `_حرفي_سطر_` (`PRE_LINE`)


### اتـجاه (Direction)

صنف يحمل قيم الاتجاه الممكنة على شكل تعدادات.
* `_من_اليسار_` (`LTR`)
* `_من_اليمين_` (`RTL`)


### زخـرفة_نص (TextDecoration)

صنف يحمل قيم زخرفة النص الممكنة على شكل تعدادات.
* `_بلا_` (`NONE`)
* `_تحت_` (`UNDERLINE`)
* `_فوق_` (`OVERLINE`)
* `_خلال_` (`LINE_THROUGH`)


### طـراز_زخرفة_نص (TextDecorationStyle)

صنف يحمل قيم طراز زخرفة النص الممكنة على شكل تعدادات.
* `_مستمر_` (`SOLID`)
* `_مزدوج_` (`DOUBLE`)
* `_منقط_` (`DOTTED`)
* `_مقطع_` (`DASHED`)
* `_متموج_` (`WAVY`)


### مـحاذاة_نص (TextAlign)

صنف يحمل قيم محاذاة النص الممكنة على شكل تعدادات.
* `_يسار_` (`LEFT`)
* `_يمين_` (`RIGHT`)
* `_وسط_` (`CENTER`)
* `_املأ_` (`JUSTIFY`)


### سـمك_خط (FontWeight)

<div dir=rtl>

```
عرف سـمك_خط {
    عرف _عادي_: 400؛ 
    عرف _سميك_: 700؛
}
```

</div>

```
def FontWeight: {
    def NORMAL: 400;
    def BOLD: 700;
}
```

صنف يحمل قيم الخط الممكنة.
* `_عادي_` (`NORMAL`)
* `_سميك_` (`BOLD`)


### طـوفان (Floating)

صنف يحمل قيم الطوفان الممكنة على شكل تعدادات.
* `_بلا_` (`NONE`)
* `_يسار_` (`LEFT`)
* `_يمين_` (`RIGHT`)


### مـوقع_خلفية (BackgroundPosition)

تعداد للتحكم بموقع صورة الخلفية عندما لا تطابق أبعاد الصورة أبعاد العنصر الحاوي لها. القيم الممكنة:
* `_مركز_` (`CENTER`) 
* `_يمين_` (`RIGHT`) 
* `_يسار_` (`LEFT`) 
* `_فوق_` (`TOP`) 
* `_تحت_` (`BOTTOM`) 


### حـجم_خلفية (BackgroundSize)

تعداد للتحكم بحجم صورة الخلفية عندما لا تطابق أبعاد الصورة أبعاد العنصر الحاوي لها. القيم الممكنة:
* `_احتواء_` (`CONTAIN`): يحجم الصورة بحيث تظهر كاملة.
* `_تغطية_` (`COVER`): يحجم الصورة بحيث تغطي كامل الخلفية. بعض أجزاء الصورة قد تكون خارج حدود الرؤية.

### خـوارزمية_تحجيم (ScalingAlgorithm)

تعداد للتحكم بخوارزمية التحجيم للصور والمراسم. القيم الممكنة:
* `_تلقائي_` (`AUTO`)
* `_أملس_` (`SMOOTH`)
* `_جودة_عالية_` (`HIGH_QUALITY`)
* `_مبكسل_` (`PIXELATED`)
* `_حواف_هشة_` (`CRISP_EDGES`)

