# WebPlatform

[[English]](components.en.md)

[[رجوع]](../README.ar.md)

<div dir=rtl>

## المركّبات (Components)

المركبات تمكن المستخدم من إنشاء أجزاء من واجهة المستخدم قابلة لإعادة الاستخدام، ما يسهل على المستخدم
إنشاء واجهات مستخدم أكثر تعقيدًا. المركب يحتوي على مشهد يتكون من ودجة واحدة أو شجرة من الودجات
بالإضافة للتفاعلات اللازمة كمعالجة الأحداث وما شابه لهذا المشهد.

### كيفية إنشاء مركب

يمكن إنشاء مركب جديد عن طريق تعريف صنف خاص بهذا المركب مشتق من الصنف `مـركب` (`Component`)، مع تعريف
الباني (constructor) ليقوم بتهيئة المركب وإنشاء المشهد.

بشكل عام يمكن استعمال المساحة المستطيلة ووضع ما نريد من ودجات ضمنها، وتطبيق الأطرزة التي نريدها عليها.

بالإضافة لكل ذلك نقوم بتعريف طريقة تعيد سند إلى هذا المركب.

مثال على ذلك:

```
// نقوم بتعريف مركب يمثل ترويسة
صنف الـترويسة {
    @حقنة عرف مركب: مـركب؛

    عملية هذا~هيئ() {
        // نقوم بتعريف مساحة مستطيلة لتحوي الودجات الفرعية لهذا المركب
        هذا.المشهد = صـندوق({}).{
            // نقوم بتطبيق بعض الأطرزة
            الطراز.{
                العرض = مـسافة.مئوي(100)؛
                الطول = مـسافة.نقاط(85)؛
                الحشوة = مـسافة4.نقاط(4، 0، 0، 0)؛
                الخلفية = خـلفية(_لون_اصلي_)؛
                سمك_الإطار = مـسافة4.نقاط(1.5)؛
                طراز_الإطار = طـراز_إطار._مستمر_؛
                لون_الإطار = _لون_اصلي_؛
                ملء_السطر = مـلء_سطر._مسافة_بينية_؛
                الإظهار = إظـهار._مرن_؛
                النسق = نـسق._عمود_؛
            }؛
            // الودجات الفرعية
            أضف_فروع({
                // نضيف هنا الودجات الفرعية للمركب
            });
        }
    }

    // جعل عملية `الـترويسة()` ترجع سندا ذكيًا بدل إنشاء متغير مؤقت.
    عملية هذا_الصنف(): سـندنا[الـترويسة] {
        أرجع سـندنا[الـترويسة].أنشئ()؛
    }
}

```

<div dir=ltr>

```
class Header {
    @injection def component: Component;

    handler this~init() {
        // نقوم بتعريف مساحة مستطيلة لتحوي الودجات الفرعية لهذا المكون
        this.view = Box({}).{
            // نقوم بتطبيق بعض الأطرزة
            style.{
                width = Length.percent(100) - Length.pt(3);
                height = Length.pt(85);
                padding = Length4.pt(4, 0, 0, 0);
                background = Background(PRIMARY_COLOR);
                borderWidth = Length4.pt(1.5);
                borderStyle = BorderStyle.SOLID;
                borderColor = PRIMARY_COLOR;
                justify = Justify.SPACE_BETWEEN;
                display = Display.FLEX;
                layout = Layout.COLUMN;
            };
            // الودجات الفرعية
            addChildren({
                // نضيف هنا الودجات الفرعية للمركب
            });
        }
    }

    // جعل عملية `الـترويسة()` ترجع سندا ذكيًا بدل إنشاء متغير مؤقت.
    handler this_type(): SrdRef[Header] {
        return SrdRef[Header].construct();
    }
}
```

</div>

### مـبدال (Switcher)

يوفر هذا المركب مساحة يمكن عرض مشهد فيها وتبديله ديناميكيًا في أي وقت مع إمكانية تطبيق مؤثرات
انتقال لعملية التبديل.

```
صنف مـبدال {
    عرف حدد_الانتقال(اسم: نـص، انتقال: انـتقال_مبدال)؛
    عرف بدل_إلى(فرع: سـندنا[ودجـة])؛
    عرف بدل_إلى(فرع: سـندنا[ودجـة]، اسم_الانتقال: نـص)؛
    عرف أيجب_الاحتفاظ_بالفرع(فرع: سند[ودجـة])؛
}
```

<div dir=ltr>

```
class Switcher {
    handler this.setTransition(name: String, transition: SwitcherTransition);
    handler this.switchTo(child: SrdRef[Widget]);
    handler this.switchTo(child: SrdRef[Widget], transitionName: String);
    def shouldPreserveChild: closure (child: ref[Widget]): Bool;
}
```

</div>

#### حدد_الانتقال (setTransition)

```
عملية هذا.حدد_الانتقال(اسم_الانتقال: نـص، الانتقال: انـتقال_مبدال)؛
```

<div dir=ltr>

```
handler this.setTransition(name: String, transition: SwitcherTransition);
```

</div>

إضافة مؤثرات انتقال للاستخدام لاحقًا في دالة `بدل_إلى` (`switchTo`).

المعطيات:

* `اسم_الانتقال` (`name`) أي اسم يميز هذا الانتقال عن غيره.

* `الانتقال` (`transition`) تعريف الانتقال الذي نرغب في تطبيقه.


#### بدل_إلى (switchTo)

```
عملية هذا.بدل_إلى(فرع: سـندنا[ودجـة])؛
عملية هذا.بدل_إلى(فرع: سـندنا[ودجـة]، اسم_الانتقال: نـص)؛
```

<div dir=ltr>

```
handler this.switchTo(child: SrdRef[Widget]);
handler this.switchTo(child: SrdRef[Widget], transitionName: String);
```

</div>

تبديل المشهد الحالي إلى مشهد جديد. الصيغة الأولى تبدل المشهد دون أي مؤثرات انتقال، بينما بدل
الصيغة الثانية المشهد مع مؤثرات الانتقال المحددة بالاسم المعطى. الاسم يجب أن يطابق أحد المؤثرات
المضافة باستخدام دالة `حدد_الانتقال` (`setTransition`).

#### أيجب_الاحتفاظ_بالفرع (shouldPreserveChild)

```
عرف أيجب_الاحتفاظ_بالفرع: مغلفة (الفرع: سند[ودجـة]): ثـنائي؛
```

<div dir=ltr>

```
def shouldPreserveChild: closure (child: ref[Widget]): Bool;
```

</div>

مغلفة للاستدعاء العكسي، تُستدعى عند خروج مشهد من الصورة (أي بعد التبديل إلى مشهد آخر) لتحديد ما إن
كنا نرغب بحذف المشهد القديم أم إبقاؤه مخفيا للاستخدام لاحقًا. في بعض الحالات قد ترغب بإبقاء المشهد
وعدم حذفه في حال رجع المستخدم إليه لاحقًا كي لا يخسر المستخدم أي مدخلات أحدثها في المشهد. إن أرجعت
هذه المغلفة قيمة 1 فإن المشهد سيبقى مخفيًا في الذاكرة ويمكن العودة إليه لاحقًا باستخدام دالة
`بدل_إلى` (`switchTo`) وإعطاؤها نفس الودجة، أما في حالة أرجعت الدالة 0 أو لم يحدد المستخدم هذه
المغلفة من الأساس فإن المشهد سيُحذف من المتصفح وسيُعاد إنشاؤه عند استدعاء `بدل_إلى` مجددًا. في هذه
الحالة حتى لو احتفظت بالودجة في الذاكرة فإن عنصر DOM المقابل لهذه الودجة سيحذف من المتصفح
وبالتالي يخسر المستخدم ما أحدثه على ذلك العنصر في المتصفح.


### انـتقال_مبدال (SwitcherTransition)

```
صنف انـتقال_مبدال {
    عرف طراز_المشهد_الحالي: طـراز؛
    عرف طراز_المشهد_التالي: طـراز؛
    عرف المدة_الكلية: صـحيح؛
}
```

<div dir=ltr>

```
class SwitcherTransition {
    def currentViewStyle: Style;
    def incomingViewStyle: Style;
    def totalDuration: Int;
}
```

</div>

يحمل هذا الصنف خصائص الانتقال المستخدم في مبدال. يعتمد تعريف الانتقال على الطرز وما تملكه من خصائص
للتحريك والمؤثرات.

`طراز_المشهد_الحالي` (`currentViewStyle`) طراز المشهد الحالي الذي سيخرج من مساحة العرض. هذا الطراز
يجب أن يحمل تعريف المؤثرات الحركية للمشهد، كأن يحدد مؤثرات انزلاق المشهد جانبيا إلى خارج مساحة العرض.

`طراز_المشهد_التالي` (`incomingViewStyle`) طراز المشهد الجديد الذي سيدخل مساحة العرض. هذا الطراز
يجب أن يحمل تعريف المؤثرات الحركية للمشهد، كأن يحدد مؤثرات انزلاق المشهد جانبيًا من خارج مساحة العرض
إلى وسطها.

`المدة_الكلية` (`totalDuration`) المدة الكاملة للانتقال.

راجع شروح الطرز لمزيد من المعلومات حول تعريف المؤثرات الحركية.


### مـبدال_توجيه (RoutingSwitcher)

يمثل هذا الصتف مبدال يقوم بالتبديل بين المشاهد بناءًا على المسارات.

#### عند_المسار_المجهول (onUnknownRoute)

```
عرف عند_المسار_المجهول: مغلف(سند[مـبدال_توجيه]، حـمولة_مسار)؛
```

<div dir=ltr>

```
def onUnknownRoute: closure(ref[RoutingSwitcher], RoutePayload);
```

</div>

مغلفة للاستدعاء العكسي تُستدعى عندما يكون المسار غير معروف بالنسبة للمبدال.

#### تسلسل_المسار_الحالي (currentRouteIndex)

عدد صحيح يمثل تسلسل المسار الحالي ضمن المسارات المعرفة في هذا المبدال.

#### مسار (route)

```
عرف مسار(مسار: نـص): سند[مـنادى_مسار]؛
عرف مسار(مسار: مـؤشر[مـحرف]): سند[مـنادى_مسار]؛
عرف مـنادى_مسار: لقب مغلفة(حـمولة_مسار): سـندنا[ودجـة]؛
```

<div dir=ltr>

```
handler this.route(r: String): ref[RouteCallback];
handler this.route(r: CharsPtr): ref[RouteCallback];
def RouteCallback: alias closure(RoutePayload): SrdRef[Widget];
```

</div>

تحديد الدالة العكسية التي ستُستدعى عند تغير المسار إلى المسار المُعطى في هذه الدالة. دالة `مسار`
تُرجع سندًا للمغلفة كي يتمكن المستخدم من تحديد قيمة للمغلفة. يجب على المغلفة أن ترجع سندا إلى
الودجة التي ستُعرض في المبدال. معطى دالة `مسار` تعبير نمطي (regular expression).

#### حدد_الانتقال (setTransition)

```
عرف حدد_الانتقال(أمامي: انـتقال_مبدال، خلفي: انـتقال_مبدال)؛
```

<div dir=ltr>

```
handler this.setTransition(forward: SwitcherTransition, backward: SwitcherTransition);
```

</div>

تحديد الانتقالين المطلوبين في حالتي الذهاب إلى الرابط السابق أو التالي.

#### استنتج_تسلسل_المسار_الحالي (determineCurrentRouteIndex)

```
عرف استنتج_تسلسل_المسار_الحالي(): صـحيح؛
عرف استنتج_تسلسل_المسار_الحالي(حمولة_مسار: سند[حـمولة_مسار]): صـحيح؛
```

<div dir=ltr>

```
handler this.determineCurrentRouteIndex(): Int;
handler this.determineCurrentRouteIndex(rp: ref[RoutePayload]): Int;
```

</div>

تستنتج تسلسل المسار الحالي نسبة للمسارات المعرفة في هذا المبدال. الصيغة الثانية تسمح بإرجاع بيانات
المسار الحالي بالإضافة لتسلسله.


### مـكداس (Stack)

يسمح هذا الصنف للمستخدم بتكديس عدة مشاهد فوق بعضها البعض، مع إمكانية إضافة تحريك عند إضافة مشهد وعند
إزالته.

#### حدد_الانتقال (setTransition)

```
عرف حدد_الانتقال(اسم: نـص، انتقال: انـتقال_مـكداس)؛
```

<div dir=ltr>

```
handler this.setTransition(name: String, transition: StackTransition);
```

</div>

إضافة مؤثرات انتقال للاستخدام لاحقًا في دالة `أضف` (`push`).

المعطيات:

* `اسم_الانتقال` (`name`) أي اسم يميز هذا الانتقال عن غيره.

* `الانتقال` (`transition`) تعريف الانتقال الذي نرغب في تطبيقه.

#### أضف (push)

```
عرف أضف(فرع: سـندنا[ودجـة])؛
عرف أضف(فرع: سـندنا[ودجـة]، اسم_الانتقال: نـص)؛
```

<div dir=ltr>

```
handler this.push(child: SrdRef[Widget]);
handler this.push(child: SrdRef[Widget], transitionName: String);
```

</div>

تستمل لإضافة مشهد إلى المكداس. المشهد المضاف يظهر أمام المشاهد الحالية. الصيغة الثانية من الدالة
تسمح بتحديد الانتقال (المؤثرات الحركية) المطلوبة. بدون تحديد الانتقال ستتم الإضافة بشكل لحظي دون أي
مؤثرات. اسم الانتقال يجب أن يطابق اسم أحد الانتقالات المضافة بدالة `حدد_الانتقال`.


#### أزل (pop)

```
عرف أزل()؛
عرف أزل(اسم_الانتقال: نـص)؛
```

<div dir=ltr>

```
handler this.pop();
handler this.pop(transitionName: String);
```

</div>

تستعمل لإزالة مشهد من قمة المكداس. الصيغة الأولى تزيل المشهد لحظيا دون مؤثرات بينما تسمح الصيغة
الثانية بتحديد الانتقال المراد تطبيقه. اسم الانتقال يجب أن يطابق اسم أحد الانتقالات المضافة بدالة
`حدد_الانتقال`.


### مـكداس_توجيه (RoutingStack)

مكداس يعتمد على المسار الحالي لتكديس المشاهد وإزالتها تلقائيًا.

#### عند_المسار_المجهول (onUnknownRoute)

```
عرف عند_المسار_المجهول: مغلف(سند[مـبدال_مـكداس]، حـمولة_مسار)؛
```

<div dir=ltr>

```
def onUnknownRoute: closure(ref[RoutingStack], RoutePayload);
```

</div>

مغلفة للاستدعاء العكسي تُستدعى عندما يكون المسار غير معروف بالنسبة للمكداس.

#### مسار (route)

```
عرف مسار(مسار: نـص): سند[مـنادى_مسار]؛
عرف مسار(مسار: مـؤشر[مـحرف]): سند[مـنادى_مسار]؛
عرف مـنادى_مسار: لقب مغلفة(حـمولة_مسار): سـندنا[ودجـة]؛
```

<div dir=ltr>

```
handler this.route(r: String): ref[RouteCallback];
handler this.route(r: CharsPtr): ref[RouteCallback];
def RouteCallback: alias closure(RoutePayload): SrdRef[Widget];
```

</div>

تحديد الدالة العكسية التي ستُستدعى عند تغير المسار إلى المسار المُعطى في هذه الدالة. دالة `مسار`
تُرجع سندًا للمغلفة كي يتمكن المستخدم من تحديد قيمة للمغلفة. يجب على المغلفة أن ترجع سندا إلى
الودجة التي ستُعرض في المكداس. معطى دالة `مسار` تعبير نمطي (regular expression).

بعكس `مـبدال_توجيه`، يستمر `مـكداس_توجيه` بمطابقة كافة المسارات بدءًا من أول مسار حتى يصل مسارًا
غير مطابق، ومع كل مطابقة يقوم بإضافة المشهد. وعندما يصل مسارا غير مطابق يُزيل المشهد المقابل لذلك
المسار تلقائيًا. على سبيل المثال، إن حددت المسارات التالية:
ا: /أول/
ب: /أول/ثاني/
ت: /أول/ثاني/ثالث/
ث: /أول/ثاني/ثالث/رابع/
ثم انتقل المستخدم مباشرة إلى المسار `/أول/ثاني/ثالث/` فإن المكداس سيضيف المسارات ا و ب و ت وليس
فقط ت. وإذا انتقل المستخدم من مسار `/أول/ثاني/` إلى `/أول/ثاني/ثالث/` فإن المكداس سيضيف فقط ت. أما
إذا انتقل من `/أول/ثاني/ثالث/` إلى `/أول/` فإن المكداس سيزيل المشهدين ت و ب، وهكذا.

#### حدد_الانتقال (setTransition)

```
عرف حدد_الانتقال(إضافة: انـتقال_مكداس، إزالة: انـتقال_مكداس)؛
```

<div dir=ltr>

```
handler this.setTransition(pushing: StackTransition, popping: StackTransition);
```

</div>

تستعمل هذه الطريقة لتحديد الانتقال لحالتي الإضافة والإزالة.


### إسـفيجي_ضمنية (EmbeddedSvg)

تستخدم لتضمين صورة SVG ضمن البرنامج التنفيذي بدل تحميلها بعد بدء البرنامج. هذا الصنف قالب يستلم
معطى واحد وهو المسار إلى ملف SVG.

```
صـنف إسـفيجي_ضمنية [مسار_إسفيجي: نص]
```

<div dir=ltr>

```
class EmbeddedSvg [filename: string]
```

</div>

المثال التالي يوضح طريقة الاستخدام:

```
    صـندوق().{
        أضف_فروع({ إسـفيجي_ضمنية["المارد/أيقونة.svg"]() })؛
    }
```

<div dir=ltr>

```
Box().{
    addChildren({ EmbeddedSvg["resources/icon.svg"] });
}
```

</div>


### مـزلق (SlidingPanel)

مركب `مـزلق` هو لوحة واجهة مستخدم تنزلق من حافة الشاشة، وتظهر فوق المحتوى الرئيسي. يستخدم عادة
لقوائم التنقل، لوحات الإعدادات، المرشحات، أو محتوى إضافي يجب أن يكون متاحًا ولكن ليس مرئيًا دائمًا.

```
صنف مـزلق {
    @حقنة عرف مركب: مـركب؛

    // الخصائص
    عرف مفتوح: ثـنائي؛
    عرف الموقع: مـوقع_مزلق؛
    عرف حجم_اللوحة: سـندنا[مـسافة]؛
    عرف مدة_التحريك: عـائم؛
    عرف أغلق_عند_نقر_الخلفية: ثـنائي؛

    // الوظائف
    عملية هذا.حدد_المحتوى(فرع: سـندنا[ودجـة])؛
    عملية هذا.افتح()؛
    عملية هذا.أغلق()؛
    عملية هذا.بدل()؛
}

صنف مـوقع_مزلق {
    عرف _يسار_: نـص؛    // المزلق ينزلق من اليسار
    عرف _يمين_: نـص؛    // المزلق ينزلق من اليمين
    عرف _فوق_: نـص؛     // المزلق ينزلق من الأعلى
    عرف _تحت_: نـص؛     // المزلق ينزلق من الأسفل
}
```

<div dir=ltr>

```
class SlidingPanel {
    @injection def component: Component;

    // Properties
    def isOpen: Bool;
    def position: SlidingPanelPosition;
    def panelSize: SrdRef[Length];
    def animationDuration: Float;
    def closeOnBackdropClick: Bool;

    // Methods
    handler this.setContent(child: SrdRef[Widget]);
    handler this.open();
    handler this.close();
    handler this.toggle();
}

class SlidingPanelPosition {
    def LEFT: String;
    def RIGHT: String;
    def TOP: String;
    def BOTTOM: String;
}
```

</div>

#### الخصائص

- **مفتوح** (`isOpen`): `ثـنائي` - يتحكم فيما إذا كان المزلق مفتوحًا أو مغلقًا حاليًا.

- **الموقع** (`position`): `مـوقع_مزلق` - يحدد من أي حافة من الشاشة ينزلق المزلق. الخيارات: `_يسار_`،
  `_يمين_`، `_فوق_`، أو `_تحت_`.

- **حجم_اللوحة** (`panelSize`): `سـندنا[مـسافة]` - العرض (للمزالق اليسرى/اليمنى) أو الارتفاع (للمزالق
  العلوية/السفلية) للوحة المزلق. القيمة الافتراضية 300 نقطة إذا لم يتم تحديدها.

- **مدة_التحريك** (`animationDuration`): `عـائم` - مدة حركة الانزلاق بالثواني. القيمة الافتراضية 0.3 ثانية.

- **أغلق_عند_نقر_الخلفية** (`closeOnBackdropClick`): `ثـنائي` - ما إذا كان النقر على الخلفية (الطبقة
  المُعتمة) يجب أن يغلق المزلق. القيمة الافتراضية `صـحيح`.

#### الوظائف

- **حدد_المحتوى(فرع: سـندنا[ودجـة])** (`setContent`) - يحدد المحتوى الذي سيتم عرضه داخل لوحة المزلق.

- **افتح()** (`open`) - يفتح المزلق مع حركة انزلاق سلسة.

- **أغلق()** (`close`) - يغلق المزلق مع حركة انزلاق سلسة.

- **بدل()** (`toggle`) - يبدل المزلق بين الحالة المفتوحة والمغلقة.

#### مثال الاستخدام

```
اشمل "مـحا"؛
مـحا.اشمل_ملف("Alusus/WebPlatform"، "مـنصة_ويب.أسس")؛
استخدم مـنصة_ويب؛

@منفذ_مرئي["/"]
دالة _رئيسية_ {
    عرف مزلقي: سـندنا[مـزلق]؛

    نـافذة.النموذج.حدد_المشهد(صـندوق({}).{
        أضف_فروع({
            // زر لفتح المزلق
            الـزر(نـص("افتح القائمة")).{
                عند_الضغط.اربط(مغلفة (مزلقي: كسند)&(و: سند[ودجـة]، ح: سند[صـحيح]) {
                    مزلقي.افتح()؛
                })؛
            }،

            // مزلق من الجانب الأيسر
            مـزلق().{
                مزلقي = هذا؛
                الموقع = مـوقع_مزلق._يسار_؛
                حجم_اللوحة = مـسافة.نقاط(320)؛

                حدد_المحتوى(
                    صـندوق({}).{
                        الطراز.{
                            الحشوة = مـسافة4.نقاط(20)؛
                        }؛
                        أضف_فروع({
                            كـتابة(نـص("قائمة التنقل"))،
                            الـزر(نـص("إغلاق")).{
                                عند_الضغط.اربط(مغلفة (مزلقي: كسند)&(و: سند[ودجـة]، ح: سند[صـحيح]) {
                                    مزلقي.أغلق()؛
                                })؛
                            }
                        })؛
                    }
                )؛
            }
        })؛
    })؛

    نفذ_حلقة_معالجة_الأحداث()؛
}
```

<div dir=ltr>

```
import "Apm";
Apm.importFile("Alusus/WebPlatform");
use WebPlatform;

@uiEndpoint["/"]
func main {
    def myPanel: SrdRef[SlidingPanel];

    Window.instance.setView(Box({}).{
        addChildren({
            // Button to open panel
            Button(String("Open Menu")).{
                onClick.connect(closure (myPanel: by_ref)&(w: ref[Widget], p: ref[Int]) {
                    myPanel.open();
                });
            },

            // Panel from left side
            SlidingPanel().{
                myPanel = this;
                position = SlidingPanelPosition.LEFT;
                panelSize = Length.pt(320);

                setContent(
                    Box({}).{
                        style.{
                            padding = Length4.pt(20);
                        };
                        addChildren({
                            Text(String("Navigation Menu")),
                            Button(String("Close")).{
                                onClick.connect(closure (myPanel: by_ref)&(w: ref[Widget], p: ref[Int]) {
                                    myPanel.close();
                                });
                            }
                        });
                    }
                );
            }
        });
    });

    runEventLoop();
}
```

</div>

#### حالات الاستخدام الشائعة

1. **قائمة التنقل** - مزلق أيسر أو أيمن يحتوي على روابط التنقل
2. **لوحة الإعدادات** - مزلق أيمن مع خيارات التكوين
3. **المرشحات** - مزلق جانبي مع عناصر تحكم المرشح للقوائم أو نتائج البحث
4. **اللوحة السفلية** - مزلق سفلي لقوائم الإجراءات بأسلوب الهواتف المحمولة
5. **الإشعارات** - مزلق علوي لعرض الإعلانات

#### المميزات

- حركات انزلاق سلسة للدخول والخروج
- طبقة خلفية معتمة بشفافية قابلة للتخصيص
- إدارة تلقائية لترتيب العمق (z-index)
- إغلاق بالنقر خارج المزلق (اختياري)
- حجم وموقع قابل للتخصيص
- تأثيرات ظل تظهر عند الفتح

#### أمثلة كاملة

شاهد الأمثلة الكاملة العاملة في:
- [مثال_مزلق.أسس](../Examples/مثال_مزلق.أسس)
- [sliding_panel_example.alusus](../Examples/sliding_panel_example.alusus) (النسخة الإنجليزية)

</div>

