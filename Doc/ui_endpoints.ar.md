# WebPlatform

[[English]](ui_endpoints.en.md)

[[رجوع]](../README.ar.md)

<div dir=rtl>

## إنشاء المنافذ المرئية

يمكن إنشاء منفذ مرئي عن طريق كتابة دالة عادية تقوم بما نريده وإضافة المبدل `@منفذ_مرئي` إليها.
هذا المبدل يرشد مـنصة_ويب لترجمة هذه الدالة إلى ويب أسمبلي ثم توفيرها عبر خادم الويب على المنفذ
المحدد في معطى هذا المبدل.

بالإضافة لهذا المبدل الرئيسي، تتوفر المبدلات التالية للتحكم بالمنفذ المولد:

* `@عنوان` (`@title`): تحديد عنوان الصفحة في المتصفح.

* `@أيقونة` (`@icon`): تحديد مسار لصورة كي تستخدم كأيقونة لهذه الصفحة.

* `@تطبيق_ويب` (`@webApp`): تحديد مسار ملف manifest المستخدم لجعل التطبيق يعمل
  كتطبيق PWA (Progressive Web App).
  
* `@خزن_مسبق` (`@preCache`): يستلم قائمة من سلاسل المحارف كمعطيات. هذه المعطيات تحدد الملفات التي
  نرغب بجعلها تخزن استباقيا على جهاز المستخدم عند تنصيب التطبيق لتمكينه من العمل بدون اتصال
  بالإنترنت.
  
* `@خزن_تفاعلي` (`@dynCache`): يستلم قائمة من سلاسل المحارف كمعطيات. هذه المعطيات تحمل تعابير
  نمطية (regular expressions) تحدد الملفات التي نرغب بتخزينها على جهاز المستخدم ديناميكيا كي
  لا يُعاد تحميلها مجددا. الملف الذي يطابق مساره أحد هذه المعطيات يُخزن في الجهاز بعد أول تحميل له.

### مثال

```
// منفذ بياني لصفحة واجهة مستخدم تمثل صفحة حول
@منفذ_مرئي["/about"]
@عنوان["مثال منصة ويب - الدردشة"]
دالة عن_البرنامج {
    نـافذة.النموذج.الطراز.{
        الحشوة = مـسافة4.نقاط(0)؛
        الهامش = مـسافة4.نقاط(0)؛
    }؛
    نـافذة.النموذج.حدد_المشهد(صـندوق({}).{
        الطراز.{
            الاتجاه = اتـجاه._من_اليمين_؛
            الإظهار = إظـهار._مرن_؛
            النسق = نـسق._عمود_؛
        }؛
        أضف_فروع({
            كـتابة(نـص("مثال الدردشة")).{ الطراز.{
                لون_الخط = لـون(0، 0، 0)؛
                حجم_الخط = مـسافة.نقاط(30.0)؛
            } }،
            كـتابة(نـص("طُور باستخدام منصة ويب الأسس")).{ الطراز.{
                لون_الخط = لـون(0، 0، 0)؛
                حجم_الخط = مـسافة.نقاط(20.0)؛
            } }
        })
    })؛

    نفذ_حلقة_معالجة_الأحداث()؛
}
```

<div dir=ltr>

```
// منفذ بياني لصفحة واجهة مستخدم تمثل صفحة حول
@uiEndpoint["/about"]
@title["WebPlatform Example - Chat"]
func about {

    Window.instance.style.{
        padding = Length4.pt(0);
        margin = Length4.pt(0);
    };
    // هنا نحدد المشهد الخاص بهذه الصفحة
    Window.instance.setView(Box({}).{
        style.{
            display = Display.FLEX;
            layout = Layout.COLUMN;
        };
        addChildren({
            Text(String("Example")).{ style.{
                fontColor = Color(0, 0, 0);
                fontSize = Length.pt(30.0);
            } },
            Text(String("Built using Alusus Web Platform")).{ style.{
                fontColor = Color(0, 0, 0);
                fontSize = Length.pt(20.0);
            } }
        })
    });

    runEventLoop();
}
```

</div>

نلاحظ أننا قمنا بتعريف دالة لتشغيل واجهة المستخدم، وأضفنا لها المبدل `@uiEndpoint` مع تحديد المسار
وأضفنا كذلك مبدل `@عنوان` لتحديد عنوان الصفحة.

في الدالة حددنا المشهد الخاص بهذه الصفحة باستعمال `نـافذة.النموذج.حدد_المشهد` وتمرير المشهد إليها.
في مثالنا هذا ستكون مجرد مساحة مستطيلة تحوي على نصين، مع تطبيق بعض الأطرزة عليها.

وفي نهاية الدالة نقوم دائماً باستدعاء الدالة `نفذ_حلقة_معالجة_الأحداث` والتي تقوم بتشغيل حلقة
الأحداث.

### رسائل النظام

ترسل مـنصة_ويب إشعارًا للمستخدم في بعض الحالات ويمكن للمستخدم استلامها عبر حدث 
`عند_استلام_رسالة` (`onMessage`). حمولة هذه الرسائل (من صنف جـيسون) تحتوي على قيمة نصية
باسم `type` يمكن للمستخدم التحقق منها لمعرفة نوع الرسالة. تتوفر الرسائل التالية:

* ‏`app_update_available`: تُرسل عندما يتوفر تحديث لتطبيق الويب. يمكن للمستخدم الاستجابة
  لهذه الرسالة عبر إظهار إشعار للمستخدم بوجود تحديث، ومن ثم استدعاء `حدث_التطبيق` (`updateApp`).

* ‏`app_install_prompt`: تُرسل عندما تتوفر واجهة تنصيب التطبيق على الجهاز. يمكن للمستخدم الاستجابة
  لهذه الرسالة بإظهار إشعار للمستخدم بإمكانية تنصيب تطبيقه، ومن ثم استدعاء 
  `اعرض_واجهة_تنصيب_التطبيق` (`showAppInstallPrompt`).

</div>

