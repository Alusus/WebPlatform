# WebPlatform

[[English]](chat_example.en.md)

[[رجوع]](../README.ar.md)

## أمثلة

### تطبيق دردشة

<div dir=rtl>

```
اشمل "مـتم/نـظام"؛
اشمل "بـناء"؛
اشمل "مـحا"؛
مـحا.اشمل_ملف("Alusus/WebPlatform"، "مـنصة_ويب.أسس")؛
مـحا.اشمل_ملف("Alusus/Http"، "بـننف.أسس")؛
مـحا.اشمل_ملف("Alusus/Json"، "جـيسون.أسس")؛
اشمل "مـتم/سندات"؛
اشمل "مـتم/مـصفوفة"؛
اشمل "مـتم/نـص"؛
اشمل "مـتم/طـرفية"؛
اشمل "مغلفة"؛

استخدم مـتم؛
استخدم مـنصة_ويب؛

//==============================================================================
// الخادم

// عدد الرسائل الأعظمي
عرف _حد_الرسائل_: 12؛
// مصفوفة لتخزين الرسائل
عرف رسائل : مـصفوفة[نـص]؛

// و له المسار المعطى POST منفذ بياني يقبل الطريقة
// يستعمل هذا المنفذ لإرسال الرسائل
@منفذ_بياني["POST"، "/messages"]
دالة أضف_رسالة (اتصال:مؤشر[بـننف.اتـصال]){
    عرف بيانات: مصفوفة[مـحرف،1024]؛
    // نقوم بجلب المعلومات التي تم إرسالها و تخزينها في الصوان
    عرف حجم_البيانات: صحيح = بـننف.اقرأ(اتصال، بيانات~مؤشر، بيانات~حجم)؛
    // في حال تجاوز الحد الأعظم للرسائل نحذف أقدم رسالة
    إذا رسائل.هات_الطول() >= _حد_الرسائل_ رسائل.أزل(0)؛
    // نضيف الرسالة الجديدة
    رسائل.أضف(نـص(بيانات~مؤشر، حجم_البيانات))؛
    بـننف.اطبع(اتصال، "HTTP/1.1 200 Ok\r\n\r\n")؛
}

// و له المسار المعطى GET منفذ بياني يقبل الطريقة
// يستعمل هذا المنفذ لجلب الرسائل
@منفذ_بياني["GET"، "/messages"]
دالة هات_الرسائل (اتصال: مؤشر[بـننف.اتـصال]) {
    // ادمج الرسائل سويةً مع سطر فارغ بين كل رسالة و الأخرى
    عرف الرد: نـص = نـص.ادمج(رسائل، "<br>")؛
    // نضع الترويسات اللازمة
    بـننف.اطبع(اتصال، "HTTP/1.1 200 Ok\r\n")؛
    بـننف.اطبع(اتصال، "Content-Type: text/plain\r\n")؛
    بـننف.اطبع(اتصال، "Cache-Control: no-cache\r\n")؛
    بـننف.اطبع(اتصال، "Content-Length: %d\r\n\r\n"، الرد.هات_الطول())؛
    // نضع محتوى الصوان الذي يحمل الرسائل
    بـننف.اطبع(اتصال، الرد.صوان)؛
}

// مسار الموارد على الخادم
@مسار_موارد عرف مسار_الموارد: "Assets/"؛


//=============================================================================
// مـركبات واجهة المستخدم

عرف خط_الكتابة: "30px Arial"؛ 

عرف _لون_اصلي_: لـون("8e50ef")؛
عرف _لون_فاتح_: لـون("c380ff")؛

// نقوم بتعريف مركب يمثل زر القائمة
صنف خـيار_القائمة {
    @حقنة عرف مركب: مـركب؛

    عملية هذا~هيئ(رابط: نـص, عنوان: نـص, أيقونة: نـص) {
        // مساحة مستطيلة لتحوي الودجات الفرعية
        هذا.المشهد = صـندوق({}).{
            // نطبق بعض الأطرزة
            الطراز.{
                الحشوة = مـسافة4.نقاط(3)؛
                الخلفية = خـلفية(_لون_فاتح_)؛
                الإظهار = إظـهار._مرن_؛
                النسق = نـسق._سطر_؛
                المحاذاة = مـحاذاة._وسط_؛
            }؛
            // نطبق طراز على ودجة فرعية من الفئة أيقونة
            الطراز(">>icon").{
                الحشوة = مـسافة4.نقاط(0, 4)؛
                العرض = مـسافة.نقاط(20)؛
                الطول = مـسافة.نقاط(0)؛
                انتقال_الطول = انـتقال(0.2)؛
            }
            // نطبق طراز على ودجة فرعية من الفئة وسم
            الطراز(">>label").{
                حجم_الخط = مـسافة.نقاط(16.0)؛
                لون_الخط = لـون("eee")؛
            }
            // نقوم بتطبيق طراز على ودجة فرعية من الفئة أيقونة 
            // هذا الطراز يطبق عندما نحوم بالمؤشر على الأيقونة
            الطراز({ مـحدد_حالة._حوم_, ">>icon" }).{
                الطول = مـسافة.نقاط(20)؛
            }
            // نقوم بتطبيق طراز على ودجة فرعية من الفئة وسم 
            // هذا الطراز يطبق عندما نحوم بالمؤشر على الوسم
            الطراز({ مـحدد_حالة._حوم_, ">>label" }).{
                حجم_الخط = مـسافة.نقاط(16.0)؛
                لون_الخط = لـون("fff")؛
            }
            // الودجات الفرعية
            أضف_فروع({
                // ودجة رابط
                // و الذي يحوي على ودجة فرعية هي نص
                ارتـباط_تشعبي(رابط, كـتابة(عنوان).{
                    // فئة هذا الرابط هو وسم
                    اسم_الفئة = نـص("label")
                }).{ الطراز.زخرفة_النص = زخـرفة_نص._بلا_ },
                // ودجة رابط
                // و الذي يحوي على ودجة فرعية هي صورة
                ارتـباط_تشعبي(رابط, صـورة().{
                    الرابط = أيقونة؛
                    // فئة هذا الرابط هو أيقونة
                    اسم_الفئة = نـص("icon")؛
                })
            })؛
        }؛
    }

    عملية هذا_الصنف(رابط: نـص, عنوان: نـص, أيقونة: نـص): سـندنا[خـيار_القائمة] {
        أرجع سـندنا[خـيار_القائمة]().{ احجز()~هيئ(رابط، عنوان، أيقونة) }؛
    }
}

// نقوم بتعريف مركب يمثل ترويسة
صنف الـترويسة {
    @حقنة عرف مركب: مـركب؛

    عملية هذا~هيئ() {
        // نقوم بتعريف مساحة مستطيلة لتحوي الودجات الفرعية لهذا المركب
        هذا.المشهد = صـندوق({}).{
            // نقوم بتطبيق بعض الأطرزة
            الطراز.{
                العرض = مـسافة.مئوي(100)؛
                الطول = مـسافة.نقاط(85)؛
                الحشوة = مـسافة4.نقاط(4، 0، 0، 0)؛
                الخلفية = خـلفية(_لون_اصلي_)؛
                سمك_الإطار = مـسافة4.نقاط(1.5)؛
                طراز_الإطار = طـراز_إطار._مستمر_؛
                لون_الإطار = _لون_اصلي_؛
                ملء_السطر = مـلء_سطر._مسافة_بينية_؛
                الإظهار = إظـهار._مرن_؛
                النسق = نـسق._عمود_؛
            }؛
            // الودجات الفرعية
            أضف_فروع({
                // مساحة مستطيلة تعرف المنطقة الأولى من المركب
                صـندوق({}).{
                    // نطبق بعض الأطرزة على هذه المساحة
                    الطراز.{
                        الإظهار = إظـهار._مرن_؛
                        النسق = نـسق._سطر_؛
                        ملء_السطر = مـلء_سطر._مسافة_بينية_؛
                        المحاذاة = مـحاذاة._وسط_؛
                    }؛
                    // الودجات الفرعية
                    أضف_فروع({
                        // مساحة تحوي نص
                        صـندوق({ كـتابة(نـص("أمثلة منصة ويب الأسس - الدردشة")).{ الطراز.{
                            لون_الخط = لـون("fff")؛
                            حجم_الخط = مـسافة.نقاط(21.0)؛
                            الطول = مـسافة.مئوي(100)؛
                            الخلفية = خـلفية(_لون_اصلي_)؛
                        } } })،
                        // صورة
                        صـورة().{
                            الرابط = نـص("/Assets/wblogo.svg")؛
                            الطراز.{
                                الطول = مـسافة.نقاط(50)؛
                                الحشوة = مـسافة4.نقاط(0، 10)؛
                            }؛
                        }
                    })؛
                }،
                // مساحة مستطيلة تعرف المنطقة الثانية من المركب
                صـندوق({}).{
                    // نطبق بعض الأطرزة على هذه المساحة
                    الطراز.{
                        الإظهار = إظـهار._مرن_؛
                        النسق = نـسق._سطر_؛
                        ملء_السطر = مـلء_سطر._مسافة_بينية_؛
                    }؛
                    // الودجات فرعية
                    أضف_فروع({
                        // نقوم بتعريف زريّ قائمة
                        // أول زر يأخذنا إلى المسار الرئيسي و له النص المعطى و له الأيقونة المعطاة
                        خـيار_القائمة(نـص("/")، نـص("الرئيسية")، نـص("/Assets/chat.svg"))،
                        // ثاني زر يأخذنا إلى مسار صفحة حول و له النص المعطى و له الأيقونة المعطاة
                        خـيار_القائمة(نـص("/about")، نـص("عن البرنامج")، نـص("/Assets/about.svg"))
                    })؛
                }
            })؛
        }؛
    }

    عملية هذا_الصنف(): سـندنا[الـترويسة] {
        أرجع سـندنا[الـترويسة].أنشئ()؛
    }
}

// مركب يمثل خانة إدخال نصية
صنف مـدخل_نصي {
    @حقنة عرف مركب: مـركب؛

    // مغلف للتعامل مع الدخل الجديد
    عرف قد_تم_الإدخال: مغلفة (نـص)؛
    // ودجة إدخال نصية
    عرف مدخل_النص: سـندنا[مـدخل_نص]؛

    عملية هذا.العرض = سـندنا[مـسافة] {
        هذا.المشهد.الطراز.العرض = قيمة؛
        أرجع قيمة؛
    }

    عملية هذا.الطول = سـندنا[مـسافة] {
        هذا.المشهد.الطراز.الطول = قيمة؛
        أرجع قيمة؛
    }

    عملية هذا~هيئ() {
        عرف الكائن: سند[هذا_الصنف](هذا)؛

        هذا.المشهد = صـندوق({}).{
            الطراز.{
                الإظهار = إظـهار._مرن_؛
                النسق = نـسق._سطر_؛
                سمك_الإطار = مـسافة4.نقاط(1.5)؛
                طراز_الإطار = طـراز_إطار._مستمر_؛
                لون_الإطار = _لون_اصلي_؛
                الخلفية = خـلفية(_لون_اصلي_)؛
                ملء_السطر = مـلء_سطر._مسافة_بينية_؛
            }؛
            // الودجات الفرعية
            أضف_فروع({
                // ودجة إدخال نصية
                مـدخل_نص().{
                    الكائن.مدخل_النص = هذا؛
                    الطراز.{
                        العرض = مـسافة.مئوي(100)؛
                        الطول = مـسافة.مئوي(100)؛
                        الخلفية = خـلفية(لـون("fff"))؛
                        حجم_الخط = مـسافة.نقاط(12.0)؛
                    }؛
                    // نقوم بربط إشارة توقف الضغط على الزر بمغلف يتم عند تنفيذه التحقق
                    // فيما إذا كان الزر الذي تم ضغطه يوافق ما نريده للإرسال
                    // onNewEntry عندها يتم تحديث نص المغلفة 
                    // كما يتم إعادة المدخل النصي إلى الحالة الفارغة لاستقبال دخل جديد
                    عند_انتهاء_الكبسة.اربط(مغلفة (ودجة: سند[مـدخل_نص]، الحمولة: سند[نـص]) {
                        إذا الحمولة == "Shift+Enter" {
                            عرف بيانات_جديده: نـص = نـص("- ") + رمّز_النص_الفائق(ودجة.هات_النص()).شذب()؛
                            ودجة.حدد_النص(نـص())؛
                            إذا ليس الكائن.قد_تم_الإدخال.أهو_لعدم() الكائن.قد_تم_الإدخال(بيانات_جديده)؛
                        }
                    })؛
                }،
                الـزر(نـص("أرسل")).{
                    الطراز.{
                        الطول = مـسافة.مئوي(100)؛
                        العرض = مـسافة.نقاط(50)؛
                        الخلفية = خـلفية(لـون(200، 200، 200))؛
                        حجم_الخط = مـسافة.نقاط(16.0)؛
                        ملء_السطر = مـلء_سطر._وسط_؛
                    }؛
                    // نقوم بربط إشارة توقف الضغط على المؤشر بمغلف يتم عند تنفيذه
                    // onNewEntry تحديث نص المغلفة 
                    // كما يتم إعادة المدخل النصي إلى الحالة الفارغة لاستقبال دخل جديد
                    عند_الضغط.اربط(مغلفة (ودجة: سند[ودجـة]، الحمولة: سند[صحيح]) {
                        عرف بيانات_جديده: نـص = نـص("- ") + رمّز_النص_الفائق(الكائن.مدخل_النص.هات_النص()).شذب()؛
                        الكائن.مدخل_النص.حدد_النص(نـص())؛
                            إذا ليس الكائن.قد_تم_الإدخال.أهو_لعدم() الكائن.قد_تم_الإدخال(بيانات_جديده)؛
                    })؛
                }
            })؛
        }؛
    }

    عملية هذا_الصنف(): سـندنا[مـدخل_نصي] {
        أرجع سـندنا[مـدخل_نصي].أنشئ()؛
    }
}

// دالة لتحويل نص عادي إلى ترميز لتنف
دالة رمّز_النص_الفائق(نص: نـص): نـص {
    أرجع نص
        .استبدل("\ج"، "<br>")
        .استبدل("،"، "&comma;")
        .استبدل("\""، "&quote;")؛
}

//=============================================================================
// صفحات واجهة المستخدم

عرف _جلب_: "GET"؛
عرف _إرسال_: "POST"؛
عرف _المسار_: "/messages"؛
عرف _ترويسة_صنف_بيانات_نصي_: "Content-Type: application/text"؛

// منفذ بياني لصفحة واجهة مستخدم تمثل الصفحة الرئيسية
@منفذ_مرئي["/"، "مثال منصة ويب - الدردشة"]
دالة رئيسي {
    عرف عند_استلام_بيانات: مغلفة (جـيسون)؛

    نـافذة.النموذج.الطراز.{
        الحشوة = مـسافة4.نقاط(0)؛
        الهامش = مـسافة4.نقاط(0)؛
    }؛
    // نحدد مشهد النافذة
    نـافذة.النموذج.حدد_المشهد(صـندوق({}).{
        الطراز.{
            الطول = مـسافة.مئوي(100)؛
            ملء_السطر = مـلء_سطر._مسافة_بينية_؛
            الاتجاه = اتـجاه._من_اليمين_؛
            الإظهار = إظـهار._مرن_؛
            النسق = نـسق._عمود_؛
        }؛
        // الودجات الفرعية
        أضف_فروع({
            // نضيف مركب الترويسة
            الـترويسة()،
            صـندوق({}).{
                الطراز.{
                    العرض = مـسافة.مئوي(100)؛
                    الحشوة = مـسافة4.نقاط(5)؛
                    الإظهار = إظـهار._مرن_؛
                    النسق = نـسق._عمود_؛
                    المرونة = مـرونة(1، 1)؛
                }
                عرف عنوان_إشعارات: سـندنا[كـتابة]؛
                أضف_فروع({
                    كـتابة(نـص()).{
                        عنوان_إشعارات = هذا؛
                        الطراز.{
                            العرض = مـسافة.مئوي(100)؛
                            الطول = مـسافة.نقاط(20)؛
                            لون_الخط = لـون(200، 50، 50)؛
                            حجم_الخط = مـسافة.نقاط(10.0)؛
                        }؛
                    }،
                    كـتابة(نـص()).{
                        الطراز.{
                            العرض = مـسافة.مئوي(100)؛
                            الطول = مـسافة.مئوي(100)؛
                            لون_الخط = لـون(50، 50، 50)؛
                            حجم_الخط = مـسافة.نقاط(20.0)؛
                        }؛
                        // نقوم بتغريف مغلف سيتم تتنفيذه عند جلب البيانات من الخادم
                        عند_استلام_بيانات = مغلفة (جيسون: جـيسون) {
                            // في البداية نتحقق من أنه قد تم جلب البيانات بدون حدوث أخطاء
                            عرف الحالة: صحيح = جيسون.هات_كائن("eventData").هات_صحيح("status")؛
                            إذا الحالة >= 200 و الحالة < 300 {
                                // نستخرج البيانات
                                عرف البيانات: نـص = جيسون.هات_كائن("eventData").هات_نص("body")؛
                                // نقوم بتحديث النص
                                إذا هذا.هات_النص() != البيانات {
                                    هذا.حدد_النص(البيانات)؛
                                }
                                // نقوم بإزالة الإشعار بعد تحديث النص
                                إذا عنوان_إشعارات.هات_النص() != "" {
                                    عنوان_إشعارات.حدد_النص(نـص(""))؛
                                }
                            } وإلا {
                                // حالة حدوث خطأ
                                // نقوم بوضع إشعار حدوث خطأ
                                عنوان_إشعارات.حدد_النص(نـص("الاتصال مقطوع. رمز حالة ب.ن.ن.ف: ") + الحالة)؛
                            }
                        }؛
                    }
                })؛
            }،
            مـدخل_نصي().{
                العرض = مـسافة.مئوي(100)؛
                الطول = مـسافة.نقاط(50)؛
                // نقوم بتعريف مغلف يتم تنفيذه عند كتابة نص جديد
                قد_تم_الإدخال = مغلفة (بيانات_جديده: نـص) {
                    // نقوم بإرسال النص إلى الخادم
                    // نحدد الطريقة، المسار، نوع البيانات، و من ثم نضع البيانات
                    أرسل_نداء(
                        _إرسال_، _المسار_، _ترويسة_صنف_بيانات_نصي_، بيانات_جديده، 10000، مغلفة(جـيسون) {}
                    )؛
                    // `نقوم بجلب البيانات من الخادم و استدعاء المغلف `عند_استلام_بيانات
                    أرسل_نداء(_جلب_، _المسار_، 0، 0، 500، عند_استلام_بيانات)؛
                }؛
            }
        })؛
    })؛

    // نقوم بتعريف مؤقت يقوم بتحديث البيانات بدون الحاجة إلى إعادة تحميل الصفحة بشكل دوري
    ابدأ_المؤقت_المتكرر(500000، مغلفة (جـيسون) {
        أرسل_نداء(_جلب_، _المسار_، 0، 0، 500، عند_استلام_بيانات)؛
    })؛
    // نجلب البيانات بشكل مباشر في أول مرة
    // أما في المرات اللاحقة بالمغلف الذي حددناه للمؤقت أو إدخال بيانات
    // جديدة سيؤدي إلى إعادة تحديث المحتوى
    أرسل_نداء(_جلب_، _المسار_، 0، 0، 500، عند_استلام_بيانات)؛

     نفذ_حلقة_معالجة_الأحداث()؛    
}

// منفذ بياني لصفحة واجهة مستخدم تمثل صفحة حول
@منفذ_مرئي["/about"، "مثال منصة ويب - الدردشة"]
دالة عن_البرنامج {
    نـافذة.النموذج.الطراز.{
        الحشوة = مـسافة4.نقاط(0)؛
        الهامش = مـسافة4.نقاط(0)؛
    }؛
    نـافذة.النموذج.حدد_المشهد(صـندوق({}).{
        الطراز.{
            الاتجاه = اتـجاه._من_اليمين_؛
            الإظهار = إظـهار._مرن_؛
            النسق = نـسق._عمود_؛
        }؛
        أضف_فروع({
            الـترويسة()،
            كـتابة(نـص("مثال الدردشة")).{ الطراز.{
                لون_الخط = لـون(0، 0، 0)؛
                حجم_الخط = مـسافة.نقاط(30.0)؛
            } }،
            كـتابة(نـص("طُور باستخدام منصة ويب الأسس")).{ الطراز.{
                لون_الخط = لـون(0، 0، 0)؛
                حجم_الخط = مـسافة.نقاط(20.0)؛
            } }
        })
    })؛

    نفذ_حلقة_معالجة_الأحداث()؛
}


//=============================================================================
// إدارة المشروع

دالة ابدأ_خادم_الدردشة {
    طـرفية.اطبع("تشغيل الخادم على المنفذ 8010...\nURL: http://localhost:8010/\n")؛
    ابن_وشغل_الخادم (مـصفوفة[مـؤشر_محارف]({ "listening_ports"، "8010"، "static_file_max_age"، "0" }))؛
}
ابدأ_خادم_الدردشة()؛
```

</div>

```
import "Build";
import "Apm";
Apm.importFile("Alusus/WebPlatform");
use Srl;
use WebPlatform;

//==============================================================================
// الخادم

def MAX_MESSAGES: 12;  // عدد الرسائل الأعظمي
def messages: Array[String];  // مصفوفة لتخزين الرسائل

// و له المسار المعطى POST منفذ بياني يقبل الطريقة
// يستعمل هذا المنفذ لإرسال الرسائل
@beEndpoint["POST", "/messages"]
func postMessages (conn: ptr[Http.Connection]) {
    def postData: array[Char, 1024];
    // نقوم بجلب المعلومات التي تم إرسالها و تخزينها في الصوان
    def postDataSize: Int = Http.read(conn, postData~ptr, postData~size);
    // في حال تجاوز الحد الأعظم للرسائل نحذف أقدم رسالة
    if messages.getLength() >= MAX_MESSAGES messages.remove(0);
    // نضيف الرسالة الجديدة
    messages.add(String(postData~ptr, postDataSize));
    Http.print(conn, "HTTP/1.1 200 Ok\r\n\r\n");
}

// و له المسار المعطى GET منفذ بياني يقبل الطريقة
// يستعمل هذا المنفذ لجلب الرسائل
@beEndpoint["GET", "/messages"]
func getMessages (conn: ptr[Http.Connection]) {
    // ادمج الرسائل سويةً مع سطر فارغ بين كل رسالة و الأخرى
    def response: String = String.merge(messages, "<br>");
    // نضع الترويسات اللازمة
    Http.print(conn, "HTTP/1.1 200 Ok\r\n");
    Http.print(conn, "Content-Type: text/plain\r\n");
    Http.print(conn, "Cache-Control: no-cache\r\n");
    Http.print(conn, "Content-Length: %d\r\n\r\n", response.getLength());
    // نضع محتوى الصوان الذي يحمل الرسائل
    Http.print(conn, response.buf);
}

// مسار الموارد على الخادم
@assetsRoute def assetsRoute: "Assets/";

//==============================================================================
// مـركبات واجهة المستخدم

def PRIMARY_COLOR: Color("8e50ef");
def LIGHT_COLOR: Color("c380ff");

// نقوم بتعريف مركب يمثل زر القائمة
class MenuButton {
    @injection def component: Component;

    handler this~init(link: String, label: String, icon: String) {
        // مساحة مستطيلة لتحوي الودجات الفرعية
        this.view = Box({}).{
            // نطبق بعض الأطرزة
            style.{
                padding = Length4.pt(3);
                background = Background(LIGHT_COLOR);
                display = Display.FLEX;
                layout = Layout.ROW;
                align = Align.CENTER;
            };
            // نطبق طراز على ودجة فرعية من الفئة أيقونة
            style(">>icon").{
                padding = Length4.pt(0, 4);
                width = Length.pt(20);
                height = Length.pt(0);
                heightTransition = Transition(0.2);
            }
            // نطبق طراز على ودجة فرعية من الفئة وسم
            style(">>label").{
                fontSize = Length.pt(16.0);
                fontColor = Color("eee");
            }
            // نقوم بتطبيق طراز على ودجة فرعية من الفئة أيقونة 
            // هذا الطراز يطبق عندما نحوم بالمؤشر على الأيقونة
            style({ StateSelector.HOVER, ">>icon" }).{
                height = Length.pt(20)
            }
            // نقوم بتطبيق طراز على ودجة فرعية من الفئة وسم 
            // هذا الطراز يطبق عندما نحوم بالمؤشر على الوسم
            style({ StateSelector.HOVER, ">>label" }).{
                fontSize = Length.pt(16.0);
                fontColor = Color("fff");
            }
            // الودجات الفرعية
            addChildren({
                // ودجة رابط
                // و الذي يحوي على ودجة فرعية هي نص
                Hyperlink(link, Text(label).{
                    // فئة هذا الرابط هو وسم
                    className = String("label")
                }).{ style.textDecoration = TextDecoration.NONE },
                // ودجة رابط
                // و الذي يحوي على ودجة فرعية هي صورة
                Hyperlink(link, Image().{
                    url = icon;
                    // فئة هذا الرابط هو أيقونة
                    className = String("icon");
                })
            });
        };
    }

    handler this_type(link: String, label: String, icon: String): SrdRef[MenuButton] {
        return SrdRef[MenuButton]().{ alloc()~init(link, label, icon) };
    }
}

// نقوم بتعريف مركب يمثل ترويسة
class Header {
    @injection def component: Component;

    handler this~init() {
        // نقوم بتعريف مساحة مستطيلة لتحوي الودجات الفرعية لهذا المركب
        this.view = Box({}).{
            // نقوم بتطبيق بعض الأطرزة
            style.{
                width = Length.percent(100) - Length.pt(3);
                height = Length.pt(85);
                padding = Length4.pt(4, 0, 0, 0);
                background = Background(PRIMARY_COLOR);
                borderWidth = Length4.pt(1.5);
                borderStyle = BorderStyle.SOLID;
                borderColor = PRIMARY_COLOR;
                justify = Justify.SPACE_BETWEEN;
                display = Display.FLEX;
                layout = Layout.COLUMN;
            };
            // الودجات الفرعية
            addChildren({
                // مساحة مستطيلة تعرف المنطقة الأولى من المركب
                Box({}).{
                    // نطبق بعض الأطرزة على هذه المساحة
                    style.{
                        display = Display.FLEX;
                        layout = Layout.ROW;
                        justify = Justify.SPACE_BETWEEN;
                        align = Align.CENTER;
                    };
                    // الودجات الفرعية
                    addChildren({
                        // مساحة تحوي نص
                        Box({ Text(String("Alusus Web Platform Examples - Chat")).{ style.{
                            fontColor = Color("fff");
                            fontSize = Length.pt(18.0);
                            height = Length.percent(100);
                            padding = Length4.pt(0, 10);
                        } } }),
                        // صورة
                        Image().{
                            url = String("/Assets/wblogo.svg");
                            style.{
                                height = Length.pt(50);
                                padding = Length4.pt(0, 10);
                            };
                        }
                    });
                },
                // مساحة مستطيلة تعرف المنطقة الثانية من المركب
                Box({}).{
                    // نطبق بعض الأطرزة على هذه المساحة
                    style.{
                        display = Display.FLEX;
                        layout = Layout.ROW;
                        justify = Justify.SPACE_BETWEEN;
                    };
                    // الودجات فرعية
                    addChildren({
                        // نقوم بتعريف زريّ قائمة
                        // أول زر يأخذنا إلى المسار الرئيسي و له النص المعطى و له الأيقونة المعطاة
                        MenuButton(String("/"), String("Main"), String("/Assets/chat.svg")),
                        // ثاني زر يأخذنا إلى مسار صفحة حول و له النص المعطى و له الأيقونة المعطاة
                        MenuButton(String("/about"), String("About"), String("/Assets/about.svg"))
                    });
                }
            });
        };
    }

    handler this_type(): SrdRef[Header] {
        return SrdRef[Header].construct();
    }
}

// مركب يمثل خانة إدخال نصية
class TextEntry {
    @injection def component: Component;

    // مغلف للتعامل مع الدخل الجديد
    def onNewEntry: closure (String);
    // ودجة إدخال نصية
    def textInput: SrdRef[TextInput];

    handler this.width = SrdRef[Length] {
        this.view.style.width = value;
        return value;
    }

    handler this.height = SrdRef[Length] {
        this.view.style.height = value;
        return value;
    }

    handler this~init() {
        def self: ref[this_type](this);

        this.view = Box({}).{
            style.{
                display = Display.FLEX;
                layout = Layout.ROW;
                justify = Justify.SPACE_BETWEEN;
                borderWidth = Length4.pt(1.5);
                borderStyle = BorderStyle.SOLID;
                borderColor = PRIMARY_COLOR;
                background = Background(PRIMARY_COLOR);
            };
            // الودجات الفرعية
            addChildren({
                // ودجة إدخال نصية
                TextInput().{
                    self.textInput = this;
                    style.{
                        width = Length.percent(100);
                        height = Length.percent(100);
                        background = Background(Color("fff"));
                        fontSize = Length.pt(12.0);
                    };
                    // نقوم بربط إشارة توقف الضغط على الزر بمغلف يتم عند تنفيذه التحقق
                    // فيما إذا كان الزر الذي تم ضغطه يوافق ما نريده للإرسال
                    // onNewEntry عندها يتم تحديث نص المغلفة 
                    // كما يتم إعادة المدخل النصي إلى الحالة الفارغة لاستقبال دخل جديد
                    onKeyUp.connect(closure (widget: ref[TextInput], payload: ref[String]) {
                        if payload == "Shift+Enter" {
                            def newData: String = String("- ") + encodeHtml(widget.getText()).trim();
                            widget.setText(String());
                            if not self.onNewEntry.isNull() self.onNewEntry(newData);
                        }
                    });
                },
                Button(String("Send")).{
                    style.{
                        height = Length.percent(100);
                        width = Length.pt(50);
                        background = Background(Color(200, 200, 200));
                        fontSize = Length.pt(16.0);
                        justify = Justify.CENTER;
                    };
                    // نقوم بربط إشارة توقف الضغط على المؤشر بمغلف يتم عند تنفيذه
                    // onNewEntry تحديث نص المغلفة 
                    // كما يتم إعادة المدخل النصي إلى الحالة الفارغة لاستقبال دخل جديد
                    onClick.connect(closure (widget: ref[Widget], payload: ref[Int]) {
                        def newData: String = String("- ") + encodeHtml(self.textInput.getText()).trim();
                        self.textInput.setText(String());
                        if not self.onNewEntry.isNull() self.onNewEntry(newData);
                    });
                }
            });
        };
    }

    handler this_type(): SrdRef[TextEntry] {
        return SrdRef[TextEntry].construct();
    }
}

// دالة لتحويل نص عادي إلى ترميز لتنف
func encodeHtml(str: String): String {
    return str
        .replace("\n", "<br>")
        .replace(",", "&comma;")
        .replace("\"", "&quot;");
}

//==============================================================================
// صفحات واجهة المستخدم
// منفذ بياني لصفحة واجهة مستخدم تمثل الصفحة الرئيسية
@uiEndpoint["/", "WebPlatform Example - Chat"]
func main {
    def onFetch: closure (json: Json);

    Window.instance.style.{
        padding = Length4.pt(0);
        margin = Length4.pt(0);
    };
    // نحدد مشهد النافذة
    Window.instance.setView(Box({}).{
        style.{
            height = Length.percent(100);
            justify = Justify.SPACE_BETWEEN;
            display = Display.FLEX;
            layout = Layout.COLUMN;
        };
        // الودجات الفرعية
        addChildren({
            Header(),  // نضيف مركب الترويسة
            Box({}).{
                style.{
                    width = Length.percent(100) - Length.pt(10);
                    padding = Length4.pt(5);
                    display = Display.FLEX;
                    layout = Layout.COLUMN;
                    flex = Flex(1);
                };
                def notificationLabel: SrdRef[Text];
                addChildren({
                    Text(String()).{
                        notificationLabel = this;
                        style.{
                            width = Length.percent(100);
                            height = Length.pt(20);
                            fontColor = Color(200, 50, 50);
                            fontSize = Length.pt(10.0);
                        };
                    },
                    Text(String()).{
                        style.{
                            width = Length.percent(100);
                            height = Length.percent(100);
                            fontColor = Color(50, 50, 50);
                            fontSize = Length.pt(20.0);
                        };
                        // نقوم بتغريف مغلف سيتم تتنفيذه عند جلب البيانات من الخادم
                        onFetch = closure (json: Json) {
                            // في البداية نتحقق من أنه قد تم جلب البيانات بدون حدوث أخطاء
                            def status: Int = json("eventData")("status");
                            if status >= 200 and status < 300 {
                                // نستخرج البيانات
                                def data: String = json("eventData")("body");
                                // نقوم بتحديث النص
                                if this.getText() != data {
                                    this.setText(data);
                                }
                                // نقوم بإزالة الإشعار بعد تحديث النص
                                if notificationLabel.getText() != "" {
                                    notificationLabel.setText(String(""));
                                }
                            } else { // حالة حدوث خطأ
                                // نقوم بوضع إشعار حدوث خطأ
                                notificationLabel.setText(String("Connection error. HTTP status: ") + status);
                            }
                        };
                    }
                });
            },
            TextEntry().{
                width = Length.percent(100) - Length.pt(3);
                height = Length.pt(50);
                // نقوم بتعريف مغلف يتم تنفيذه عند كتابة نص جديد
                onNewEntry = closure (newData: String) {
                    // نقوم بإرسال النص إلى الخادم
                    // نحدد الطريقة، المسار، نوع البيانات، و من ثم نضع البيانات
                    sendRequest(
                        "POST", "/messages", "Content-Type: application/text", newData, 10000,
                        closure (Json) {}
                    );
                    // نقوم بجلب البيانات من الخادم و استدعاء المغلف
                    // onFetch
                    sendRequest("GET", "/messages", null, null, 500, onFetch);
                };
            }
        })
    });

    // نقوم بتعريف مؤقت يقوم بتحديث البيانات بدون الحاجة إلى إعادة تحميل الصفحة بشكل دوري
    startTimer(500000, closure (json: Json) {
        sendRequest("GET", "/messages", null, null, 500, onFetch);
    });
    // نجلب البيانات بشكل مباشر في أول مرة
    // أما في المرات اللاحقة بالمغلف الذي حددناه للمؤقت أو إدخال بيانات
    // جديدة سيؤدي إلى إعادة تحديث المحتوى
    sendRequest("GET", "/messages", null, null, 500, onFetch);

    runEventLoop();
}

// منفذ بياني لصفحة واجهة مستخدم تمثل صفحة حول
@uiEndpoint["/about", "WebPlatform Example - Chat"]
func about {
    Window.instance.style.{
        padding = Length4.pt(0);
        margin = Length4.pt(0);
    };
    Window.instance.setView(Box({}).{
        style.{
            display = Display.FLEX;
            layout = Layout.COLUMN;
        };
        addChildren({
            Header(),
            Text(String("Chat Example")).{ style.{
                fontColor = Color(0, 0, 0);
                fontSize = Length.pt(30.0);
            } },
            Text(String("Built using Alusus Web Platform")).{ style.{
                fontColor = Color(0, 0, 0);
                fontSize = Length.pt(20.0);
            } }
        })
    });

    runEventLoop();
}


//==============================================================================
// Program Entry

func startChatServer {
    Console.print("Starting server on port 8010...\nURL: http://localhost:8010/\n");
    buildAndRunServer[serverModules](Array[CharsPtr]({ "listening_ports", "8010", "static_file_max_age", "0" }));
}

startChatServer();
```

