/*
 * Copyright (C) 2026 Sarmad Abdullah
 *
 * This file is part of Alusus WebPlatform library.
 *
 * This library is free software; you can redistribute it and/or
 * modify it under the terms of the GNU Lesser General Public
 * License as published by the Free Software Foundation; either
 * version 3 of the License, or (at your option) any later version.
 *
 * This library is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
 * Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public
 * License along with this library; if not, see <https://www.gnu.org/licenses/>.
 */

@merge module WebPlatform {
    //==========================================================================
    // Signal
    // A signal that can be subscribed to for handling events. It can have
    // multiple subscriptions.
    class Signal [ownerType: type, payloadType: type] {
        def slots: Array[closure (ref[ownerType], payload: ref[payloadType])];
        def onConnectionsChanged: closure (connectionCount: Int);

        handler this.emit(owner: ref[ownerType], payload: ref[payloadType]) {
            def i: Int;
            for i = 0, i < this.slots.getLength(), ++i {
                this.slots(i)(owner, payload);
            }
        }

        handler this.connect(slot: closure (ref[ownerType], payload: ref[payloadType])): ArchInt {
            def i: Int;
            for i = 0, i < this.slots.getLength(), ++i {
                if this.slots(i) == slot return slot.data.obj~ptr~cast[ArchInt];
            }
            this.slots.add(slot);
            if not this.onConnectionsChanged.isNull() this.onConnectionsChanged(this.slots.getLength());
            return slot.data.obj~ptr~cast[ArchInt];
        }

        handler this.disconnect(slot: closure (ref[ownerType], payload: ref[payloadType])) {
            def i: Int;
            for i = 0, i < this.slots.getLength(), ++i {
                if this.slots(i) == slot {
                    this.slots.remove(i);
                    if not this.onConnectionsChanged.isNull() this.onConnectionsChanged(this.slots.getLength());
                    return;
                }
            }
        }

        handler this.disconnect(id: ArchInt) {
            def i: Int;
            for i = 0, i < this.slots.getLength(), ++i {
                if this.slots(i).data.obj~ptr~cast[ArchInt] == id {
                    this.slots.remove(i);
                    if not this.onConnectionsChanged.isNull() this.onConnectionsChanged(this.slots.getLength());
                    return;
                }
            }
        }

        handler this.getConnectionCount (): Int {
            return this.slots.getLength();
        }
    }
}
