/*
 * Copyright (C) 2026 Sarmad Abdullah
 *
 * This file is part of Alusus WebPlatform library.
 *
 * This library is free software; you can redistribute it and/or
 * modify it under the terms of the GNU Lesser General Public
 * License as published by the Free Software Foundation; either
 * version 3 of the License, or (at your option) any later version.
 *
 * This library is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
 * Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public
 * License along with this library; if not, see <https://www.gnu.org/licenses/>.
 */

@merge module WebPlatform {
    //==========================================================================
    // AudioResource
    class AudioResource {
        def id: ArchInt = 0;
        def url: String;

        handler this~terminate() {
            if this.id != 0 releaseResource(this.id);
        }

        handler this.load(u: ptr[array[Char]]): SrdRef[Promise[Int]] {
            def p: SrdRef[Promise[Int]] = Promise[Int].new();
            this.url = u;
            loadAudio(u, closure (json: Json) {
                def data: Json = json("eventData");
                if this.id != 0 {
                    releaseResource(this.id);
                    this.id = 0;
                }
                if data("success") {
                    this.id = data("resourceId");
                    p.resolve(0);
                } else p.reject(GenericError.new(Errors.AUDIO_LOAD, "Loading failed"));
            });
            return p;
        }

        handler this.play(loop: Bool) {
            this.play(loop, false);
        }

        handler this.play(loop: Bool, stopPrevious: Bool) {
            playAudio(this.id, loop, stopPrevious);
        }

        handler this.stop() {
            stopAudio(this.id);
        }

        handler this.pause() {
            pauseAudio(this.id);
        }

        handler this.resume() {
            resumeAudio(this.id);
        }

        handler this.volume = Float {
            setAudioVolume(this.id, value);
            return value;
        }
        handler this.volume:Float {
            return getAudioVolume(this.id);
        }

        handler this.isPlaying(): Bool {
            return isAudioPlaying(this.id);
        }
    }
}

