/*
 * Copyright (C) 2026 Sarmad Abdullah
 *
 * This file is part of Alusus WebPlatform library.
 *
 * This library is free software; you can redistribute it and/or
 * modify it under the terms of the GNU Lesser General Public
 * License as published by the Free Software Foundation; either
 * version 3 of the License, or (at your option) any later version.
 *
 * This library is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
 * Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public
 * License along with this library; if not, see <https://www.gnu.org/licenses/>.
 */

@merge module WebPlatform {
    //==========================================================================
    // Component
    class Component {
        @injection def widget: Widget;
        def _view: SrdRef[Widget];
        def ownerId: String;
        def nextSiblingId: String;

        //---------
        // Handlers

        handler this~init() {}

        handler this.view = SrdRef[Widget] {
            if this.id != "" this.unbuild();
            this._view = value;
            if this.ownerId != "" this.build(this.ownerId, this.nextSiblingId);
            return this._view;
        }

        handler this.view:ref[SrdRef[Widget]] {
            return this._view;
        }

        //--------------------
        // Abstract Properties

        handler (this: Widget).id: String set_ptr {
            if this._view.isNull() return String()
            else return this._view.id;
        }

        handler (this: Widget).style: SrdRef[StyleSet] set_ptr {
            return this._view.style;
        }
        handler (this: Widget).style = SrdRef[StyleSet] set_ptr {
            return this._view.style;
        }

        handler (this:Widget).setClassName (val: temp_ref[String]) set_ptr {
            this._view.setClassName(val);
        }
        handler (this:Widget).getClassName (): ref[String] set_ptr {
            return this._view.getClassName();
        }
        handler (this:Widget).className = (val: temp_ref[String]) set_ptr {
            this._view.className = val;
            return this._view.className;
        }
        handler (this:Widget).className: ref[String] set_ptr {
            return this._view.className;
        }

        //--------------------
        // Abstract Events

        handler (this:Widget).onResize: ref[DomEventSignal[Widget, Int]] set_ptr {
            return this._view.onResize;
        }

        handler (this:Widget).onMouseEnter: ref[DomEventSignal[Widget, Int]] set_ptr {
            return this._view.onMouseEnter;
        }

        handler (this:Widget).onMouseOut: ref[DomEventSignal[Widget, Int]] set_ptr {
            return this._view.onMouseOut;
        }

        handler (this:Widget).onMouseDown: ref[DomEventSignal[Widget, MouseButtonPayload]] set_ptr {
            return this._view.onMouseDown;
        }

        handler (this:Widget).onMouseUp: ref[DomEventSignal[Widget, MouseButtonPayload]] set_ptr {
            return this._view.onMouseUp;
        }

        handler (this:Widget).onMouseMove: ref[DomEventSignal[Widget, MouseMovePayload]] set_ptr {
            return this._view.onMouseMove;
        }

        handler (this:Widget).onTouchStart: ref[DomEventSignal[Widget, Array[TouchPayload]]] set_ptr {
            return this._view.onTouchStart;
        }

        handler (this:Widget).onTouchEnd: ref[DomEventSignal[Widget, Array[TouchPayload]]] set_ptr {
            return this._view.onTouchEnd;
        }

        handler (this:Widget).onTouchMove: ref[DomEventSignal[Widget, Array[TouchPayload]]] set_ptr {
            return this._view.onTouchMove;
        }

        handler (this:Widget).onClick: ref[DomEventSignal[Widget, Int]] set_ptr {
            return this._view.onClick;
        }

        //-------------------
        // Abstract Functions

        handler this.beforeBuild() as_ptr {};
        handler this.afterBuild() as_ptr {};
        handler this.beforeUnbuild() as_ptr {};
        handler this.afterUnbuild() as_ptr {};

        //-------------------
        // Function Overrides

        handler (this:Widget).build (ownerId: String, nextSiblingId: String) set_ptr {
            this.beforeBuild();
            if this._view.obj~ptr != 0 this._view.build(ownerId, nextSiblingId);
            this.ownerId = ownerId;
            this.nextSiblingId = nextSiblingId;
            this.afterBuild();
        }

        handler (this:Widget).unbuild() set_ptr {
            this.beforeUnbuild();
            if this._view.obj~ptr != 0 this._view.unbuild();
            this.afterUnbuild();
        }
    }
}

