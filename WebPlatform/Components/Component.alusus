@merge module WebPlatform {
    //==========================================================================
    // Component
    class Component {
        @injection def widget: Widget;
        def _view: SrdRef[Widget];
        def ownerId: String;
        def nextSiblingId: String;

        //---------
        // Handlers

        handler this~init() {}

        handler this.view = SrdRef[Widget] {
            if this.id != "" this.unbuild();
            this._view = value;
            if this.ownerId != "" this.build(this.ownerId, this.nextSiblingId);
            return this._view;
        }

        handler this.view:ref[SrdRef[Widget]] {
            return this._view;
        }

        //-------------------
        // Abstract Functions

        handler this.beforeBuild() as_ptr {};
        handler this.afterBuild() as_ptr {};
        handler this.beforeUnbuild() as_ptr {};
        handler this.afterUnbuild() as_ptr {};

        //-------------------
        // Function Overrides

        handler (this:Widget).build (ownerId: String, nextSiblingId: String) set_ptr {
            this.beforeBuild();
            if this._view.obj~ptr != 0 this._view.build(ownerId, nextSiblingId);
            this.id = this._view.id;
            this.ownerId = ownerId;
            this.nextSiblingId = nextSiblingId;
            this.afterBuild();
        }

        handler (this:Widget).unbuild() set_ptr {
            this.beforeUnbuild();
            this.id = String();
            if this._view.obj~ptr != 0 this._view.unbuild();
            this.afterUnbuild();
        }
    }
}

