/*
 * Copyright (C) 2026 Sarmad Abdullah
 *
 * This file is part of Alusus WebPlatform library.
 *
 * This library is free software; you can redistribute it and/or
 * modify it under the terms of the GNU Lesser General Public
 * License as published by the Free Software Foundation; either
 * version 3 of the License, or (at your option) any later version.
 *
 * This library is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
 * Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public
 * License along with this library; if not, see <https://www.gnu.org/licenses/>.
 */

@merge module WebPlatform {
    function dispatchEvents {
        while 1 {
            def jsonStr: ptr[array[Char]] = fetchNextEvent();
            if jsonStr == 0 break;
            def json: Json(jsonStr);
            def index: ArchInt = findEventHandlerIndex(json("cbId"));
            if index != -1 {
                if json("eventName")~cast[String] == "_abort_" {
                    eventHandlers.remove(index);
                } else if json("recurring") {
                    eventHandlers(index).cb(json);
                } else {
                    def cb: closure (Json) = eventHandlers(index).cb;
                    eventHandlers.remove(index);
                    cb(json);
                }
            }
        }
    }
    
    function runEventLoop {
        while 1 {
            waitForEvent();
            dispatchEvents();
        }
    }

    function await [Rtype: type = Int] (p: SrdRef[Promise[Rtype]]): Rtype {
        if p.status != Status.NEW return p.result;
        while 1 {
            waitForEvent();
            dispatchEvents();
            if p.status != Status.NEW break;
        }
        return p.result;
    }
}

