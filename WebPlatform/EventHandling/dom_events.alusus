/*
 * Copyright (C) 2026 Sarmad Abdullah
 *
 * This file is part of Alusus WebPlatform library.
 *
 * This library is free software; you can redistribute it and/or
 * modify it under the terms of the GNU Lesser General Public
 * License as published by the Free Software Foundation; either
 * version 3 of the License, or (at your option) any later version.
 *
 * This library is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
 * Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public
 * License along with this library; if not, see <https://www.gnu.org/licenses/>.
 */

@merge module WebPlatform {
    class ElementEventIdentification {
        def elementName: String;
        def eventName: String;
    }

    function registerElementEventHandler (
        elementName: ptr[array[Char]], eventName: ptr[array[Char]], preventDefault: Bool, cb: closure (Json)
    ): ArchInt {
        def index: ArchInt = addEventHandler(cb);
        _registerElementEventHandler(elementName, eventName, preventDefault, eventHandlers(index).id);
        def eventIdentification: SrdRef[ElementEventIdentification];
        eventIdentification.construct();
        eventIdentification.elementName = elementName;
        eventIdentification.eventName = eventName;
        eventHandlers(index).info = castSrdRef[eventIdentification, Void];
        return eventHandlers(index).id;
    }

    function registerElementKeyEventHandler (
        elementName: ptr[array[Char]], eventName: ptr[array[Char]], keysToSwallow: ptr[array[Char]], cb: closure (Json)
    ): ArchInt {
        def index: ArchInt = addEventHandler(cb);
        _registerElementKeyEventHandler(elementName, eventName, keysToSwallow, eventHandlers(index).id);
        def eventIdentification: SrdRef[ElementEventIdentification];
        eventIdentification.construct();
        eventIdentification.elementName = elementName;
        eventIdentification.eventName = eventName;
        eventHandlers(index).info = castSrdRef[eventIdentification, Void];
        return eventHandlers(index).id;
    }

    function unregisterElementEventHandler (id: ArchInt) {
        def i: ArchInt = findEventHandlerIndex(id);
        if i == -1 return;
        def eventIdentification: SrdRef[ElementEventIdentification] =
            castSrdRef[eventHandlers(i).info, ElementEventIdentification];
        _unregisterElementEventHandler(eventIdentification.elementName, eventIdentification.eventName);
        eventHandlers.remove(i);
    }

    function observeResize (elementName: ptr[array[Char]], cb: closure (Json)): ArchInt {
        def index: ArchInt = addEventHandler(cb);
        _observeResize(elementName, eventHandlers(index).id);
        def eventIdentification: SrdRef[ElementEventIdentification];
        eventIdentification.construct();
        eventIdentification.elementName = elementName;
        eventIdentification.eventName = "observeResize";
        eventHandlers(index).info = castSrdRef[eventIdentification, Void];
        return eventHandlers(index).id;
    }

    function unobserveResize (id: ArchInt) {
        def i: ArchInt = findEventHandlerIndex(id);
        if i == -1 return;
        def eventIdentification: SrdRef[ElementEventIdentification] =
            castSrdRef[eventHandlers(i).info, ElementEventIdentification];
        _unobserveResize(eventIdentification.elementName);
        eventHandlers.remove(i);
    }
}
