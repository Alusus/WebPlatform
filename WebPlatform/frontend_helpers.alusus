/*
 * Copyright (C) 2026 Sarmad Abdullah
 *
 * This file is part of Alusus WebPlatform library.
 *
 * This library is free software; you can redistribute it and/or
 * modify it under the terms of the GNU Lesser General Public
 * License as published by the Free Software Foundation; either
 * version 3 of the License, or (at your option) any later version.
 *
 * This library is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
 * Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public
 * License along with this library; if not, see <https://www.gnu.org/licenses/>.
 */

@merge module WebPlatform {
    //==========================================================================
    // Functions and Macros

    function getNewId (): String {
        @shared def counter: Int(0);
        return String("id") + (++counter);
    }

    macro defineProperty [propName, upperCasePropName, propType] {
        def v__propName__: propType;
        handler this.set__upperCasePropName__ (val: temp_ref[propType]) {
            this.v__propName__ = val;
            this.onPropUpdated();
        }
        handler this.get__upperCasePropName__ (): ref[propType] {
            return this.v__propName__;
        }
        handler this.propName = (val: temp_ref[propType]) {
            this.v__propName__ = val;
            this.onPropUpdated();
            return this.v__propName__;
        }
        handler this.propName: ref[propType] {
            return this.v__propName__;
        }
    }

    macro defineAbstractProperty [propName, upperCasePropName, propType] {
        handler this.set__upperCasePropName__ (val: temp_ref[propType]) as_ptr;
        handler this.get__upperCasePropName__ (): ref[propType] as_ptr;
        handler this.propName = (val: temp_ref[propType]) as_ptr;
        handler this.propName: ref[propType] as_ptr;
    }

    macro implementAbstractProperty [parentClass, propName, upperCasePropName, propType] {
        def v__propName__: propType;
        handler (this:parentClass).set__upperCasePropName__ (val: temp_ref[propType]) set_ptr {
            this.v__propName__ = val;
            this.onPropUpdated();
        }
        handler (this:parentClass).get__upperCasePropName__ (): ref[propType] set_ptr {
            return this.v__propName__;
        }
        handler (this:parentClass).propName = (val: temp_ref[propType]) set_ptr {
            this.v__propName__ = val;
            this.onPropUpdated();
            return this.v__propName__;
        }
        handler (this:parentClass).propName: ref[propType] set_ptr {
            return this.v__propName__;
        }
    }

    //==========================================================================
    // MouseMovePayload
    // The payload of the mouse-move event.
    class MouseMovePayload {
        def posX: Float = 0;
        def posY: Float = 0;
        def deltaX: Float = 0;
        def deltaY: Float = 0;

        handler this~init() {}
        handler this~init(x: Float, y: Float, dx: Float, dy: Float) {
            this.posX = x;
            this.posY = y;
            this.deltaX = dx;
            this.deltaY = dy;
        }
        handler this~init(temp_ref[MouseMovePayload]) {
            this = value;
        }
        handler this = temp_ref[MouseMovePayload] {
            this.posX = value.posX;
            this.posY = value.posY;
            this.deltaX = value.deltaX;
            this.deltaY = value.deltaY;
        }
    }

    //==========================================================================
    // MouseButtonPayload
    // The payload of the mouse-down and mouse-up events.
    class MouseButtonPayload {
        def button: Int = 0;
        def posX: Float = 0;
        def posY: Float = 0;

        handler this~init() {}
        handler this~init(b: Int, x: Float, y: Float) {
            this.button = b;
            this.posX = x;
            this.posY = y;
        }
        handler this~init(temp_ref[MouseButtonPayload]) {
            this = value;
        }
        handler this = temp_ref[MouseButtonPayload] {
            this.button = value.button;
            this.posX = value.posX;
            this.posY = value.posY;
        }
    }

    //==========================================================================
    // TouchPayload
    // The payload of a single touch in a touch event.
    class TouchPayload {
        def identifier: String;
        def screenX: Float;
        def screenY: Float;
        def clientX: Float;
        def clientY: Float;
        def pageX: Float;
        def pageY: Float;
        def radiusX: Float;
        def radiusY: Float;
        def rotationAngle: Float;
        def force: Float;
        def changed: Bool;

        handler this~init() {}
        handler this~init(temp_ref[TouchPayload]) {
            this = value;
        }
        handler this = temp_ref[TouchPayload] {
            this.identifier = value.identifier;
            this.screenX = value.screenX;
            this.screenY = value.screenY;
            this.clientX = value.clientX;
            this.clientY = value.clientY;
            this.pageX = value.pageX;
            this.pageY = value.pageY;
            this.radiusX = value.radiusX;
            this.radiusY = value.radiusY;
            this.rotationAngle = value.rotationAngle;
            this.force = value.force;
            this.changed = value.changed;
        }
    }
}
