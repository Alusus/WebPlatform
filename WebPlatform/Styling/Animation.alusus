/*
 * Copyright (C) 2026 Sarmad Abdullah
 *
 * This file is part of Alusus WebPlatform library.
 *
 * This library is free software; you can redistribute it and/or
 * modify it under the terms of the GNU Lesser General Public
 * License as published by the Free Software Foundation; either
 * version 3 of the License, or (at your option) any later version.
 *
 * This library is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
 * Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public
 * License along with this library; if not, see <https://www.gnu.org/licenses/>.
 */

@merge module WebPlatform {
    //==========================================================================
    // Animation
    // class to deal with keyframes and animation
    class Animation {
        def name : String = getNewId();
        def buildCounter: Int = 0;
        def styles: Map[Float, Style];
        def totalDuration: Float;
        def onUpdated: closure() = closure() {
            this.update();
        }

        handler this~init() {}
        handler this~init(styles: Array[Style]) this.set(styles);
        handler this~init(styles: Map[Float, Style]) this.set(styles);

        handler this.set(totalDuration: Float, styles: Array[Style]) {
            this.totalDuration = totalDuration;
            this.styles.values = styles;

            def i: Int = 0;
            def sectionSize: Float = 100f / (styles.getLength() - 1);
            for i = 0, i < styles.getLength(), i += 1 {
                this.styles.keys.add(i * sectionSize);
                this.styles.values(i).onUpdated = this.onUpdated;
            }
        }
        handler this.set(totalDuration: Float, styles: Map[Float, Style]) {
            this.totalDuration = totalDuration;
            this.styles = styles;

            def i: Int = 0;
            for i = 0, i < this.styles.getLength(), i += 1 {
                this.styles(i).onUpdated = this.onUpdated;
            }
        }

        handler this.build() {
            if this.buildCounter++ > 0 return;

            createElement("style", this.name, "head");
            this.update();
        }

        handler this.unbuild() {
            if this.buildCounter == 0 return;
            if --this.buildCounter > 0 return;

            deleteElement(this.name);
        }

        handler this.update() {
            if this.buildCounter == 0 return;

            def cssString: StringBuilder;
            cssString += "@keyframes ";
            cssString += this.name;
            cssString += " {";
            def i: Int;
            for i = 0, i < this.styles.getLength(), ++i {
                cssString += this.styles.keyAt(i);
                cssString += "% {";
                cssString += this.styles(i).getCssString();
                cssString += "}";
            }
            cssString += "}";
            setElementAttribute(this.name, "innerHTML", cssString);
        }

        handler this_type(): SrdRef[Animation] {
            return SrdRef[Animation].construct();
        }
        handler this_type(d: Float, s: Array[Style]): SrdRef[Animation] {
            return SrdRef[Animation].construct().{ set(d, s) };
        }
        handler this_type(d: Float, s: Map[Float, Style]): SrdRef[Animation] {
            return SrdRef[Animation].construct().{ set(d, s) };
        }
    }
}

