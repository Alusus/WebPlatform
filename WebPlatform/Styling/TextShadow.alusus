/*
 * Copyright (C) 2026 Sarmad Abdullah
 *
 * This file is part of Alusus WebPlatform library.
 *
 * This library is free software; you can redistribute it and/or
 * modify it under the terms of the GNU Lesser General Public
 * License as published by the Free Software Foundation; either
 * version 3 of the License, or (at your option) any later version.
 *
 * This library is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
 * Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public
 * License along with this library; if not, see <https://www.gnu.org/licenses/>.
 */

@merge module WebPlatform {
    class TextShadow {
        def offsetX: SrdRef[Length];
        def offsetY: SrdRef[Length];
        def blurRadius: SrdRef[Length];
        def color: Color;

        handler this~init() {}
        handler this~init(ox: SrdRef[Length], oy: SrdRef[Length], c: Color) {
            this.offsetX = ox;
            this.offsetY = oy;
            this.color = c;
        }
        handler this~init(ox: SrdRef[Length], oy: SrdRef[Length], br: SrdRef[Length], c: Color) {
            this.offsetX = ox;
            this.offsetY = oy;
            this.blurRadius = br;
            this.color = c;
        }

        handler this~init(temp_ref[TextShadow]) {
            this = value;
        }

        handler this = temp_ref[TextShadow] {
            this.offsetX = value.offsetX;
            this.offsetY = value.offsetY;
            this.blurRadius = value.blurRadius;
            this.color = value.color;
        }

        handler this.toString(): String {
            if this.blurRadius.isNull() {
                return String.format(
                    "%s %s %s",
                    this.offsetX.toString().buf, this.offsetY.toString().buf, this.color.toString().buf
                );
            } else {
                return String.format(
                    "%s %s %s %s",
                    this.offsetX.toString().buf, this.offsetY.toString().buf,
                    this.blurRadius.toString().buf, this.color.toString().buf
                );
            }
        }
    }
}
