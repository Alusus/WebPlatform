/*
 * Copyright (C) 2026 Sarmad Abdullah
 *
 * This file is part of Alusus WebPlatform library.
 *
 * This library is free software; you can redistribute it and/or
 * modify it under the terms of the GNU Lesser General Public
 * License as published by the Free Software Foundation; either
 * version 3 of the License, or (at your option) any later version.
 *
 * This library is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
 * Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public
 * License along with this library; if not, see <https://www.gnu.org/licenses/>.
 */

@merge module WebPlatform {
    //==========================================================================
    // Box
    // A rectangular area that contains other widgets.
    class Box {
        @injection def basicWidget: BasicWidget;
        def children: Array[SrdRef[Widget]];

        //---------
        // Handlers

        handler this~init() {
        }

        handler this.addChildren (count: Int, children: ...temp_ref[SrdRef[Widget]]) {
            while count-- > 0 {
                def child: ref[SrdRef[Widget]](children~next_arg[ref[SrdRef[Widget]]]);
                this.children.add(child);
                if this.id != "" child.build(this.id, String());
            }
        }

        handler this.insertChild (index: Int, child: SrdRef[Widget]) {
            if index < 0 index = 0
            else if index >= this.children.getLength() {
                this.addChildren({ child });
                return;
            }
            this.children.insert(index, child);
            if this.id != "" child.build(this.id, this.children(index + 1).id);
        }

        handler this.removeChildren (count: Int, children: ...temp_ref[SrdRef[Widget]]) {
            while count-- > 0 {
                def child: ref[SrdRef[Widget]](children~next_arg[ref[SrdRef[Widget]]]);
                def i: Int;
                for i = 0, i < this.children.getLength(), ++i {
                    if this.children(i).obj~ptr == child.obj~ptr {
                        child.unbuild();
                        this.children.remove(i);
                        return;
                    }
                }
            }
        }

        handler this.removeAllChildren() {
            def i: Int;
            for i = this.children.getLength() - 1, i >= 0, --i {
                this.children(i).unbuild();
                this.children.remove(i);
            }
        }

        //-------------------
        // Function Overrides

        handler (this:Widget).build (ownerId: String, nextSiblingId: String) set_ptr {
            this.id = getNewId();
            createElement("div", this.id, ownerId, nextSiblingId);
            this.buildCommon();
            this.update();
            def i: Int;
            for i = 0, i < this.children.getLength(), ++i {
                this.children(i).build(this.id, String());
            }
        }

        handler (this:Widget).unbuild() set_ptr {
            if this.id.getLength() > 0 {
                deleteElement(this.id);
                this.unbuildCommon();

                def i: Int;
                for i = 0, i < this.children.getLength(), ++i {
                    this.children(i).unbuild();
                }

                this.id = String();
            }
        }

        handler (this:Widget).update() set_ptr {
            this.updateCommon();
        }

        //-----------------
        // Shared Functions

        handler this_type(): SrdRef[Box] {
            return Box({});
        }

        handler this_type(count: Int, args: ...temp_ref[SrdRef[Widget]]): SrdRef[Box] {
            def r: SrdRef[Box];
            r.alloc()~init();
            while count-- > 0 r.addChildren({ args~next_arg[ref[SrdRef[Widget]]] });
            return r;
        }
    }
}

