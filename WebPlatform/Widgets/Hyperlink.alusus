/*
 * Copyright (C) 2026 Sarmad Abdullah
 *
 * This file is part of Alusus WebPlatform library.
 *
 * This library is free software; you can redistribute it and/or
 * modify it under the terms of the GNU Lesser General Public
 * License as published by the Free Software Foundation; either
 * version 3 of the License, or (at your option) any later version.
 *
 * This library is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
 * Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public
 * License along with this library; if not, see <https://www.gnu.org/licenses/>.
 */

@merge module WebPlatform {
    //==========================================================================
    // Hyperlink
    class Hyperlink {
        @injection def basicWidget: BasicWidget;
        def child: SrdRef[Widget];

        //-----------
        // Properties

        defineProperty[url, Url, String];
        defineProperty[newTab, NewTab, Bool];

        //---------
        // Handlers

        handler this~init() {
            this.newTab = false;
        }

        handler this~init(url: String) {
            this.newTab = false;
            this.url = url;
        }

        handler this.setChild (child: temp_ref[SrdRef[Widget]]) {
            if this.child.obj~ptr != 0 this.child.unbuild();
            this.child = child;
            if this.child.obj~ptr != 0 && this.id.getLength() > 0 this.child.build(this.id, String());
        }

        handler this.onPropUpdated() this.update();

        //-------------------
        // Function Overrides

        handler (this:Widget).build (ownerId: String, nextSiblingId: String) set_ptr {
            this.id = getNewId();
            createElement("a", this.id, ownerId, nextSiblingId);
            this.buildCommon();
            this.update();

            if this.child.obj~ptr != 0 this.child.build(this.id, String());
        }

        handler (this:Widget).unbuild() set_ptr {
            if this.id.getLength() > 0 {
                deleteElement(this.id);
                this.unbuildCommon();

                if this.child.obj~ptr != 0 this.child.unbuild();

                this.id = String();
            }
        }

        handler (this:Widget).update() set_ptr {
            if this.id.getLength() > 0 {
                this.updateCommon();
                setElementAttribute(this.id, "href", this.url);
                if this.newTab setElementAttribute(this.id, "target", "_blank")
                else setElementAttribute(this.id, "target", "");
            }
        }

        //-----------------
        // Shared Functions

        handler this_type(): SrdRef[Hyperlink] {
            return SrdRef[Hyperlink].construct();
        }

        handler this_type(url: String, child: temp_ref[SrdRef[Widget]]): SrdRef[Hyperlink] {
            def r: SrdRef[Hyperlink];
            r.alloc()~init(url);
            r.setChild(child);
            return r;
        }
    }
}

