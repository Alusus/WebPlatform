/*
 * Copyright (C) 2026 Sarmad Abdullah
 *
 * This file is part of Alusus WebPlatform library.
 *
 * This library is free software; you can redistribute it and/or
 * modify it under the terms of the GNU Lesser General Public
 * License as published by the Free Software Foundation; either
 * version 3 of the License, or (at your option) any later version.
 *
 * This library is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
 * Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public
 * License along with this library; if not, see <https://www.gnu.org/licenses/>.
 */

@merge module WebPlatform {
    //==========================================================================
    // Canvas
    // A free form drawing area.
    class Canvas {
        @injection def basicWidget: BasicWidget;
        @injection def drawing: Drawing;
        def resourceId: ArchInt;

        //-----------
        // Properties

        defineProperty[bitmapWidth, BitmapWidth, SrdRef[Length]];
        defineProperty[bitmapHeight, BitmapHeight, SrdRef[Length]];

        //---------
        // Handlers

        handler this~init() {
        }

        handler this.onPropUpdated() this.update();

        //-------------------
        // Function Overrides

        handler (this:Widget).build (ownerId: String, nextSiblingId: String) set_ptr {
            this.id = getNewId();
            createElement("canvas", this.id, ownerId, nextSiblingId);
            this.resourceId = registerElementAsResource(this.id);
            this.buildCommon();
            this.update();
        }

        handler (this:Widget).unbuild() set_ptr {
            if this.id.getLength() > 0 {
                deleteElement(this.id);
                releaseResource(this.resourceId);
                this.unbuildCommon();
                this.id = String();
            }
        }

        handler (this:Widget).update() set_ptr {
            if this.id.getLength() > 0 {
                this.updateCommon();
                setElementAttribute(this.id, "width", this.bitmapWidth.toString());
                setElementAttribute(this.id, "height", this.bitmapHeight.toString());
            }
        }

        handler (this: Drawing).getResourceId(): ArchInt set_ptr {
            unshiftThis[drawing];
            return this.resourceId;
        }

        //-----------------
        // Shared Functions

        handler this_type(): SrdRef[Canvas] {
            def r: SrdRef[Canvas];
            r.alloc()~init();
            return r;
        }
    }
}

