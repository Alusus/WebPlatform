/*
 * Copyright (C) 2026 Sarmad Abdullah
 *
 * This file is part of Alusus WebPlatform library.
 *
 * This library is free software; you can redistribute it and/or
 * modify it under the terms of the GNU Lesser General Public
 * License as published by the Free Software Foundation; either
 * version 3 of the License, or (at your option) any later version.
 *
 * This library is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
 * Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public
 * License along with this library; if not, see <https://www.gnu.org/licenses/>.
 */

@merge module WebPlatform {
    //==========================================================================
    // DocView
    class DocView {
        @injection def basicWidget: BasicWidget;

        //-----------
        // Properties
        defineProperty[html, Text, String];

        //----------
        // Handlers

        handler this~init() {
        }

        handler this~init(t: String) {
            this.html = t;
        }

        handler this.setHtml(html: String) {
            this.html = html;
        }

        handler this.setMarkdown(md: String) {
            this.setMarkdown(md, false);
        }

        handler this.setMarkdown(md: String, linksInNewTab: Bool) {
            def mdTranslator: MarkdownTranslator[RegExp];
            mdTranslator.linksInNewTab = linksInNewTab;
            this.setHtml(mdTranslator.translate(md));
        }

        handler this.onPropUpdated() this.update();

        //-------------------
        // Function Overrides

        handler (this:Widget).build (ownerId: String, nextSiblingId: String) set_ptr {
            this.id = getNewId();
            createElement("div", this.id, ownerId, nextSiblingId);
            this.buildCommon();
            this.update();
        }

        handler (this:Widget).unbuild() set_ptr {
            if this.id.getLength() > 0 {
                deleteElement(this.id);
                this.unbuildCommon();
                this.id = String();
            }
        }

        handler (this:Widget).update() set_ptr {
            if this.id.getLength() > 0 {
                this.updateCommon();
                setElementAttribute(this.id, "innerHTML", this.html);
            }
        }

        //-----------------
        // Shared Functions

        handler this_type(): SrdRef[DocView] {
            return SrdRef[DocView].construct();
        }

        handler this_type(t: String): SrdRef[DocView] {
            return SrdRef[DocView]().{ alloc()~init(t) };
        }
    }
}
