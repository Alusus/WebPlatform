/*
 * Copyright (C) 2026 Sarmad Abdullah
 *
 * This file is part of Alusus WebPlatform library.
 *
 * This library is free software; you can redistribute it and/or
 * modify it under the terms of the GNU Lesser General Public
 * License as published by the Free Software Foundation; either
 * version 3 of the License, or (at your option) any later version.
 *
 * This library is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
 * Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public
 * License along with this library; if not, see <https://www.gnu.org/licenses/>.
 */

@merge module WebPlatform {
    //==========================================================================
    // RadioButton
    class RadioButton {
        @injection def basicWidget: BasicWidget;

        //-----------------------
        // Properties and Signals

        defineProperty[value, Value, String];
        defineProperty[name, Name, String];
        defineProperty[disabled, Disabled, bool];

        def _checked: Bool;
        handler this.setChecked (c: Bool) {
            this._checked = c;
            if this.id.getLength() > 0 {
                if c setElementAttribute(this.id, "checked", "true")
                else setElementAttribute(this.id, "checked", "false");
            }
        }
        handler this.getChecked (): Bool {
            if this.id.getLength() == 0 return this._checked
            else return String.isEqual(getElementAttribute(this.id, "checked"), "true");
        }
        handler this.checked = (val: Bool) {
            this.setChecked(val);
            return this._checked;
        }
        handler this.checked: Bool {
            return this.getChecked();
        }

        def onChanged: DomEventSignal[RadioButton, Int];
        def onChangedId: ArchInt = -1;

        //---------
        // Handlers

        handler this~init() {
            this.initSignals();
            this.disabled = false;
            this._checked = false;
        }

        handler this~init(n: String, v: String) {
            this.initSignals();
            this.name = n;
            this.value = v;
            this.disabled = false;
            this._checked = false;
        }

        handler this~init(n: String, v: String, c: Bool) {
            this.initSignals();
            this.name = n;
            this.value = v;
            this.disabled = false;
            this._checked = c;
        }

        handler this.initSignals() {
            this.onChanged.onConnectionsChanged = closure (connectionCount: Int) {
                handleSignalConnectionChanged[this.id, onChanged, "change", {
                    this.onChanged.emit(this, Int(0));
                }];
            };
        }

        //-----------------
        // Member Functions

        handler this.onPropUpdated() this.update();

        //-------------------
        // Function Overrides

        handler (this:Widget).build (ownerId: String, nextSiblingId: String) set_ptr {
            this.id = getNewId();
            createElement("input", this.id, ownerId, nextSiblingId);
            this.buildCommon();
            this.update();
            this.onChanged.onConnectionsChanged(this.onChanged.getConnectionCount());
        }

        handler (this:Widget).unbuild() set_ptr {
            if this.onChangedId != -1 unregisterElementEventHandler(this.onChangedId);
            this.onChangedId = -1;

            deleteElement(this.id);
            this.unbuildCommon();
            this.id = String();
        }

        handler (this:Widget).update() set_ptr {
            if this.id.getLength() > 0 {
                this.updateCommon();
                setElementAttribute(this.id, "type", "radio");
                setElementAttribute(this.id, "value", this.value);
                setElementAttribute(this.id, "name", this.name);
                if this._checked {
                    setElementAttribute(this.id, "checked", "true");
                } else {
                    removeElementAttribute(this.id, "checked");
                }
                if this.disabled == true {
                    setElementAttribute(this.id, "disabled", "true");
                } else {
                    removeElementAttribute(this.id, "disabled");
                }
            }
        }

        //-----------------
        // Shared Functions

        handler this_type(): SrdRef[RadioButton] {
            def r: SrdRef[RadioButton];
            r.alloc()~init();
            return r;
        }

        handler this_type(n: String, v: String): SrdRef[RadioButton] {
            def r: SrdRef[RadioButton];
            r.alloc()~init(n, v);
            return r;
        }

        handler this_type(n: String, v: String, c: Bool): SrdRef[RadioButton] {
            def r: SrdRef[RadioButton];
            r.alloc()~init(n, v, c);
            return r;
        }
    }
}
